			  Z80 ASSEMBLER - ZASM VER 1.6
  0003                	GETL		EQU		0003H
  0006                	LETLN		EQU		0006H
  0009                	NEWLIN		EQU		0009H
  000C                	PRNTS		EQU		000CH
  0015                	MSGPR		EQU		0015H
  0018                	PLIST		EQU		0018H
  001B                	GETKEY		EQU		001BH
  03BA                	PRTWRD		EQU		03BAH
  03C3                	PRTBYT		EQU		03C3H
  0410                	HLHEX		EQU		0410H
  041F                	TWOHEX		EQU		041FH
  046C                	WRMSG		EQU		046CH
  0970                	DISPLAY		EQU		0970H
  0BB9                	ADCN		EQU		0BB9H
  0DDC                	DPCT		EQU		0DDCH
  10F0                	IBUFE		EQU		10F0H
  10F1                	FNAME		EQU		10F1H
  1102                	EADRS		EQU		1102H
  1102                	FSIZE		EQU		1102H
  1104                	SADRS		EQU		1104H
  1106                	EXEAD		EQU		1106H
  1171                	DSPX		EQU		1171H
  11A3                	LBUF		EQU		11A3H
  11AE                	MBUF		EQU		11AEH
  0082                	MONITOR_80K	EQU		0082H
  00AD                	MONITOR_700	EQU		00ADH
                      	
                      	;0D8H PORTA 送信データ(下位4ビット)
                      	;0D9H PORTB 受信データ(8ビット)
                      	;
                      	;0DAH PORTC Bit
                      	;7 IN  CHK
                      	;6 IN
                      	;5 IN
                      	;4 IN 
                      	;3 OUT
                      	;2 OUT FLG
                      	;1 OUT
                      	;0 OUT
                      	;
                      	;0DBH コントロールレジスタ
                      	
                      	
  F000                	       ORG		0F000H
                      	
  F000  00            			NOP                   ;ROM識別コード
  F001  C313F0        			JP		START
                      	;******************** MONITOR CMTルーチン代替 *************************************
  F004  C3C6F6        			JP		MSHED
  F007  C3F1F6        			JP		MSDAT
  F00A  C320F7        			JP		MLHED
  F00D  C393F7        			JP		MLDAT
  F010  C3C1F7        			JP		MVRFY
                      	
                      			
  F013  CD74F0        	START:	CALL	INIT
  F016  11A311        			LD		DE,LBUF     ;MZ-80K、MZ-700とも起動コマンドは'*FD'に統一
  F019  1A            			LD		A,(DE)
  F01A  FE2A          			CP		'*'
  F01C  C27BF1        			JP		NZ,MON
  F01F  13            			INC 	DE
  F020  1A            			LD		A,(DE)
  F021  FE46          			CP		'F'
  F023  C27BF1        			JP		NZ,MON
  F026  13            			INC		DE
  F027  1A            			LD		A,(DE)
  F028  FE44          			CP		'D'
  F02A  C27BF1        			JP		NZ,MON
                      			
  F02D  13            			INC		DE          ;FDの次の文字へ移動
  F02E  1A            	STT2:	LD		A,(DE)
  F02F  FE20          			CP		20H         ;FDの後に1文字空白があれば以降をファイルネームとしてロード(ファイルネームは32文字まで)
  F031  284C          			JR		Z,SDLOAD
  F033  FE0D          			CP		0DH         ;FDだけで改行の場合にはDEFNAMEの文字列をファイルネームとしてロード
  F035  200D          			JR		NZ,STETC    ;該当なしなら他コマンドをチェック
  F037  D5            	STT3:	PUSH	DE          ;設定ファイル名(0000.mzt)を転送
  F038  21BCF6        			LD		HL,DEFNAME
  F03B  13            			INC		DE
  F03C  010500        			LD		BC,NEND-DEFNAME
  F03F  EDB0          			LDIR
  F041  D1            			POP		DE
  F042  183B          			JR		SDLOAD      ;LOAD処理へ
  F044                	STETC:
  F044  FE53          			CP		'S'         ;FDS:SAVE処理へ
  F046  CAE1F0        			JP		Z,STSV
  F049  FE41          			CP		'A'			;FDA:自動起動ファイル設定処理へ
  F04B  CAECF1        			JP		Z,STAS
  F04E  FE4C          			CP		'L'         ;FDL:ファイル一覧表示
  F050  CAF7F1        			JP		Z,STLT
  F053  FE44          			CP		'D'         ;FDD:DELETE
  F055  CA86F2        			JP		Z,STDE
  F058  FE52          			CP		'R'         ;FDR:RENAME
  F05A  CAC3F2        			JP		Z,STRN
  F05D  FE50          			CP		'P'         ;FDP:DUMP
  F05F  CAF0F2        			JP		Z,STPR
  F062  FE43          			CP		'C'         ;FDC:COPY
  F064  CA94F3        			JP		Z,STCP
  F067  FE4D          			CP		'M'         ;FDM:MEMORY DUMP
  F069  CAC1F3        			JP		Z,STMD
  F06C  FE57          			CP		'W'         ;FDW:MEMORY WRITE
  F06E  CA40F4        			JP		Z,STMW
  F071  C330F1        			JP		CMDERR
                      	
                      	;**** 8255初期化 ****
                      	;PORTC下位BITをOUTPUT、上位BITをINPUT、PORTBをINPUT、PORTAをOUTPUT
  F074  3E8A          	INIT:	LD		A,8AH
  F076  D3DB          			OUT		(0DBH),A
                      	;出力BITをリセット
  F078  3E00          	INIT2:	LD		A,00H      ;PORTA <- 0
  F07A  D3D8          			OUT		(0D8H),A
  F07C  D3DA          			OUT		(0DAH),A   ;PORTC <- 0
  F07E  C9            			RET
                      	
                      	;**** LOAD ****
                      	;受信ヘッダ情報をセットし、SDカードからLOAD実行
  F07F  3E81          	SDLOAD:	LD		A,81H  ;LOADコマンド81H
  F081  CD04F5        			CALL	STCMD
  F084  CD8EF0        			CALL	HDRCV      ;ヘッダ情報受信
  F087  CDCFF0        			CALL	DBRCV      ;データ受信
  F08A  2A0611        			LD		HL,(EXEAD)
  F08D  E9            			JP		(HL)
                      	
                      	;ヘッダ受信
  F08E  21F110        	HDRCV:	LD		HL,FNAME
  F091  0611          			LD		B,11H
  F093  CD87F4        	HDRC1:	CALL	RCVBYTE    ;ファイルネーム受信
  F096  77            			LD		(HL),A
  F097  23            			INC		HL
  F098  05            			DEC		B
  F099  20F8          			JR		NZ,HDRC1
  F09B  1110F5        			LD		DE,MSG_LD  ;ファイルネームLOADING表示
  F09E  CD1500        			CALL	MSGPR
  F0A1  11F110        			LD		DE,FNAME
  F0A4  CD1500        			CALL	MSGPR
  F0A7  CD0600        			CALL	LETLN
  F0AA  210411        			LD		HL,SADRS  ;SADRS取得
  F0AD  CD87F4        			CALL	RCVBYTE
  F0B0  77            			LD		(HL),A
  F0B1  23            			INC		HL
  F0B2  CD87F4        			CALL	RCVBYTE
  F0B5  77            			LD		(HL),A
  F0B6  210211        			LD		HL,FSIZE   ;FSIZE取得
  F0B9  CD87F4        			CALL	RCVBYTE
  F0BC  77            			LD		(HL),A
  F0BD  23            			INC		HL
  F0BE  CD87F4        			CALL	RCVBYTE
  F0C1  77            			LD		(HL),A
  F0C2  210611        			LD		HL,EXEAD   ;EXEAD取得
  F0C5  CD87F4        			CALL	RCVBYTE
  F0C8  77            			LD		(HL),A
  F0C9  23            			INC		HL
  F0CA  CD87F4        			CALL	RCVBYTE
  F0CD  77            			LD		(HL),A
  F0CE  C9            			RET
                      	
                      	;データ受信
  F0CF  ED5B0211      	DBRCV:	LD		DE,(FSIZE)
  F0D3  2A0411        			LD		HL,(SADRS)
  F0D6  CD87F4        	DBRLOP:	CALL	RCVBYTE
  F0D9  77            			LD		(HL),A
  F0DA  1B            			DEC		DE
  F0DB  7A            			LD		A,D
  F0DC  B3            			OR		E
  F0DD  23            			INC		HL
  F0DE  20F6          			JR		NZ,DBRLOP   ;DE=0までLOOP
  F0E0  C9            			RET
                      	
                      	;**** SAVE ****
  F0E1  13            	STSV:	INC		DE
  F0E2  13            			INC		DE
  F0E3  D5            			PUSH	DE
  F0E4  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればSADRSにセットして続行
  F0E7  383D          			JR		C,STSV1
                      	
  F0E9  220411        			LD		(SADRS),HL      ;SARDS保存
  F0EC  D1            			POP		DE
  F0ED  13            			INC		DE
  F0EE  13            			INC		DE
  F0EF  13            			INC		DE
  F0F0  13            			INC		DE
  F0F1  13            			INC		DE
  F0F2  D5            			PUSH	DE          ;5文字進めて4桁の16進数であればEADRSにセットして続行
  F0F3  CD1004        			CALL	HLHEX
  F0F6  382E          			JR		C,STSV1
  F0F8  E5            			PUSH	HL
  F0F9  ED4B0411      			LD		BC,(SADRS)
  F0FD  ED42          			SBC		HL,BC       ;EADRSがSADRSより大きくない場合はエラー
  F0FF  E1            			POP		HL
  F100  2824          			JR		Z,STSV1
  F102  FA26F1        			JP		M,STSV1
                      	
  F105  220211        			LD		(EADRS),HL      ;EADRS保存
  F108  D1            			POP		DE
  F109  13            			INC		DE
  F10A  13            			INC		DE
  F10B  13            			INC		DE
  F10C  13            			INC		DE
  F10D  13            			INC		DE          ;5文字進めて4桁の16進数であればEXEADにセットして続行
  F10E  D5            			PUSH	DE
  F10F  CD1004        			CALL	HLHEX
  F112  3812          			JR		C,STSV1
                      			
  F114  220611        			LD		(EXEAD),HL      ;EXEAD保存
  F117  D1            			POP		DE
  F118  13            			INC		DE
  F119  13            			INC		DE
  F11A  13            			INC		DE
  F11B  13            			INC		DE
  F11C  13            			INC		DE			;5文字進めてファイルネームがあれば続行
  F11D  1A            			LD		A,(DE)
  F11E  FE31          			CP		31H
  F120  FA2BF1        			JP		M,STSV2
  F123  EB            			EX		DE,HL
  F124  180F          			JR		SDSAVE      ;SAVE処理へ
  F126                	STSV1:                      ;16進数4桁の取得に失敗又はEADRSがSARDSより大きくない
  F126  113AF5        			LD		DE,MSG_AD
  F129  184A          			JR		ERRMSG
  F12B                	STSV2:                      ;ファイルネームの取得に失敗
  F12B  114AF5        			LD		DE,MSG_FNAME
  F12E  1845          			JR		ERRMSG
  F130                	CMDERR:                     ;コマンド異常
  F130  115CF5        			LD		DE,MSG_CMD
  F133  1840          			JR		ERRMSG
                      	
                      	;送信ヘッダ情報をセットし、SDカードへSAVE実行
  F135  3E80          	SDSAVE:	LD		A,80H      ;SAVEコマンド80H
  F137  CDE2F4        			CALL	STCD
  F13A  CD90F1        			CALL	HDSEND     ;ヘッダ情報送信
  F13D  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F140  A7            			AND		A          ;00以外ならERROR
  F141  2008          			JR		NZ,SVERR
  F143  CDD5F1        			CALL	DBSEND     ;データ送信
  F146  111AF5        			LD		DE,MSG_SV
  F149  182A          			JR		ERRMSG
                      	
  F14B                	SVERR:
  F14B  FEF0          			CP		0F0H
  F14D  2005          			JR		NZ,ERR2
  F14F  116CF5        			LD		DE,MSG_F0  ;SD-CARD INITIALIZE ERROR
  F152  1821          			JR		ERRMSG
  F154  FEF2          	ERR2:	CP		0F2H
  F156  2005          			JR		NZ,ERR3
  F158  1193F5        			LD		DE,MSG_F2  ;NOT OBJECT FILE
  F15B  1818          			JR		ERRMSG
  F15D  FEF1          	ERR3:	CP		0F1H
  F15F  2005          			JR		NZ,ERR4
  F161  1185F5        			LD		DE,MSG_F1  ;NOT FIND FILE
  F164  180F          			JR		ERRMSG
  F166  FEF3          	ERR4:	CP		0F3H
  F168  2005          			JR		NZ,ERR99
  F16A  11A3F5        			LD		DE,MSG_F3  ;FILE EXIST
  F16D  1806          			JR		ERRMSG
  F16F  CDC303        	ERR99:	CALL	PRTBYT
  F172  11B5F6        			LD		DE,MSG99   ;その他ERROR
  F175  CD1500        	ERRMSG:	CALL	MSGPR
  F178  CD0600        			CALL	LETLN
  F17B  214E01        	MON:	LD		HL,014EH
  F17E  7E            			LD		A,(HL)
  F17F  FE50          			CP		'P'             ;014EHが'P'ならMZ-80K
  F181  CA8200        			JP		Z,MONITOR_80K
  F184  21EB06        			LD		HL,06EBH
  F187  7E            			LD		A,(HL)
  F188  FE4D          			CP		'M'             ;06EBHが'M'ならMZ-700
  F18A  CAAD00        			JP		Z,MONITOR_700
  F18D  C30000        			JP		0000H           ;識別できなかったら0000Hへジャンプ
                      	
                      	;ヘッダ送信
  F190  E5            	HDSEND:	PUSH	HL
  F191  0620          			LD		B,20H
  F193  7E            	SS1:	LD		A,(HL)     ;FNAME送信
  F194  CD9AF4        			CALL	SNDBYTE
  F197  23            			INC		HL
  F198  05            			DEC		B
  F199  20F8          			JR		NZ,SS1
  F19B  3E0D          			LD		A,0DH
  F19D  CD9AF4        			CALL	SNDBYTE
  F1A0  E1            			POP		HL
  F1A1  0610          			LD		B,10H
  F1A3  7E            	SS2:	LD		A,(HL)     ;PNAME送信
  F1A4  CD9AF4        			CALL	SNDBYTE
  F1A7  23            			INC		HL
  F1A8  05            			DEC		B
  F1A9  20F8          			JR		NZ,SS2
  F1AB  3E0D          			LD		A,0DH
  F1AD  CD9AF4        			CALL	SNDBYTE
  F1B0  210411        			LD		HL,SADRS   ;SADRS送信
  F1B3  7E            			LD		A,(HL)
  F1B4  CD9AF4        			CALL	SNDBYTE
  F1B7  23            			INC		HL
  F1B8  7E            			LD		A,(HL)
  F1B9  CD9AF4        			CALL	SNDBYTE
  F1BC  210211        			LD		HL,EADRS   ;EADRS送信
  F1BF  7E            			LD		A,(HL)
  F1C0  CD9AF4        			CALL	SNDBYTE
  F1C3  23            			INC		HL
  F1C4  7E            			LD		A,(HL)
  F1C5  CD9AF4        			CALL	SNDBYTE
  F1C8  210611        			LD		HL,EXEAD   ;EXEAD送信
  F1CB  7E            			LD		A,(HL)
  F1CC  CD9AF4        			CALL	SNDBYTE
  F1CF  23            			INC		HL
  F1D0  7E            			LD		A,(HL)
  F1D1  CD9AF4        			CALL	SNDBYTE
  F1D4  C9            			RET
                      	
                      	;データ送信
                      	;SADRSからEADRSまでを送信
  F1D5  2A0211        	DBSEND:	LD		HL,(EADRS)
  F1D8  EB            			EX		DE,HL
  F1D9  2A0411        			LD		HL,(SADRS)
  F1DC  7E            	DBSLOP:	LD		A,(HL)
  F1DD  CD9AF4        			CALL	SNDBYTE
  F1E0  7C            			LD		A,H
  F1E1  BA            			CP		D
  F1E2  2004          			JR		NZ,DBSLP1
  F1E4  7D            			LD		A,L
  F1E5  BB            			CP		E
  F1E6  2803          			JR		Z,DBSLP2   ;HL = DE までLOOP
  F1E8  23            	DBSLP1:	INC		HL
  F1E9  18F1          			JR		DBSLOP
  F1EB  C9            	DBSLP2:	RET
                      	
                      	;**** AUTO START SET ****
  F1EC  3E82          	STAS:	LD		A,82H      ;AUTO START SETコマンド82H
  F1EE  CD04F5        			CALL	STCMD
  F1F1  1129F5        			LD		DE,MSG_AS
  F1F4  C375F1        			JP		ERRMSG
                      	
                      	;**** DIRLIST ****
  F1F7  13            	STLT:	INC		DE
  F1F8  1A            			LD		A,(DE)
  F1F9  FE0D          			CP		0DH
  F1FB  2004          			JR		NZ,STLT1
  F1FD  3E30          			LD		A,30H
  F1FF  180C          			JR		DIRLIST    ;FDLだけなら'30H'を送信(全ファイルを表示)
  F201  13            	STLT1:	INC		DE         ;FDLの後に数字一文字があれば'31H'〜'39H'を送信(20件を1ページとしてnページを表示)
  F202  1A            			LD		A,(DE)
  F203  FE30          			CP		30H
  F205  FA30F1        			JP		M,CMDERR
  F208  FE39          			CP		39H
  F20A  F230F1        			JP		P,CMDERR
  F20D                	DIRLIST:
  F20D  F5            			PUSH	AF
  F20E  3E83          			LD		A,83H      ;DIRLISTコマンド83Hを送信
  F210  CDE2F4        			CALL	STCD       ;コマンドコード送信
  F213  F1            			POP		AF
  F214  CD9AF4        			CALL	SNDBYTE           ;ページ指示を送信
  F217  21C1F6        	DL1:	LD		HL,DEFDIR         ;行頭に'*FD 'を付けることでカーソルを移動させリターンで実行できるように
  F21A  11A311        			LD		DE,LBUF
  F21D  010500        			LD		BC,DEND-DEFDIR
  F220  EDB0          			LDIR
  F222  EB            			EX		DE,HL
  F223  CD87F4        	DL2:	CALL	RCVBYTE           ;'00H'を受信するまでを一行とする
  F226  FE00          			CP		00H
  F228  280C          			JR		Z,DL3
  F22A  FEFF          			CP		0FFH              ;'0FFH'を受信したら終了
  F22C  2813          			JR		Z,DL4
  F22E  FEFE          			CP		0FEH              ;'0FEH'を受信したら一時停止して一文字入力待ち
  F230  2819          			JR		Z,DL5
  F232  77            			LD		(HL),A
  F233  23            			INC		HL
  F234  18ED          			JR		DL2
  F236  11A311        	DL3:	LD		DE,LBUF           ;'00H'を受信したら一行分を表示して改行
  F239  CD1500        			CALL	MSGPR
  F23C  CD0600        			CALL	LETLN
  F23F  18D6          			JR		DL1
  F241  CD87F4        	DL4:	CALL	RCVBYTE           ;状態取得(00H=OK)
  F244  A7            			AND		A                 ;00以外ならERROR
  F245  C24BF1        			JP		NZ,SVERR
  F248  C37BF1        			JP		MON
                      	
  F24B  11AEF5        	DL5:	LD		DE,MSG_KEY1        ;HIT ANT KEY表示
  F24E  CD1500        			CALL	MSGPR
  F251  3EC2          			LD		A,0C2H
  F253  CD7009        			CALL	DISPLAY
  F256  11C1F5        			LD		DE,MSG_KEY2        ;HIT ANT KEY表示
  F259  CD1500        			CALL	MSGPR
  F25C  CD0600        			CALL	LETLN
  F25F  CD1B00        	DL6:	CALL	GETKEY            ;1文字入力待ち
  F262  FE00          			CP		00H
  F264  28F9          			JR		Z,DL6
  F266  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F268  2815          			JR		Z,DL7
  F26A  FE12          			CP		12H               ;カーソル↑で打ち切り
  F26C  2807          			JR		Z,DL9
  F26E  CD0600        			CALL	LETLN             ;それ以外で継続
  F271  3E00          			LD		A,00H
  F273  180C          			JR		DL8
  F275  3EC2          	DL9:	LD		A,0C2H            ;カーソル↑で打ち切ったときにカーソル2行上へ
  F277  CDDC0D        			CALL	DPCT
  F27A  3EC2          			LD		A,0C2H
  F27C  CDDC0D        			CALL	DPCT
  F27F  3EFF          	DL7:	LD		A,0FFH            ;0FFH中断コードを送信
  F281  CD9AF4        	DL8:	CALL	SNDBYTE
  F284  189D          			JR		DL2
                      	
                      	;**** FILE DELETE ****
  F286  3E84          	STDE:	LD		A,84H      ;FILE DELETEコマンド84H
  F288  CD04F5        			CALL	STCMD
                      	
  F28B  11D2F5        			LD		DE,MSG_DELQ ;'DELETE?'表示
  F28E  CD1500        			CALL	MSGPR
  F291  CD0600        			CALL	LETLN
  F294  CD1B00        	STDE3:	CALL	GETKEY
  F297  FE00          			CP		00H
  F299  28F9          			JR		Z,STDE3
  F29B  FE59          			CP		59H         ;'Y'ならOKとして00Hを送信
  F29D  2004          			JR		NZ,STDE4
  F29F  3E00          			LD		A,00H
  F2A1  1802          			JR		STDE5
  F2A3  3EFF          	STDE4:	LD		A,0FFH      ;'Y'以外ならCANSELとして0FFHを送信
  F2A5  CD9AF4        	STDE5:	CALL	SNDBYTE
  F2A8  CD87F4        			CALL	RCVBYTE
  F2AB  FE00          			CP		00H         ;00Hを受信すればDELETE完了
  F2AD  2005          			JR		NZ,STDE6
  F2AF  11F1F5        			LD		DE,MSG_DELY ;'DELETE OK'表示
  F2B2  180C          			JR		STDE8
  F2B4  FE01          	STDE6:	CP		01H         ;01Hを受信すればCANSEL完了
  F2B6  2005          			JR		NZ,STDE7
  F2B8  11FBF5        			LD		DE,MSG_DELN ;'DELETE CANSEL'表示
  F2BB  1803          			JR		STDE8
  F2BD  C34BF1        	STDE7:	JP		SVERR
  F2C0  C375F1        	STDE8:	JP		ERRMSG
                      	
                      	;**** FILE RENAME ****
  F2C3  3E85          	STRN:	LD		A,85H      ;FILE RENAMEコマンド85H
  F2C5  CD04F5        			CALL	STCMD
                      	
  F2C8  1109F6        			LD		DE,MSG_REN ;'NEW NAME:'表示
  F2CB  CD1500        			CALL	MSGPR
                      			
  F2CE  3E09          			LD		A,09H
  F2D0  327111        			LD		(DSPX),A  ;カーソル位置を'NEW NAME:'の次へ
  F2D3  11A311        			LD		DE,LBUF    ;NEW FILE NAMEを取得
  F2D6  CD0300        			CALL	GETL
  F2D9  11AB11        			LD		DE,LBUF+8  ;NEW FILE NAMEを送信
  F2DC  CDD3F4        			CALL	STFN
  F2DF  CDEDF4        			CALL	STFS
                      			
  F2E2  CD87F4        			CALL	RCVBYTE
  F2E5  FE00          			CP		00H         ;00Hを受信すればRENAME完了
  F2E7  C24BF1        			JP		NZ,SVERR
  F2EA  1155F6        			LD		DE,MSG_RENY
  F2ED  C375F1        			JP		ERRMSG
                      	
                      	;**** FILE DUMP ****
  F2F0  3E86          	STPR:	LD		A,86H      ;FILE DUMPEコマンド86H
  F2F2  CD04F5        			CALL	STCMD
                      	
  F2F5  3EC6          			LD		A,0C6H     ;画面クリア
  F2F7  CDDC0D        			CALL	DPCT
  F2FA  210411        	STPR6:	LD		HL,SADRS   ;SADRS取得
  F2FD  CD87F4        			CALL	RCVBYTE
  F300  77            			LD		(HL),A
  F301  23            			INC		HL
  F302  CD87F4        			CALL	RCVBYTE
  F305  77            			LD		(HL),A
  F306  2A0411        			LD		HL,(SADRS)
  F309  7C            			LD		A,H
  F30A  FEFF          			CP		0FFH        ;ADRSに0FFFFHが送信されてきたらDUMP処理終了
  F30C  2008          			JR		NZ,STPR7
  F30E  7D            			LD		A,L
  F30F  FEFF          			CP		0FFH
  F311  2003          			JR		NZ,STPR7
  F313  C38EF3        			JP		STPR8
  F316  115FF6        	STPR7:	LD		DE,MSG_AD1 ;DUMP TITLE表示
  F319  CD1500        			CALL	MSGPR
  F31C  CD0600        			CALL	LETLN
  F31F  0E10          			LD		C,10H      ;16行(128Byte)を表示
  F321  C5            	STPR0:	PUSH	BC
  F322  0608          			LD		B,08H      ;一行(8Byte)を受信
  F324  21A311        			LD		HL,LBUF
  F327  CD87F4        	STPR1:	CALL	RCVBYTE
  F32A  77            			LD		(HL),A
  F32B  05            			DEC		B
  F32C  23            			INC		HL
  F32D  20F8          			JR		NZ,STPR1
                      	
  F32F  2A0411        			LD		HL,(SADRS) ;アドレス表示
  F332  CDBA03        			CALL	PRTWRD
  F335  110800        			LD		DE,0008H   ;一画面(128Byte)中はアドレスを受け取らないので自前でインクリメント
  F338  19            			ADD		HL,DE
  F339  220411        			LD		(SADRS),HL
                      			
  F33C  0608          			LD		B,08H      ;一行(8Byte)のデータを16進数表示
  F33E  11A311        			LD		DE,LBUF
  F341  CD0C00        	STPR2:	CALL	PRNTS
  F344  1A            			LD		A,(DE)
  F345  CDC303        			CALL	PRTBYT
  F348  13            			INC		DE
  F349  05            			DEC		B
  F34A  20F5          			JR		NZ,STPR2
                      			
  F34C  CD0C00        			CALL	PRNTS
  F34F  11A311        			LD		DE,LBUF
  F352  0608          			LD		B,08H
  F354  1A            	STPR9:	LD		A,(DE)
  F355  CDB90B        			CALL	ADCN
  F358  CD7009        			CALL	DISPLAY
  F35B  13            			INC		DE
  F35C  05            			DEC		B
  F35D  20F5          			JR		NZ,STPR9
                      	
  F35F  CD0600        			CALL	LETLN
  F362  C1            			POP		BC
  F363  0D            			DEC		C
  F364  20BB          			JR		NZ,STPR0
                      			
  F366  1185F6        			LD		DE,MSG_AD2        ;入力待ちメッセージ表示
  F369  CD1500        			CALL	MSGPR
  F36C  CD0600        			CALL	LETLN
  F36F  CD0600        			CALL	LETLN
  F372  CD1B00        	STPR3:	CALL	GETKEY            ;1文字入力待ち
  F375  FE00          			CP		00H
  F377  28F9          			JR		Z,STPR3
  F379  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F37B  2806          			JR		Z,STPR4
  F37D  CD9AF4        			CALL	SNDBYTE           ;SHIFT+BREAK以外はASCIIコードのまま送信、ARDUINO側で'B'を処理
  F380  C3FAF2        			JP		STPR6
  F383  3EFF          	STPR4:	LD		A,0FFH            ;0FFH中断コードを送信
  F385  CD9AF4        	STPR5:	CALL	SNDBYTE
  F388  CD87F4        			CALL	RCVBYTE           ;SHIFT+BREAKでの中断時はADRS'0FFFFH'の受信及び状態コードの受信を破棄
  F38B  CD87F4        			CALL	RCVBYTE
  F38E  CD87F4        	STPR8:	CALL	RCVBYTE
  F391  C37BF1        			JP		MON
                      	
                      	;**** FILE COPY ****
  F394  3E87          	STCP:	LD		A,87H      ;FILE COPYコマンド87H
  F396  CD04F5        			CALL	STCMD
  F399  1109F6        			LD		DE,MSG_REN ;'NEW NAME:'表示
  F39C  CD1500        			CALL	MSGPR
                      			
  F39F  3E09          			LD		A,09H
  F3A1  327111        			LD		(DSPX),A    ;カーソル位置を'NEW NAME:'の次へ
  F3A4  11A311        			LD		DE,LBUF      ;NEW FILE NAMEを取得
  F3A7  CD0300        			CALL	GETL
  F3AA  11AB11        			LD		DE,LBUF+8    ;NEW FILE NAMEを送信
  F3AD  CDD3F4        			CALL	STFN
  F3B0  CDEDF4        			CALL	STFS
                      			
  F3B3  CD87F4        			CALL	RCVBYTE
  F3B6  FE00          			CP		00H         ;00Hを受信すればRENAME完了
  F3B8  C24BF1        			JP		NZ,SVERR
  F3BB  11A7F6        			LD		DE,MSG_CPY
  F3BE  C375F1        			JP		ERRMSG
                      	
                      	;**** MEMORY DUMP ****
  F3C1  13            	STMD:	INC		DE
  F3C2  13            			INC		DE
  F3C3  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればSADRSにセットして続行
  F3C6  DA26F1        			JP		C,STSV1
  F3C9  220411        			LD		(SADRS),HL      ;SARDS保存
                      	
  F3CC  115FF6        	STMD6:	LD		DE,MSG_AD1 ;DUMP TITLE表示
  F3CF  CD1500        			CALL	MSGPR
  F3D2  CD0600        			CALL	LETLN
  F3D5  0E10          			LD		C,10H      ;16行(128Byte)を表示
  F3D7  2A0411        	STMD7:	LD		HL,(SADRS) ;アドレス表示
  F3DA  CDBA03        			CALL	PRTWRD
  F3DD  CD0C00        			CALL	PRNTS
                      	
                      			
  F3E0  0608          			LD		B,08H
  F3E2  7E            	STMD0:	LD		A,(HL)
  F3E3  CDC303        			CALL	PRTBYT
  F3E6  CD0C00        			CALL	PRNTS
  F3E9  CD1B00        			CALL	GETKEY
  F3EC  FE64          			CP		64H
  F3EE  284D          			JR		Z,STMD4
  F3F0  23            			INC		HL
  F3F1  05            			DEC		B
  F3F2  20EE          			JR		NZ,STMD0
                      	
  F3F4  2A0411        			LD		HL,(SADRS)
  F3F7  0608          			LD		B,08H
  F3F9  7E            	STMD2:	LD		A,(HL)
  F3FA  CDB90B        			CALL	ADCN
  F3FD  CD7009        			CALL	DISPLAY
  F400  CD1B00        			CALL	GETKEY
  F403  FE64          			CP		64H
  F405  2836          			JR		Z,STMD4
  F407  23            			INC		HL
  F408  05            			DEC		B
  F409  20EE          			JR		NZ,STMD2
                      	
  F40B  220411        			LD		(SADRS),HL
  F40E  CD0600        			CALL	LETLN
                      	
  F411  0D            			DEC		C
  F412  20C3          			JR		NZ,STMD7
                      			
  F414  1185F6        			LD		DE,MSG_AD2        ;入力待ちメッセージ表示
  F417  CD1500        			CALL	MSGPR
  F41A  CD0600        			CALL	LETLN
  F41D  CD0600        			CALL	LETLN
  F420  CD1B00        	STMD3:	CALL	GETKEY            ;1文字入力待ち
  F423  FE00          			CP		00H
  F425  28F9          			JR		Z,STMD3
  F427  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F429  2812          			JR		Z,STMD4
  F42B  FE42          			CP		42H
  F42D  200B          			JR		NZ,STMD5
  F42F  2A0411        			LD		HL,(SADRS)
  F432  110001        			LD		DE,0100H
  F435  ED52          			SBC		HL,DE
  F437  220411        			LD		(SADRS),HL
  F43A  C3CCF3        	STMD5:	JP		STMD6
  F43D  C37BF1        	STMD4:	JP		MON
                      	
                      	;**** MEMORY WRITE ****
  F440  13            	STMW:	INC		DE
  F441  13            			INC		DE
  F442  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればHLにセットして続行
  F445  DA26F1        			JP		C,STSV1
                      	
  F448  13            			INC		DE
  F449  13            			INC		DE
  F44A  13            			INC		DE
  F44B  13            			INC		DE
  F44C  1A            			LD		A,(DE)
  F44D  FE0D          			CP		0DH
  F44F  2833          			JR		Z,STMW9     ;アドレスのみなら終了
  F451  CDCAF4        			CALL	STSP        ;空白は飛ばす
  F454                	STMW1:
                      	
  F454  CD1F04        			CALL	TWOHEX
  F457  380C          			JR		C,STMW8
  F459  77            			LD		(HL),A      ;2桁の16進数があれば(HL)に書き込み
  F45A  23            			INC		HL
  F45B  CDCAF4        			CALL	STSP        ;空白は飛ばす
  F45E  1A            			LD		A,(DE)
  F45F  FE0D          			CP		0DH         ;一行終了
  F461  2802          			JR		Z,STMW8
  F463  18EF          			JR		STMW1
                      			
  F465                	STMW8:	
  F465  11AFF6        			LD		DE,MSG_FDW  ;行頭に'*FDW '
  F468  CD1500        			CALL	MSGPR
  F46B  CDBA03        			CALL	PRTWRD      ;アドレス表示
  F46E  CD0C00        			CALL	PRNTS
  F471  11A311        			LD		DE,LBUF     ;一行入力
  F474  CD0300        			CALL	GETL
  F477  11A311        			LD		DE,LBUF
  F47A  1A            			LD		A,(DE)
  F47B  FE1B          			CP		1BH
  F47D  2805          			JR		Z,STMW9     ;SHIFT+BREAKで破棄、終了
  F47F  11A611        			LD		DE,LBUF+3
  F482  18BC          			JR		STMW
  F484  C37BF1        	STMW9:	JP		MON
                      	
                      	;**** 1BYTE受信 ****
                      	;受信DATAをAレジスタにセットしてリターン
  F487                	RCVBYTE:
  F487  CDBCF4        			CALL	F1CHK      ;PORTC BIT7が1になるまでLOOP
  F48A  3E05          			LD		A,05H
  F48C  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 1
  F48E  DBD9          			IN		A,(0D9h)   ;PORTB -> A
  F490  F5            			PUSH 	AF
  F491  CDC3F4        			CALL	F2CHK      ;PORTC BIT7が0になるまでLOOP
  F494  3E04          			LD		A,04H
  F496  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 0
  F498  F1            			POP 	AF
  F499  C9            			RET
                      			
                      	;**** 1BYTE送信 ****
                      	;Aレジスタの内容をPORTA下位4BITに4BITずつ送信
  F49A                	SNDBYTE:
  F49A  F5            			PUSH	AF
  F49B  1F            			RRA
  F49C  1F            			RRA
  F49D  1F            			RRA
  F49E  1F            			RRA
  F49F  E60F          			AND		0FH
  F4A1  CDABF4        			CALL	SND4BIT
  F4A4  F1            			POP		AF
  F4A5  E60F          			AND		0FH
  F4A7  CDABF4        			CALL	SND4BIT
  F4AA  C9            			RET
                      	
                      	;**** 4BIT送信 ****
                      	;Aレジスタ下位4ビットを送信する
  F4AB                	SND4BIT:
  F4AB  D3D8          			OUT		(0D8H),A
  F4AD  3E05          			LD		A,05H
  F4AF  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 1
  F4B1  CDBCF4        			CALL	F1CHK      ;PORTC BIT7が1になるまでLOOP
  F4B4  3E04          			LD		A,04H
  F4B6  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 0
  F4B8  CDC3F4        			CALL	F2CHK
  F4BB  C9            			RET
                      			
                      	;**** BUSYをCHECK(1) ****
                      	; 82H BIT7が1になるまでLOP
  F4BC  DBDA          	F1CHK:	IN		A,(0DAH)
  F4BE  E680          			AND		80H        ;PORTC BIT7 = 1?
  F4C0  28FA          			JR		Z,F1CHK
  F4C2  C9            			RET
                      	
                      	;**** BUSYをCHECK(0) ****
                      	; 82H BIT7が0になるまでLOOP
  F4C3  DBDA          	F2CHK:	IN		A,(0DAH)
  F4C5  E680          			AND		80H        ;PORTC BIT7 = 0?
  F4C7  20FA          			JR		NZ,F2CHK
  F4C9  C9            			RET
                      	
                      	;****** 空白読み飛ばし *********
  F4CA  1A            	STSP:	LD		A,(DE)
  F4CB  FE20          			CP		20H
  F4CD  2003          			JR		NZ,STSP1
  F4CF  13            			INC		DE
  F4D0  18F8          			JR		STSP
  F4D2  C9            	STSP1:	RET
                      	
                      	;****** FILE NAME 取得 (IN:DE コマンド文字の次の文字 OUT:HL ファイルネームの先頭)*********
  F4D3  F5            	STFN:	PUSH	AF
  F4D4  13            	STFN1:	INC		DE         ;ファイルネームまでスペース読み飛ばし
  F4D5  1A            			LD		A,(DE)
  F4D6  FE20          			CP		20H
  F4D8  28FA          			JR		Z,STFN1
  F4DA  FE30          			CP		30H        ;「0」以上の文字でなければエラーとする
  F4DC  FA2BF1        			JP		M,STSV2
  F4DF  EB            			EX		DE,HL
  F4E0  F1            			POP		AF
  F4E1  C9            			RET
                      	
                      	;**** コマンド送信 (IN:A コマンドコード)****
  F4E2  CD9AF4        	STCD:	CALL	SNDBYTE    ;Aレジスタのコマンドコードを送信
  F4E5  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F4E8  A7            			AND		A          ;00以外ならERROR
  F4E9  C24BF1        			JP		NZ,SVERR
  F4EC  C9            			RET
                      	
                      	;**** ファイルネーム送信(IN:HL ファイルネームの先頭) ******
  F4ED  0620          	STFS:	LD		B,20H
  F4EF  7E            	STFS1:	LD		A,(HL)     ;FNAME送信
  F4F0  CD9AF4        			CALL	SNDBYTE
  F4F3  23            			INC		HL
  F4F4  05            			DEC		B
  F4F5  20F8          			JR		NZ,STFS1
  F4F7  3E0D          			LD		A,0DH
  F4F9  CD9AF4        			CALL	SNDBYTE
  F4FC  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F4FF  A7            			AND		A          ;00以外ならERROR
  F500  C24BF1        			JP		NZ,SVERR
  F503  C9            			RET
                      	
                      	;**** コマンド、ファイル名送信 (IN:A コマンドコード HL:ファイルネームの先頭)****
  F504  CDD3F4        	STCMD:	CALL	STFN       ;ファイルネーム取得
  F507  E5            			PUSH	HL
  F508  CDE2F4        			CALL	STCD       ;コマンドコード送信
  F50B  E1            			POP		HL
  F50C  CDEDF4        			CALL	STFS       ;ファイルネーム送信
  F50F  C9            			RET
                      	
                      	;******** MESSAGE DATA ********************
  F510                	MSG_LD:
  F510  16            			DB		16H
  F511  4C4F4144494E47			DB		'LOADING '
  F519  0D            			DB		0DH
                      	
  F51A                	MSG_SV:
  F51A  53415645204649			DB		'SAVE FINISHED!'
  F528  0D            			DB		0DH
                      			
  F529                	MSG_AS:
  F529  41535441525420			DB		'ASTART FINISHED!'
  F539  0D            			DB		0DH
                      			
  F53A                	MSG_AD:
  F53A  41444452455353			DB		'ADDRESS FAILED!'
  F549  0D            			DB		0DH
                      			
  F54A                	MSG_FNAME:
  F54A  46494C45204E41			DB		'FILE NAME FAILED!'
  F55B  0D            			DB		0DH
                      			
  F55C                	MSG_CMD:
  F55C  434F4D4D414E44			DB		'COMMAND FAILED!'
  F56B  0D            			DB		0DH
                      			
  F56C                	MSG_F0:
  F56C  53442D43415244			DB		'SD-CARD INITIALIZE ERROR'
  F584  0D            			DB		0DH
                      			
  F585                	MSG_F1:
  F585  4E4F542046494E			DB		'NOT FIND FILE'
  F592  0D            			DB		0DH
                      			
  F593                	MSG_F2:
  F593  4E4F54204F424A			DB		'NOT OBJECT FILE'
  F5A2  0D            			DB		0DH
                      			
  F5A3                	MSG_F3:
  F5A3  46494C45204558			DB		'FILE EXIST'
  F5AD  0D            			DB		0DH
                      			
  F5AE                	MSG_KEY1:
  F5AE  48495420414E59			DB		'HIT ANY KEY(BREAK:'
  F5C0  0D            			DB		0DH
  F5C1                	MSG_KEY2:
  F5C1  204F5220534849			DB		' OR SHIFT+BREAK)'
  F5D1  0D            			DB		0DH
                      			
  F5D2                	MSG_DELQ:
  F5D2  46494C45204445			DB		'FILE DELETE?(Y:OK ELSE:CANSEL)'
  F5F0  0D            			DB		0DH
                      			
  F5F1                	MSG_DELY:
  F5F1  44454C45544520			DB		'DELETE OK'
  F5FA  0D            			DB		0DH
                      			
  F5FB                	MSG_DELN:
  F5FB  44454C45544520			DB		'DELETE CANSEL'
  F608  0D            			DB		0DH
                      			
  F609                	MSG_REN:
  F609  4E4557204E414D			DB		'NEW NAME:                            '
  F62E  0D            			DB		0DH
                      			
  F62F                	MSG_DNAME:
  F62F  444F532046494C			DB		'DOS FILE:                            '
  F654  0D            			DB		0DH
                      			
  F655                	MSG_RENY:
  F655  52454E414D4520			DB		'RENAME OK'
  F65E  0D            			DB		0DH
                      			
  F65F                	MSG_AD1:
  F65F  41445253202B30			DB		'ADRS +0 +1 +2 +3 +4 +5 +6 +7 01234567'
  F684  0D            			DB		0DH
                      			
  F685                	MSG_AD2:
  F685  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:SHIFT+BREAK'
  F6A6  0D            			DB		0DH
                      			
  F6A7                	MSG_CPY:
  F6A7  434F5059204F4B			DB		'COPY OK'
  F6AE  0D            			DB		0DH
                      			
  F6AF                	MSG_FDW:
  F6AF  2A46445720    			DB		'*FDW '
  F6B4  0D            			DB		0DH
                      	
  F6B5                	MSG99:
  F6B5  204552524F52  			DB		' ERROR'
  F6BB  0D            			DB		0DH
                      			
  F6BC                	DEFNAME:
  F6BC  30303030      			DB		'0000'
  F6C0  0D            			DB		0DH
  F6C1                	NEND:
                      	
  F6C1                	DEFDIR:
  F6C1  2A46442020    			DB		'*FD  '
  F6C6                	DEND:
                      	
                      	;*********************** 0436H MONITOR ライト インフォメーション代替処理 ************
  F6C6                	MSHED:
  F6C6  D5            			PUSH	DE
  F6C7  C5            			PUSH	BC
  F6C8  E5            			PUSH	HL
  F6C9  3E91          			LD		A,91H      ;HEADER SAVEコマンド91H
  F6CB  CDC4F7        			CALL	MCMD       ;コマンドコード送信
                      	
  F6CE  116C04        			LD		DE,WRMSG   ;'WRITING '
  F6D1  CD1500        			CALL	MSGPR        ;メッセージ表示
  F6D4  11F110        			LD		DE,FNAME     ;ファイルネーム
  F6D7  CD1500        			CALL	MSGPR       ;メッセージ表示
                      	
  F6DA  21F010        			LD		HL,IBUFE
  F6DD  0680          			LD		B,80H
  F6DF  7E            	MSH1:	LD		A,(HL)     ;インフォメーション ブロック送信
  F6E0  CD9AF4        			CALL	SNDBYTE
  F6E3  23            			INC		HL
  F6E4  05            			DEC		B
  F6E5  20F8          			JR		NZ,MSH1
                      	
  F6E7  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F6EA  A7            			AND		A          ;00以外ならERROR
  F6EB  C2DAF7        			JP		NZ,MERR
                      	
  F6EE  C3D4F7        			JP		MRET       ;正常RETURN
                      	
                      	;******************** 0475H MONITOR ライト データ代替処理 **********************
  F6F1                	MSDAT:
  F6F1  D5            			PUSH	DE
  F6F2  C5            			PUSH	BC
  F6F3  E5            			PUSH	HL
  F6F4  3E92          			LD		A,92H      ;DATA SAVEコマンド92H
  F6F6  CDC4F7        			CALL	MCMD       ;コマンドコード送信
                      	
  F6F9  210211        			LD		HL,FSIZE   ;FSIZE送信
  F6FC  7E            			LD		A,(HL)
  F6FD  CD9AF4        			CALL	SNDBYTE
  F700  23            			INC		HL
  F701  7E            			LD		A,(HL)
  F702  CD9AF4        			CALL	SNDBYTE
                      	
  F705  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F708  A7            			AND		A          ;00以外ならERROR
  F709  C2DAF7        			JP		NZ,MERR
                      	
  F70C  ED5B0211      			LD		DE,(FSIZE)
  F710  2A0411        			LD		HL,(SADRS)
  F713  7E            	MSD1:	LD		A,(HL)
  F714  CD9AF4        			CALL	SNDBYTE      ;SADRSからFSIZE Byteを送信。分割セーブの場合、直前に0436HでOPENされたファイルを対象として256バイトずつ0475HがCALLされる。
  F717  1B            			DEC		DE
  F718  7A            			LD		A,D
  F719  B3            			OR		E
  F71A  23            			INC		HL
  F71B  20F6          			JR		NZ,MSD1
                      			
  F71D  C3D4F7        			JP		MRET       ;正常RETURN
                      	
                      	;************************** 04D8H MONITOR リード インフォメーション代替処理 *****************
  F720                	MLHED:
  F720  D5            			PUSH	DE
  F721  C5            			PUSH	BC
  F722  E5            			PUSH	HL
  F723  3E93          			LD		A,93H      ;HEADER LOADコマンド93H
  F725  CDC4F7        			CALL	MCMD       ;コマンドコード送信
                      	
  F728  0608          			LD		B,08H
  F72A  11A311        			LD		DE,LBUF
  F72D  3E0D          			LD		A,0DH
  F72F  77            	MLH0:	LD		(HL),A
  F730  13            			INC		DE
  F731  05            			DEC		B
  F732  20FB          			JR		NZ,MLH0
                      	
  F734  3E03          			LD		A,03H          ;一行分をクリアするため3文字削除、37文字出力
  F736  327111        			LD		(DSPX),A
  F739  3EC7          			LD		A,0C7H
  F73B  CDDC0D        			CALL	DPCT
  F73E  CDDC0D        			CALL	DPCT
  F741  CDDC0D        			CALL	DPCT
  F744  112FF6        			LD		DE,MSG_DNAME   ;'DOS FILE:'
  F747  CD1500        			CALL	MSGPR
  F74A  3E09          			LD		A,09H          ;カーソルを9文字目に戻す
  F74C  327111        			LD		(DSPX),A
                      	
  F74F  11AE11        			LD		DE,MBUF    ;ファイルネームを指示するための苦肉の策。LOADコマンドとしてはファイルネームなしとして改行したのちに行バッファの位置をずらしてDOSファイルネームを入力する。
  F752  CD0300        			CALL	GETL
                      			
  F755  11B711        			LD		DE,MBUF+9
                      	;		LD		DE,MBUF
  F758                	MLH1:
  F758  1A            			LD		A,(DE)
  F759  FE20          			CP		20H                 ;行頭のスペースをファイルネームまで読み飛ばし
  F75B  2003          			JR		NZ,MLH2
  F75D  13            			INC		DE
  F75E  18F8          			JR		MLH1
                      	
  F760  0620          	MLH2:	LD		B,20H
  F762  1A            	MLH4:	LD		A,(DE)     ;FNAME送信
  F763  CD9AF4        			CALL	SNDBYTE
  F766  13            			INC		DE
  F767  05            			DEC		B
  F768  20F8          			JR		NZ,MLH4
  F76A  3E0D          			LD		A,0DH
  F76C  CD9AF4        			CALL	SNDBYTE
                      			
  F76F  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F772  A7            			AND		A          ;00以外ならERROR
  F773  C2DAF7        			JP		NZ,MERR
                      	
  F776  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F779  A7            			AND		A          ;00以外ならERROR
  F77A  C2DAF7        			JP		NZ,MERR
                      	
  F77D  21F010        			LD		HL,IBUFE
  F780  0680          			LD		B,80H
  F782  CD87F4        	MLH5:	CALL	RCVBYTE    ;読みだされたインフォメーションブロックを受信
  F785  77            			LD		(HL),A
  F786  23            			INC		HL
  F787  05            			DEC		B
  F788  20F8          			JR		NZ,MLH5
                      	
  F78A  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F78D  A7            			AND		A          ;00以外ならERROR
  F78E  C2DAF7        			JP		NZ,MERR
                      	
  F791  1841          			JR		MRET       ;正常RETURN
                      	
                      	;**************************** 04F8H MONITOR リード データ代替処理 ********************
  F793                	MLDAT:
  F793  D5            			PUSH	DE
  F794  C5            			PUSH	BC
  F795  E5            			PUSH	HL
  F796  3E94          			LD		A,94H      ;DATA LOADコマンド94H
  F798  CDC4F7        			CALL	MCMD       ;コマンドコード送信
                      	
  F79B  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F79E  A7            			AND		A          ;00以外ならERROR
  F79F  C2DAF7        			JP		NZ,MERR
                      	
  F7A2  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7A5  A7            			AND		A          ;00以外ならERROR
  F7A6  C2DAF7        			JP		NZ,MERR
                      	
  F7A9  110211        			LD		DE,FSIZE   ;FSIZE送信
  F7AC  1A            			LD		A,(DE)
  F7AD  CD9AF4        			CALL	SNDBYTE
  F7B0  13            			INC		DE
  F7B1  1A            			LD		A,(DE)
  F7B2  CD9AF4        			CALL	SNDBYTE
  F7B5  CDCFF0        			CALL	DBRCV      ;FSIZE分のデータを受信し、SADRSから格納。分割ロードの場合、直前に0436HでOPENされたファイルを対象として256バイトずつSADRSが加算されて04F8HがCALLされる。
                      	
  F7B8  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7BB  A7            			AND		A          ;00以外ならERROR
  F7BC  C2DAF7        			JP		NZ,MERR
                      	
  F7BF  1813          			JR		MRET       ;正常RETURN
                      	
                      	;************************** 0588H VRFY CMT ベリファイ代替処理 *******************
  F7C1  AF            	MVRFY:	XOR		A          ;正常終了フラグ
  F7C2  FB            			EI
  F7C3  C9            			RET
                      	
                      	;******* 代替処理用コマンドコード送信 (IN:A コマンドコード) **********
  F7C4  F5            	MCMD:	PUSH	AF
  F7C5  CD74F0        			CALL	INIT
  F7C8  F1            			POP		AF
  F7C9  CD9AF4        			CALL	SNDBYTE    ;コマンドコード送信
  F7CC  CD87F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7CF  A7            			AND		A          ;00以外ならERROR
  F7D0  C2DAF7        			JP		NZ,MERR
  F7D3  C9            			RET
                      	
                      	;****** 代替処理用正常RETURN処理 **********
  F7D4  E1            	MRET:	POP		HL
  F7D5  C1            			POP		BC
  F7D6  D1            			POP		DE
  F7D7  AF            			XOR		A          ;正常終了フラグ
  F7D8  FB            			EI
  F7D9  C9            			RET
                      	
                      	;******* 代替処理用ERROR処理 **************
  F7DA                	MERR:
  F7DA  FEF0          			CP		0F0H
  F7DC  2005          			JR		NZ,MERR2
  F7DE  116CF5        			LD		DE,MSG_F0
  F7E1  1818          			JR		MERRMSG
                      			
  F7E3  FEF2          	MERR2:	CP		0F2H
  F7E5  2005          			JR		NZ,MERR3
  F7E7  1193F5        			LD		DE,MSG_F2
  F7EA  180F          			JR		MERRMSG
                      			
  F7EC  FEF1          	MERR3:	CP		0F1H
  F7EE  2005          			JR		NZ,MERR99
  F7F0  1185F5        			LD		DE,MSG_F1
  F7F3  1806          			JR		MERRMSG
                      			
  F7F5  CDC303        	MERR99:	CALL	PRTBYT
  F7F8  11B5F6        			LD		DE,MSG99
                      			
  F7FB                	MERRMSG:
  F7FB  CD1500        			CALL	MSGPR
  F7FE  CD0600        			CALL	LETLN
  F801  E1            			POP		HL
  F802  C1            			POP		BC
  F803  D1            			POP		DE
  F804  3E02          			LD		A,02H
  F806  37            			SCF
  F807  FB            			EI
  F808  C9            			RET
                      	
  F809                			END
