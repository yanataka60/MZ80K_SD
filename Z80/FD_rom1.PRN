			  Z80 ASSEMBLER - ZASM VER 1.6
                      	;2021.12.12 MZ-700でFDP、FDMが文字化けする現象に対処
                      	;2022. 1.23 04D8H MONITOR リード インフォメーション代替処理のバグを修正
                      	;2022. 1.24 ファイルネームの後ろの20h詰めを0dhに修正するための処理をArduino側からMZ-80K側に修正
                      	;2022. 1.25 0475H MONITOR ライト データ代替処理、04F8H MONITOR リード データ代替処理での8255初期化を廃止
                      	;2022. 1.26 FDコマンドでロード可能なファイル種類コードは0x01のみとしていた制限を撤廃
                      	;2022. 1.29 CMT代替処理RETURN時の割込み許可(EI)を削除
                      	;2022. 1.31 FDコマンド実行後アプリ動作が固まってしまう機械、アプリへの対処
                      	;2022. 1.31 FDLコマンド仕様変更 FDL xの場合、ファイル名先頭一文字を比較して一致したものだけを出力
                      	;           Bキーで前の20件を表示
                      	;2022. 2. 8 FDLコマンド仕様変更 FDL xの場合、ファイル名先頭1文字〜32文字までに拡張
                      	;2022. 2.10 04D8H MONITOR リード インフォメーション代替処理の中からFDLコマンドを使えるように修正
                      	;           FDLコマンド処理をサブルーチン化
                      	;2022. 2.11 04D8H MONITOR リード インフォメーション代替処理の中から呼ぶFDLコマンドがMZ-700 MONITOR 1Z-009A、1Z-009B環境下では使えないバグを修正しました。
                      	;
  0003                	GETL		EQU		0003H
  0006                	LETLN		EQU		0006H
  0009                	NEWLIN		EQU		0009H
  000C                	PRNTS		EQU		000CH
  0015                	MSGPR		EQU		0015H
  0018                	PLIST		EQU		0018H
  001B                	GETKEY		EQU		001BH
  0033                	TIMST		EQU		0033H
  03BA                	PRTWRD		EQU		03BAH
  03C3                	PRTBYT		EQU		03C3H
  0410                	HLHEX		EQU		0410H
  041F                	TWOHEX		EQU		041FH
  0BB9                	ADCN		EQU		0BB9H
  0DB5                	DISPCH		EQU		0DB5H
  0DDC                	DPCT		EQU		0DDCH
  10F0                	IBUFE		EQU		10F0H
  10F1                	FNAME		EQU		10F1H
  1102                	EADRS		EQU		1102H
  1102                	FSIZE		EQU		1102H
  1104                	SADRS		EQU		1104H
  1106                	EXEAD		EQU		1106H
  1171                	DSPX		EQU		1171H
  11A3                	LBUF		EQU		11A3H
  11AE                	MBUF		EQU		11AEH
  0082                	MONITOR_80K	EQU		0082H
  00AD                	MONITOR_700	EQU		00ADH
                      	
                      	;0D8H PORTA 送信データ(下位4ビット)
                      	;0D9H PORTB 受信データ(8ビット)
                      	;
                      	;0DAH PORTC Bit
                      	;7 IN  CHK
                      	;6 IN
                      	;5 IN
                      	;4 IN 
                      	;3 OUT
                      	;2 OUT FLG
                      	;1 OUT
                      	;0 OUT
                      	;
                      	;0DBH コントロールレジスタ
                      	
                      	
  F000                	       ORG		0F000H
                      	
  F000  00            			NOP                   ;ROM識別コード
  F001  C313F0        			JP		START
                      	;******************** MONITOR CMTルーチン代替 *************************************
  F004  C395F7        	ENT1:	JP		MSHED
  F007  C3E3F7        	ENT2:	JP		MSDAT
  F00A  C317F8        	ENT3:	JP		MLHED
  F00D  C304F9        	ENT4:	JP		MLDAT
  F010  C337F9        	ENT5:	JP		MVRFY
                      			
  F013  CD82F0        	START:	CALL	INIT
  F016  11A311        			LD		DE,LBUF     ;MZ-80K、MZ-700とも起動コマンドは'*FD'に統一
  F019  1A            			LD		A,(DE)
  F01A  FE2A          			CP		'*'
  F01C  C29CF1        			JP		NZ,MON
  F01F  13            			INC 	DE
  F020  1A            			LD		A,(DE)
  F021  FE46          			CP		'F'
  F023  C29CF1        			JP		NZ,MON
  F026  13            			INC		DE
  F027  1A            			LD		A,(DE)
  F028  FE44          			CP		'D'
  F02A  C29CF1        			JP		NZ,MON
                      			
  F02D  13            			INC		DE          ;FDの次の文字へ移動
  F02E  1A            	STT2:	LD		A,(DE)
  F02F  FE20          			CP		20H         ;FDの後に1文字空白があれば以降をファイルネームとしてロード(ファイルネームは32文字まで)
  F031  285A          			JR		Z,SDLOAD
  F033  FE2F          			CP		'/'         ;FDの後が'/'なら以降をファイルネームとしてロード、実行はしない(ファイルネームは32文字まで)
  F035  2856          			JR		Z,SDLOAD
  F037  FE0D          			CP		0DH         ;FDだけで改行の場合にはDEFNAMEの文字列をファイルネームとしてロード
  F039  200D          			JR		NZ,STETC    ;該当なしなら他コマンドをチェック
  F03B  D5            	STT3:	PUSH	DE          ;設定ファイル名(0000.mzt)を転送
  F03C  218BF7        			LD		HL,DEFNAME
  F03F  13            			INC		DE
  F040  010500        			LD		BC,NEND-DEFNAME
  F043  EDB0          			LDIR
  F045  D1            			POP		DE
  F046  1845          			JR		SDLOAD      ;LOAD処理へ
  F048                	STETC:
  F048  FE53          			CP		'S'         ;FDS:SAVE処理へ
  F04A  CAFFF0        			JP		Z,STSV
  F04D  FE41          			CP		'A'			;FDA:自動起動ファイル設定処理へ
  F04F  CA12F2        			JP		Z,STAS
  F052  FE4C          			CP		'L'         ;FDL:ファイル一覧表示
  F054  CA1DF2        			JP		Z,STLT
  F057  FE44          			CP		'D'         ;FDD:DELETE
  F059  CAB8F2        			JP		Z,STDE
  F05C  FE52          			CP		'R'         ;FDR:RENAME
  F05E  CAF5F2        			JP		Z,STRN
  F061  FE50          			CP		'P'         ;FDP:DUMP
  F063  CA22F3        			JP		Z,STPR
  F066  FE43          			CP		'C'         ;FDC:COPY
  F068  CAC7F3        			JP		Z,STCP
  F06B  FE4D          			CP		'M'         ;FDM:MEMORY DUMP
  F06D  CAF4F3        			JP		Z,STMD
  F070  FE57          			CP		'W'         ;FDW:MEMORY WRITE
  F072  CA79F4        			JP		Z,STMW
  F075  FE5A          			CP		'Z'         ;FDZ:MZ-700 PATCH START
  F077  CAC6F4        			JP		Z,STMZ
  F07A  FE55          			CP		'U'         ;FDU:MZ-700 裏RAM START
  F07C  CA35F5        			JP		Z,STURA
  F07F  C34CF1        			JP		CMDERR
                      	
                      	;**** 8255初期化 ****
                      	;PORTC下位BITをOUTPUT、上位BITをINPUT、PORTBをINPUT、PORTAをOUTPUT
  F082  3E8A          	INIT:	LD		A,8AH
  F084  D3DB          			OUT		(0DBH),A
                      	;出力BITをリセット
  F086  3E00          	INIT2:	LD		A,00H      ;PORTA <- 0
  F088  D3D8          			OUT		(0D8H),A
  F08A  D3DA          			OUT		(0DAH),A   ;PORTC <- 0
  F08C  C9            			RET
                      	
                      	;**** LOAD ****
                      	;受信ヘッダ情報をセットし、SDカードからLOAD実行
  F08D  3E81          	SDLOAD:	LD		A,81H  ;LOADコマンド81H
  F08F  CDB8F5        			CALL	STCMD
  F092  CDACF0        			CALL	HDRCV      ;ヘッダ情報受信
  F095  CDEDF0        			CALL	DBRCV      ;データ受信
  F098  3AA611        			LD		A,(LBUF+3)
  F09B  FE2F          			CP		'/'        ;'*FD/'であれば実行アドレスに飛ばずにMONITORコマンド待ちに戻る
  F09D  CA9CF1        			JP		Z,MON
                      	; FDコマンド実行後アプリ動作が固まってしまう機械、アプリへの対処
  F0A0  3E00          			LD		A,00H
  F0A2  110000        			LD		DE,0000H
  F0A5  CD3300        			CALL	TIMST
                      			
  F0A8  2A0611        			LD		HL,(EXEAD)
  F0AB  E9            			JP		(HL)
                      	
                      	;ヘッダ受信
  F0AC  21F110        	HDRCV:	LD		HL,FNAME
  F0AF  0611          			LD		B,11H
  F0B1  CD4CF5        	HDRC1:	CALL	RCVBYTE    ;ファイルネーム受信
  F0B4  77            			LD		(HL),A
  F0B5  23            			INC		HL
  F0B6  05            			DEC		B
  F0B7  20F8          			JR		NZ,HDRC1
  F0B9  11CCF5        			LD		DE,MSG_LD  ;ファイルネームLOADING表示
  F0BC  CD1500        			CALL	MSGPR
  F0BF  11F110        			LD		DE,FNAME
  F0C2  CD1500        			CALL	MSGPR
  F0C5  CD0600        			CALL	LETLN
  F0C8  210411        			LD		HL,SADRS  ;SADRS取得
  F0CB  CD4CF5        			CALL	RCVBYTE
  F0CE  77            			LD		(HL),A
  F0CF  23            			INC		HL
  F0D0  CD4CF5        			CALL	RCVBYTE
  F0D3  77            			LD		(HL),A
  F0D4  210211        			LD		HL,FSIZE   ;FSIZE取得
  F0D7  CD4CF5        			CALL	RCVBYTE
  F0DA  77            			LD		(HL),A
  F0DB  23            			INC		HL
  F0DC  CD4CF5        			CALL	RCVBYTE
  F0DF  77            			LD		(HL),A
  F0E0  210611        			LD		HL,EXEAD   ;EXEAD取得
  F0E3  CD4CF5        			CALL	RCVBYTE
  F0E6  77            			LD		(HL),A
  F0E7  23            			INC		HL
  F0E8  CD4CF5        			CALL	RCVBYTE
  F0EB  77            			LD		(HL),A
  F0EC  C9            			RET
                      	
                      	;データ受信
  F0ED  ED5B0211      	DBRCV:	LD		DE,(FSIZE)
  F0F1  2A0411        			LD		HL,(SADRS)
  F0F4  CD4CF5        	DBRLOP:	CALL	RCVBYTE
  F0F7  77            			LD		(HL),A
  F0F8  1B            			DEC		DE
  F0F9  7A            			LD		A,D
  F0FA  B3            			OR		E
  F0FB  23            			INC		HL
  F0FC  20F6          			JR		NZ,DBRLOP   ;DE=0までLOOP
  F0FE  C9            			RET
                      	
                      	;**** SAVE ****
  F0FF  13            	STSV:	INC		DE
  F100  13            			INC		DE
  F101  D5            			PUSH	DE
  F102  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればSADRSにセットして続行
  F105  383B          			JR		C,STSV1
                      	
  F107  220411        			LD		(SADRS),HL      ;SARDS保存
  F10A  D1            			POP		DE
  F10B  13            			INC		DE
  F10C  13            			INC		DE
  F10D  13            			INC		DE
  F10E  13            			INC		DE
  F10F  13            			INC		DE
  F110  D5            			PUSH	DE          ;5文字進めて4桁の16進数であればEADRSにセットして続行
  F111  CD1004        			CALL	HLHEX
  F114  382C          			JR		C,STSV1
  F116  E5            			PUSH	HL
  F117  ED4B0411      			LD		BC,(SADRS)
  F11B  ED42          			SBC		HL,BC       ;EADRSがSADRSより大きくない場合はエラー
  F11D  E1            			POP		HL
  F11E  2822          			JR		Z,STSV1
  F120  3820          			JR		C,STSV1
                      	
  F122  220211        			LD		(EADRS),HL      ;EADRS保存
  F125  D1            			POP		DE
  F126  13            			INC		DE
  F127  13            			INC		DE
  F128  13            			INC		DE
  F129  13            			INC		DE
  F12A  13            			INC		DE          ;5文字進めて4桁の16進数であればEXEADにセットして続行
  F12B  D5            			PUSH	DE
  F12C  CD1004        			CALL	HLHEX
  F12F  3811          			JR		C,STSV1
                      			
  F131  220611        			LD		(EXEAD),HL      ;EXEAD保存
  F134  D1            			POP		DE
  F135  13            			INC		DE
  F136  13            			INC		DE
  F137  13            			INC		DE
  F138  13            			INC		DE
  F139  13            			INC		DE			;5文字進めてファイルネームがあれば続行
  F13A  1A            			LD		A,(DE)
  F13B  FE31          			CP		31H
  F13D  3808          			JR		C,STSV2
  F13F  EB            			EX		DE,HL
  F140  180F          			JR		SDSAVE      ;SAVE処理へ
  F142                	STSV1:                      ;16進数4桁の取得に失敗又はEADRSがSARDSより大きくない
  F142  1116F6        			LD		DE,MSG_AD
  F145  184F          			JR		ERRMSG
  F147                	STSV2:                      ;ファイルネームの取得に失敗
  F147  1126F6        			LD		DE,MSG_FNAME
  F14A  184A          			JR		ERRMSG
  F14C                	CMDERR:                     ;コマンド異常
  F14C  1138F6        			LD		DE,MSG_CMD
  F14F  1845          			JR		ERRMSG
                      	
                      	;送信ヘッダ情報をセットし、SDカードへSAVE実行
  F151  3E80          	SDSAVE:	LD		A,80H      ;SAVEコマンド80H
  F153  CD9EF5        			CALL	STCD
  F156  A7            			AND		A          ;00以外ならERROR
  F157  C26CF1        			JP		NZ,SVERR
  F15A  CDB6F1        			CALL	HDSEND     ;ヘッダ情報送信
  F15D  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F160  A7            			AND		A          ;00以外ならERROR
  F161  2009          			JR		NZ,SVERR
  F163  CDFBF1        			CALL	DBSEND     ;データ送信
  F166  11DFF5        			LD		DE,MSG_SV
  F169  182B          			JR		ERRMSG
                      	
  F16B                	SVER0:
  F16B  D1            			POP		DE         ;CALL元STACKを破棄する
  F16C                	SVERR:
  F16C  FEF0          			CP		0F0H
  F16E  2005          			JR		NZ,ERR3
  F170  1148F6        			LD		DE,MSG_F0  ;SD-CARD INITIALIZE ERROR
  F173  1821          			JR		ERRMSG
                      	;FDコマンドでロード可能なファイル種類コードは0x01のみとしていた制限を撤廃
                      	;ERR2:	CP		0F2H
                      	;		JR		NZ,ERR3
                      	;		LD		DE,MSG_F2  ;NOT OBJECT FILE
                      	;		JR		ERRMSG
  F175  FEF1          	ERR3:	CP		0F1H
  F177  2005          			JR		NZ,ERR4
  F179  1161F6        			LD		DE,MSG_F1  ;NOT FIND FILE
  F17C  1818          			JR		ERRMSG
  F17E  FEF3          	ERR4:	CP		0F3H
  F180  2005          			JR		NZ,ERR5
  F182  116FF6        			LD		DE,MSG_F3  ;FILE EXIST
  F185  180F          			JR		ERRMSG
  F187  FEF4          	ERR5:	CP		0F4H
  F189  2005          			JR		NZ,ERR99
  F18B  1138F6        			LD		DE,MSG_CMD
  F18E  1806          			JR		ERRMSG
  F190  CDC303        	ERR99:	CALL	PRTBYT
  F193  1184F7        			LD		DE,MSG99   ;その他ERROR
  F196  CD1500        	ERRMSG:	CALL	MSGPR
  F199  CD0600        			CALL	LETLN
  F19C  214E01        	MON:	LD		HL,014EH
  F19F  7E            			LD		A,(HL)
  F1A0  FE50          			CP		'P'             ;014EHが'P'ならMZ-80K
  F1A2  CA8200        			JP		Z,MONITOR_80K
  F1A5  FE4E          			CP		'N'             ;014EHが'N'ならFN-700
  F1A7  CA8200        			JP		Z,MONITOR_80K
  F1AA  21EB06        			LD		HL,06EBH
  F1AD  7E            			LD		A,(HL)
  F1AE  FE4D          			CP		'M'             ;06EBHが'M'ならMZ-700
  F1B0  CAAD00        			JP		Z,MONITOR_700
  F1B3  C30000        			JP		0000H           ;識別できなかったら0000Hへジャンプ
                      	
                      	;ヘッダ送信
  F1B6  E5            	HDSEND:	PUSH	HL
  F1B7  0620          			LD		B,20H
  F1B9  7E            	SS1:	LD		A,(HL)     ;FNAME送信
  F1BA  CD5FF5        			CALL	SNDBYTE
  F1BD  23            			INC		HL
  F1BE  05            			DEC		B
  F1BF  20F8          			JR		NZ,SS1
  F1C1  3E0D          			LD		A,0DH
  F1C3  CD5FF5        			CALL	SNDBYTE
  F1C6  E1            			POP		HL
  F1C7  0610          			LD		B,10H
  F1C9  7E            	SS2:	LD		A,(HL)     ;PNAME送信
  F1CA  CD5FF5        			CALL	SNDBYTE
  F1CD  23            			INC		HL
  F1CE  05            			DEC		B
  F1CF  20F8          			JR		NZ,SS2
  F1D1  3E0D          			LD		A,0DH
  F1D3  CD5FF5        			CALL	SNDBYTE
  F1D6  210411        			LD		HL,SADRS   ;SADRS送信
  F1D9  7E            			LD		A,(HL)
  F1DA  CD5FF5        			CALL	SNDBYTE
  F1DD  23            			INC		HL
  F1DE  7E            			LD		A,(HL)
  F1DF  CD5FF5        			CALL	SNDBYTE
  F1E2  210211        			LD		HL,EADRS   ;EADRS送信
  F1E5  7E            			LD		A,(HL)
  F1E6  CD5FF5        			CALL	SNDBYTE
  F1E9  23            			INC		HL
  F1EA  7E            			LD		A,(HL)
  F1EB  CD5FF5        			CALL	SNDBYTE
  F1EE  210611        			LD		HL,EXEAD   ;EXEAD送信
  F1F1  7E            			LD		A,(HL)
  F1F2  CD5FF5        			CALL	SNDBYTE
  F1F5  23            			INC		HL
  F1F6  7E            			LD		A,(HL)
  F1F7  CD5FF5        			CALL	SNDBYTE
  F1FA  C9            			RET
                      	
                      	;データ送信
                      	;SADRSからEADRSまでを送信
  F1FB  2A0211        	DBSEND:	LD		HL,(EADRS)
  F1FE  EB            			EX		DE,HL
  F1FF  2A0411        			LD		HL,(SADRS)
  F202  7E            	DBSLOP:	LD		A,(HL)
  F203  CD5FF5        			CALL	SNDBYTE
  F206  7C            			LD		A,H
  F207  BA            			CP		D
  F208  2004          			JR		NZ,DBSLP1
  F20A  7D            			LD		A,L
  F20B  BB            			CP		E
  F20C  2803          			JR		Z,DBSLP2   ;HL = DE までLOOP
  F20E  23            	DBSLP1:	INC		HL
  F20F  18F1          			JR		DBSLOP
  F211  C9            	DBSLP2:	RET
                      	
                      	;**** AUTO START SET ****
  F212  3E82          	STAS:	LD		A,82H      ;AUTO START SETコマンド82H
  F214  CDB8F5        			CALL	STCMD
  F217  11EEF5        			LD		DE,MSG_AS
  F21A  C396F1        			JP		ERRMSG
                      	
                      	
                      	;**** DIRLIST ****
  F21D  13            	STLT:	INC		DE
  F21E  2190F7        			LD		HL,DEFDIR         ;行頭に'*FD 'を付けることでカーソルを移動させリターンで実行できるように
  F221  010500        			LD		BC,DEND-DEFDIR
  F224  CD2EF2        			CALL	DIRLIST
  F227  A7            			AND		A                 ;00以外ならERROR
  F228  C26CF1        			JP		NZ,SVERR
  F22B  C39CF1        			JP		MON
                      	
                      	
                      	;**** DIRLIST本体 (HL=行頭に付加する文字列の先頭アドレス BC=行頭に付加する文字列の長さ) ****
                      	;****              戻り値 A=エラーコード ****
  F22E                	DIRLIST:
  F22E  3E83          			LD		A,83H      ;DIRLISTコマンド83Hを送信
  F230  CD9EF5        			CALL	STCD       ;コマンドコード送信
  F233  A7            			AND		A          ;00以外ならERROR
  F234  C2B7F2        			JP		NZ,DLRET
                      			
  F237  C5            			PUSH	BC
  F238  0621          			LD		B,21H
  F23A  1A            	STLT1:	LD		A,(DE)
  F23B  FE0D          			CP		0DH
  F23D  2002          			JR		NZ,STLT2
  F23F  3E00          			LD		A,00H
  F241  CD5FF5        	STLT2:	CALL	SNDBYTE           ;ページ指示を送信
  F244  13            			INC		DE
  F245  05            			DEC		B
  F246  20F2          			JR		NZ,STLT1
  F248  C1            			POP		BC
  F249                	DL1:
  F249  E5            			PUSH	HL
  F24A  C5            			PUSH	BC
                      	;		LD		HL,DEFDIR         ;行頭に'*FD 'を付けることでカーソルを移動させリターンで実行できるように
  F24B  11A311        			LD		DE,LBUF
                      	;		LD		BC,DEND-DEFDIR
  F24E  EDB0          			LDIR
  F250  EB            			EX		DE,HL
  F251  CD4CF5        	DL2:	CALL	RCVBYTE           ;'00H'を受信するまでを一行とする
  F254  FE00          			CP		00H
  F256  280C          			JR		Z,DL3
  F258  FEFF          			CP		0FFH              ;'0FFH'を受信したら終了
  F25A  2815          			JR		Z,DL4
  F25C  FEFE          			CP		0FEH              ;'0FEH'を受信したら一時停止して一文字入力待ち
  F25E  2818          			JR		Z,DL5
  F260  77            			LD		(HL),A
  F261  23            			INC		HL
  F262  18ED          			JR		DL2
  F264  11A311        	DL3:	LD		DE,LBUF           ;'00H'を受信したら一行分を表示して改行
  F267  CD1500        			CALL	MSGPR
  F26A  CD0600        			CALL	LETLN
  F26D  C1            			POP		BC
  F26E  E1            			POP		HL
  F26F  18D8          			JR		DL1
  F271  CD4CF5        	DL4:	CALL	RCVBYTE           ;状態取得(00H=OK)
  F274  C1            			POP		BC
  F275  E1            			POP		HL
  F276  183F          			JR		DLRET
                      	
  F278  117AF6        	DL5:	LD		DE,MSG_KEY1        ;HIT ANT KEY表示
  F27B  CD1500        			CALL	MSGPR
  F27E  3EC2          			LD		A,0C2H
  F280  CDB50D        			CALL	DISPCH
  F283  1191F6        			LD		DE,MSG_KEY2        ;HIT ANT KEY表示
  F286  CD1500        			CALL	MSGPR
  F289  CD0600        			CALL	LETLN
  F28C  CD1B00        	DL6:	CALL	GETKEY            ;1文字入力待ち
  F28F  FE00          			CP		00H
  F291  28F9          			JR		Z,DL6
  F293  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F295  2816          			JR		Z,DL7
  F297  FE12          			CP		12H               ;カーソル↑で打ち切り
  F299  2808          			JR		Z,DL9
  F29B  FE42          			CP		42H               ;「B」で前ページ
  F29D  2810          			JR		Z,DL8
  F29F  3E00          			LD		A,00H             ;それ以外で継続
  F2A1  180C          			JR		DL8
  F2A3  3EC2          	DL9:	LD		A,0C2H            ;カーソル↑で打ち切ったときにカーソル2行上へ
  F2A5  CDDC0D        			CALL	DPCT
  F2A8  3EC2          			LD		A,0C2H
  F2AA  CDDC0D        			CALL	DPCT
  F2AD  3EFF          	DL7:	LD		A,0FFH            ;0FFH中断コードを送信
  F2AF  CD5FF5        	DL8:	CALL	SNDBYTE
  F2B2  CD0600        			CALL	LETLN
  F2B5  189A          			JR		DL2
                      			
  F2B7                	DLRET:		
  F2B7  C9            			RET
                      			
                      	
                      	;**** FILE DELETE ****
  F2B8  3E84          	STDE:	LD		A,84H      ;FILE DELETEコマンド84H
  F2BA  CDB8F5        			CALL	STCMD
                      	
  F2BD  11A1F6        			LD		DE,MSG_DELQ ;'DELETE?'表示
  F2C0  CD1500        			CALL	MSGPR
  F2C3  CD0600        			CALL	LETLN
  F2C6  CD1B00        	STDE3:	CALL	GETKEY
  F2C9  FE00          			CP		00H
  F2CB  28F9          			JR		Z,STDE3
  F2CD  FE59          			CP		59H         ;'Y'ならOKとして00Hを送信
  F2CF  2004          			JR		NZ,STDE4
  F2D1  3E00          			LD		A,00H
  F2D3  1802          			JR		STDE5
  F2D5  3EFF          	STDE4:	LD		A,0FFH      ;'Y'以外ならCANSELとして0FFHを送信
  F2D7  CD5FF5        	STDE5:	CALL	SNDBYTE
  F2DA  CD4CF5        			CALL	RCVBYTE
  F2DD  FE00          			CP		00H         ;00Hを受信すればDELETE完了
  F2DF  2005          			JR		NZ,STDE6
  F2E1  11C0F6        			LD		DE,MSG_DELY ;'DELETE OK'表示
  F2E4  180C          			JR		STDE8
  F2E6  FE01          	STDE6:	CP		01H         ;01Hを受信すればCANSEL完了
  F2E8  2005          			JR		NZ,STDE7
  F2EA  11CAF6        			LD		DE,MSG_DELN ;'DELETE CANSEL'表示
  F2ED  1803          			JR		STDE8
  F2EF  C36CF1        	STDE7:	JP		SVERR
  F2F2  C396F1        	STDE8:	JP		ERRMSG
                      	
                      	;**** FILE RENAME ****
  F2F5  3E85          	STRN:	LD		A,85H      ;FILE RENAMEコマンド85H
  F2F7  CDB8F5        			CALL	STCMD
                      	
  F2FA  11D8F6        			LD		DE,MSG_REN ;'NEW NAME:'表示
  F2FD  CD1500        			CALL	MSGPR
                      			
  F300  3E09          			LD		A,09H
  F302  327111        			LD		(DSPX),A  ;カーソル位置を'NEW NAME:'の次へ
  F305  11A311        			LD		DE,LBUF    ;NEW FILE NAMEを取得
  F308  CD0300        			CALL	GETL
  F30B  11AB11        			LD		DE,LBUF+8  ;NEW FILE NAMEを送信
  F30E  CD8FF5        			CALL	STFN
  F311  CDA5F5        			CALL	STFS
                      			
  F314  CD4CF5        			CALL	RCVBYTE
  F317  FE00          			CP		00H         ;00Hを受信すればRENAME完了
  F319  C26CF1        			JP		NZ,SVERR
  F31C  1124F7        			LD		DE,MSG_RENY
  F31F  C396F1        			JP		ERRMSG
                      	
                      	;**** FILE DUMP ****
  F322  3E86          	STPR:	LD		A,86H      ;FILE DUMPEコマンド86H
  F324  CDB8F5        			CALL	STCMD
                      	
                      	;		LD		A,0C6H     ;画面クリア
                      	;		CALL	DPCT
  F327  210411        	STPR6:	LD		HL,SADRS   ;SADRS取得
  F32A  CD4CF5        			CALL	RCVBYTE
  F32D  77            			LD		(HL),A
  F32E  23            			INC		HL
  F32F  CD4CF5        			CALL	RCVBYTE
  F332  77            			LD		(HL),A
  F333  2A0411        			LD		HL,(SADRS)
  F336  7C            			LD		A,H
  F337  FEFF          			CP		0FFH        ;ADRSに0FFFFHが送信されてきたらDUMP処理終了
  F339  2008          			JR		NZ,STPR7
  F33B  7D            			LD		A,L
  F33C  FEFF          			CP		0FFH
  F33E  2003          			JR		NZ,STPR7
  F340  C3C1F3        			JP		STPR8
  F343  112EF7        	STPR7:	LD		DE,MSG_AD1 ;DUMP TITLE表示
  F346  CD1500        			CALL	MSGPR
  F349  CD0600        			CALL	LETLN
  F34C  0E10          			LD		C,10H      ;16行(128Byte)を表示
  F34E  C5            	STPR0:	PUSH	BC
  F34F  0608          			LD		B,08H      ;一行(8Byte)を受信
  F351  21A311        			LD		HL,LBUF
  F354  CD4CF5        	STPR1:	CALL	RCVBYTE
  F357  77            			LD		(HL),A
  F358  23            			INC		HL
  F359  05            			DEC		B
  F35A  20F8          			JR		NZ,STPR1
                      	
  F35C  2A0411        			LD		HL,(SADRS) ;アドレス表示
  F35F  CDBA03        			CALL	PRTWRD
  F362  110800        			LD		DE,0008H   ;一画面(128Byte)中はアドレスを受け取らないので自前でインクリメント
  F365  19            			ADD		HL,DE
  F366  220411        			LD		(SADRS),HL
                      			
  F369  0608          			LD		B,08H      ;一行(8Byte)のデータを16進数表示
  F36B  11A311        			LD		DE,LBUF
  F36E  CD0C00        	STPR2:	CALL	PRNTS
  F371  1A            			LD		A,(DE)
  F372  CDC303        			CALL	PRTBYT
  F375  13            			INC		DE
  F376  05            			DEC		B
  F377  20F5          			JR		NZ,STPR2
                      			
  F379  CD0C00        			CALL	PRNTS
  F37C  11A311        			LD		DE,LBUF    ;一行(8Byte)のデータをキャラクタ表示
  F37F  0608          			LD		B,08H
  F381  1A            	STPR9:	LD		A,(DE)
  F382  FE10          			CP		10H        ;MZ-700での文字化けに対処
  F384  3002          			JR		NC,STPRA
  F386  3E20          			LD		A,20H
  F388  CDB90B        	STPRA:	CALL	ADCN
  F38B  CDB50D        			CALL	DISPCH
  F38E  13            			INC		DE
  F38F  05            			DEC		B
  F390  20EF          			JR		NZ,STPR9
                      	
  F392  CD0600        			CALL	LETLN
  F395  C1            			POP		BC
  F396  0D            			DEC		C
  F397  20B5          			JR		NZ,STPR0
                      			
  F399  1154F7        			LD		DE,MSG_AD2        ;入力待ちメッセージ表示
  F39C  CD1500        			CALL	MSGPR
  F39F  CD0600        			CALL	LETLN
  F3A2  CD0600        			CALL	LETLN
  F3A5  CD1B00        	STPR3:	CALL	GETKEY            ;1文字入力待ち
  F3A8  FE00          			CP		00H
  F3AA  28F9          			JR		Z,STPR3
  F3AC  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F3AE  2806          			JR		Z,STPR4
  F3B0  CD5FF5        			CALL	SNDBYTE           ;SHIFT+BREAK以外はASCIIコードのまま送信、ARDUINO側で'B'を処理
  F3B3  C327F3        			JP		STPR6
  F3B6  3EFF          	STPR4:	LD		A,0FFH            ;0FFH中断コードを送信
  F3B8  CD5FF5        	STPR5:	CALL	SNDBYTE
  F3BB  CD4CF5        			CALL	RCVBYTE           ;SHIFT+BREAKでの中断時はADRS'0FFFFH'の受信及び状態コードの受信を破棄
  F3BE  CD4CF5        			CALL	RCVBYTE
  F3C1  CD4CF5        	STPR8:	CALL	RCVBYTE
  F3C4  C39CF1        			JP		MON
                      	
                      	;**** FILE COPY ****
  F3C7  3E87          	STCP:	LD		A,87H      ;FILE COPYコマンド87H
  F3C9  CDB8F5        			CALL	STCMD
  F3CC  11D8F6        			LD		DE,MSG_REN ;'NEW NAME:'表示
  F3CF  CD1500        			CALL	MSGPR
                      			
  F3D2  3E09          			LD		A,09H
  F3D4  327111        			LD		(DSPX),A    ;カーソル位置を'NEW NAME:'の次へ
  F3D7  11A311        			LD		DE,LBUF      ;NEW FILE NAMEを取得
  F3DA  CD0300        			CALL	GETL
  F3DD  11AB11        			LD		DE,LBUF+8    ;NEW FILE NAMEを送信
  F3E0  CD8FF5        			CALL	STFN
  F3E3  CDA5F5        			CALL	STFS
                      			
  F3E6  CD4CF5        			CALL	RCVBYTE
  F3E9  FE00          			CP		00H         ;00Hを受信すればRENAME完了
  F3EB  C26CF1        			JP		NZ,SVERR
  F3EE  1176F7        			LD		DE,MSG_CPY
  F3F1  C396F1        			JP		ERRMSG
                      	
                      	;**** MEMORY DUMP ****
  F3F4  13            	STMD:	INC		DE
  F3F5  13            			INC		DE
  F3F6  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればSADRSにセットして続行
  F3F9  DA42F1        			JP		C,STSV1
  F3FC  220411        			LD		(SADRS),HL      ;SARDS保存
                      	
  F3FF  112EF7        	STMD6:	LD		DE,MSG_AD1 ;DUMP TITLE表示
  F402  CD1500        			CALL	MSGPR
  F405  CD0600        			CALL	LETLN
  F408  0E10          			LD		C,10H      ;16行(128Byte)を表示
  F40A  2A0411        	STMD7:	LD		HL,(SADRS) ;アドレス表示
  F40D  CDBA03        			CALL	PRTWRD
  F410  CD0C00        			CALL	PRNTS
                      	
                      			
  F413  0608          			LD		B,08H      ;一行(8Byte)のデータを16進数表示
  F415  7E            	STMD0:	LD		A,(HL)
  F416  CDC303        			CALL	PRTBYT
  F419  CD0C00        			CALL	PRNTS
  F41C  CD1B00        			CALL	GETKEY
  F41F  FE64          			CP		64H
  F421  2853          			JR		Z,STMD4
  F423  23            			INC		HL
  F424  05            			DEC		B
  F425  20EE          			JR		NZ,STMD0
                      	
  F427  2A0411        			LD		HL,(SADRS)
  F42A  0608          			LD		B,08H      ;一行(8Byte)のデータをキャラクタ表示
  F42C  7E            	STMD2:	LD		A,(HL)
  F42D  FE10          			CP		10H        ;MZ-700での文字化けに対処
  F42F  3002          			JR		NC,STMD8
  F431  3E20          			LD		A,20H
  F433  CDB90B        	STMD8:	CALL	ADCN
  F436  CDB50D        			CALL	DISPCH
  F439  CD1B00        			CALL	GETKEY
  F43C  FE64          			CP		64H        ;表示途中でもSHIFT+BREAKで打ち切り
  F43E  2836          			JR		Z,STMD4
  F440  23            			INC		HL
  F441  05            			DEC		B
  F442  20E8          			JR		NZ,STMD2
                      	
  F444  220411        			LD		(SADRS),HL
  F447  CD0600        			CALL	LETLN
                      	
  F44A  0D            			DEC		C
  F44B  20BD          			JR		NZ,STMD7
                      			
  F44D  1154F7        			LD		DE,MSG_AD2        ;入力待ちメッセージ表示
  F450  CD1500        			CALL	MSGPR
  F453  CD0600        			CALL	LETLN
  F456  CD0600        			CALL	LETLN
  F459  CD1B00        	STMD3:	CALL	GETKEY            ;1文字入力待ち
  F45C  FE00          			CP		00H
  F45E  28F9          			JR		Z,STMD3
  F460  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F462  2812          			JR		Z,STMD4
  F464  FE42          			CP		42H
  F466  200B          			JR		NZ,STMD5
  F468  2A0411        			LD		HL,(SADRS)
  F46B  110001        			LD		DE,0100H
  F46E  ED52          			SBC		HL,DE
  F470  220411        			LD		(SADRS),HL
  F473  C3FFF3        	STMD5:	JP		STMD6
  F476  C39CF1        	STMD4:	JP		MON
                      	
                      	;**** MEMORY WRITE ****
  F479  13            	STMW:	INC		DE
  F47A  13            			INC		DE
  F47B  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればHLにセットして続行
  F47E  DA42F1        			JP		C,STSV1
                      	
  F481  13            			INC		DE
  F482  13            			INC		DE
  F483  13            			INC		DE
  F484  13            			INC		DE
  F485  1A            	STSP1:	LD		A,(DE)
  F486  FE0D          			CP		0DH
  F488  2839          			JR		Z,STMW9     ;アドレスのみなら終了
  F48A  FE20          			CP		20H
  F48C  2003          			JR		NZ,STMW1
  F48E  13            			INC		DE          ;空白は飛ばす
  F48F  18F4          			JR		STSP1
                      	
  F491                	STMW1:
  F491  CD1F04        			CALL	TWOHEX
  F494  380E          			JR		C,STMW8
  F496  77            			LD		(HL),A      ;2桁の16進数があれば(HL)に書き込み
  F497  23            			INC		HL
                      	
  F498  1A            	STSP2:	LD		A,(DE)
  F499  FE0D          			CP		0DH         ;一行終了
  F49B  2807          			JR		Z,STMW8
  F49D  FE20          			CP		20H
  F49F  20F0          			JR		NZ,STMW1
  F4A1  13            			INC		DE          ;空白は飛ばす
  F4A2  18F4          			JR		STSP2
                      	
  F4A4                	STMW8:	
  F4A4  117EF7        			LD		DE,MSG_FDW  ;行頭に'*FDW '
  F4A7  CD1500        			CALL	MSGPR
  F4AA  CDBA03        			CALL	PRTWRD      ;アドレス表示
  F4AD  CD0C00        			CALL	PRNTS
  F4B0  11A311        			LD		DE,LBUF     ;一行入力
  F4B3  CD0300        			CALL	GETL
  F4B6  11A311        			LD		DE,LBUF
  F4B9  1A            			LD		A,(DE)
  F4BA  FE1B          			CP		1BH
  F4BC  2805          			JR		Z,STMW9     ;SHIFT+BREAKで破棄、終了
  F4BE  11A611        			LD		DE,LBUF+3
  F4C1  18B6          			JR		STMW
  F4C3  C39CF1        	STMW9:	JP		MON
                      	
                      	;**** MZ-700 PATCH START ****
  F4C6  F3            	STMZ:	DI
  F4C7  210000        			LD		HL,0000H      ;ROMを2000Hにコピー
  F4CA  110020        			LD		DE,2000H
  F4CD  010010        			LD		BC,1000H
  F4D0  EDB0          			LDIR
  F4D2  D3E0          			OUT		(0E0H),A      ;裏RAM ON
  F4D4  210020        			LD		HL,2000H      ;裏RAMにROMの内容をコピー
  F4D7  110000        			LD		DE,0000H
  F4DA  010010        			LD		BC,1000H
  F4DD  EDB0          			LDIR
  F4DF  2108F5        			LD		HL,STMZ2      ;書き換えアドレス
  F4E2  1126F5        			LD		DE,STMZ3      ;書き換えデータ
  F4E5  060F          			LD		B,0FH
  F4E7  C5            	STMZ1:	PUSH	BC
  F4E8  4E            			LD		C,(HL)
  F4E9  23            			INC		HL
  F4EA  46            			LD		B,(HL)
  F4EB  1A            			LD		A,(DE)
  F4EC  02            			LD		(BC),A
  F4ED  C1            			POP		BC
  F4EE  13            			INC		DE
  F4EF  23            			INC		HL
  F4F0  05            			DEC		B
  F4F1  20F4          			JR		NZ,STMZ1
  F4F3  21AD00        			LD		HL,00ADH
  F4F6  7E            			LD		A,(HL)
  F4F7  FECD          			CP		0CDH
  F4F9  C20000        			JP		NZ,0000H         ;MZ-700と判断できなければ0000Hからスタート
  F4FC  11FFF5        			LD		DE,MSG_ST
  F4FF  CD1500        			CALL	MSGPR
  F502  CD0600        			CALL	LETLN
  F505  C3AD00        			JP		MONITOR_700      ;MZ-700と判断できれば00ADHからスタート
                      	
  F508  370438043904  	STMZ2:	DW		0437H,0438H,0439H
  F50E  760477047804  			DW		0476H,0477H,0478H
  F514  D904DA04DB04  			DW		04D9H,04DAH,04DBH
  F51A  F904FA04FB04  			DW		04F9H,04FAH,04FBH
  F520  89058A058B05  			DW		0589H,058AH,058BH
                      	
  F526  C3            	STMZ3:	DB		0C3H
  F527  04F0          			DW		ENT1
  F529  C3            			DB		0C3H
  F52A  07F0          			DW		ENT2
  F52C  C3            			DB		0C3H
  F52D  0AF0          			DW		ENT3
  F52F  C3            			DB		0C3H
  F530  0DF0          			DW		ENT4
  F532  C3            			DB		0C3H
  F533  10F0          			DW		ENT5
                      	
                      	;**** MZ-700 裏RAM START ****
  F535  D3E0          	STURA:	OUT		(0E0H),A      ;裏RAM ON
  F537  21AD00        			LD		HL,00ADH
  F53A  7E            			LD		A,(HL)
  F53B  FECD          			CP		0CDH
  F53D  C20000        			JP		NZ,0000H         ;0CDHでなければNZ-700などと判断して0000Hからスタート
  F540  11FFF5        			LD		DE,MSG_ST
  F543  CD1500        			CALL	MSGPR
  F546  CD0600        			CALL	LETLN
  F549  C3AD00        			JP		MONITOR_700      ;(00ADH)が0CDHなら1Z-009A又は1Z-009Bのパッチ済みMONITORと判断して00ADHからスタート
                      	
                      	;**** 1BYTE受信 ****
                      	;受信DATAをAレジスタにセットしてリターン
  F54C                	RCVBYTE:
  F54C  CD81F5        			CALL	F1CHK      ;PORTC BIT7が1になるまでLOOP
  F54F  DBD9          			IN		A,(0D9h)   ;PORTB -> A
  F551  F5            			PUSH 	AF
  F552  3E05          			LD		A,05H
  F554  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 1
  F556  CD88F5        			CALL	F2CHK      ;PORTC BIT7が0になるまでLOOP
  F559  3E04          			LD		A,04H
  F55B  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 0
  F55D  F1            			POP 	AF
  F55E  C9            			RET
                      			
                      	;**** 1BYTE送信 ****
                      	;Aレジスタの内容をPORTA下位4BITに4BITずつ送信
  F55F                	SNDBYTE:
  F55F  F5            			PUSH	AF
  F560  1F            			RRA
  F561  1F            			RRA
  F562  1F            			RRA
  F563  1F            			RRA
  F564  E60F          			AND		0FH
  F566  CD70F5        			CALL	SND4BIT
  F569  F1            			POP		AF
  F56A  E60F          			AND		0FH
  F56C  CD70F5        			CALL	SND4BIT
  F56F  C9            			RET
                      	
                      	;**** 4BIT送信 ****
                      	;Aレジスタ下位4ビットを送信する
  F570                	SND4BIT:
  F570  D3D8          			OUT		(0D8H),A
  F572  3E05          			LD		A,05H
  F574  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 1
  F576  CD81F5        			CALL	F1CHK      ;PORTC BIT7が1になるまでLOOP
  F579  3E04          			LD		A,04H
  F57B  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 0
  F57D  CD88F5        			CALL	F2CHK
  F580  C9            			RET
                      			
                      	;**** BUSYをCHECK(1) ****
                      	; 82H BIT7が1になるまでLOP
  F581  DBDA          	F1CHK:	IN		A,(0DAH)
  F583  E680          			AND		80H        ;PORTC BIT7 = 1?
  F585  28FA          			JR		Z,F1CHK
  F587  C9            			RET
                      	
                      	;**** BUSYをCHECK(0) ****
                      	; 82H BIT7が0になるまでLOOP
  F588  DBDA          	F2CHK:	IN		A,(0DAH)
  F58A  E680          			AND		80H        ;PORTC BIT7 = 0?
  F58C  20FA          			JR		NZ,F2CHK
  F58E  C9            			RET
                      	
                      	;****** FILE NAME 取得 (IN:DE コマンド文字の次の文字 OUT:HL ファイルネームの先頭)*********
  F58F  F5            	STFN:	PUSH	AF
  F590  13            	STFN1:	INC		DE         ;ファイルネームまでスペース読み飛ばし
  F591  1A            			LD		A,(DE)
  F592  FE20          			CP		20H
  F594  28FA          			JR		Z,STFN1
  F596  FE30          			CP		30H        ;「0」以上の文字でなければエラーとする
  F598  DA47F1        			JP		C,STSV2
  F59B  EB            			EX		DE,HL
  F59C  F1            			POP		AF
  F59D  C9            			RET
                      	
                      	;**** コマンド送信 (IN:A コマンドコード)****
  F59E  CD5FF5        	STCD:	CALL	SNDBYTE    ;Aレジスタのコマンドコードを送信
  F5A1  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F5A4  C9            			RET
                      	
                      	;**** ファイルネーム送信(IN:HL ファイルネームの先頭) ******
  F5A5  0620          	STFS:	LD		B,20H
  F5A7  7E            	STFS1:	LD		A,(HL)     ;FNAME送信
  F5A8  CD5FF5        			CALL	SNDBYTE
  F5AB  23            			INC		HL
  F5AC  05            			DEC		B
  F5AD  20F8          			JR		NZ,STFS1
  F5AF  3E0D          			LD		A,0DH
  F5B1  CD5FF5        			CALL	SNDBYTE
  F5B4  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F5B7  C9            			RET
                      	
                      	;**** コマンド、ファイル名送信 (IN:A コマンドコード HL:ファイルネームの先頭)****
  F5B8  CD8FF5        	STCMD:	CALL	STFN       ;ファイルネーム取得
  F5BB  E5            			PUSH	HL
  F5BC  CD9EF5        			CALL	STCD       ;コマンドコード送信
  F5BF  E1            			POP		HL
  F5C0  A7            			AND		A          ;00以外ならERROR
  F5C1  C26BF1        			JP		NZ,SVER0
  F5C4  CDA5F5        			CALL	STFS       ;ファイルネーム送信
  F5C7  A7            			AND		A          ;00以外ならERROR
  F5C8  C26BF1        			JP		NZ,SVER0
  F5CB  C9            			RET
                      	
                      	;******** MESSAGE DATA ********************
  F5CC                	MSG_LD:
  F5CC  16            			DB		16H
  F5CD  4C4F4144494E47			DB		'LOADING '
  F5D5  0D            			DB		0DH
                      	
  F5D6                	WRMSG:
  F5D6  57524954494E47			DB		'WRITING '
  F5DE  0D            			DB		0DH
                      	
  F5DF                	MSG_SV:
  F5DF  53415645204649			DB		'SAVE FINISHED!'
  F5ED  0D            			DB		0DH
                      			
  F5EE                	MSG_AS:
  F5EE  41535441525420			DB		'ASTART FINISHED!'
  F5FE  0D            			DB		0DH
                      			
  F5FF                	MSG_ST:
  F5FF  50415443484544			DB		'PATCHED MONITOR START!'
  F615  0D            			DB		0DH
                      			
  F616                	MSG_AD:
  F616  41444452455353			DB		'ADDRESS FAILED!'
  F625  0D            			DB		0DH
                      			
  F626                	MSG_FNAME:
  F626  46494C45204E41			DB		'FILE NAME FAILED!'
  F637  0D            			DB		0DH
                      			
  F638                	MSG_CMD:
  F638  434F4D4D414E44			DB		'COMMAND FAILED!'
  F647  0D            			DB		0DH
                      			
  F648                	MSG_F0:
  F648  53442D43415244			DB		'SD-CARD INITIALIZE ERROR'
  F660  0D            			DB		0DH
                      			
  F661                	MSG_F1:
  F661  4E4F542046494E			DB		'NOT FIND FILE'
  F66E  0D            			DB		0DH
                      			
                      	;MSG_F2:
                      	;		DB		'NOT OBJECT FILE'
                      	;		DB		0DH
                      			
  F66F                	MSG_F3:
  F66F  46494C45204558			DB		'FILE EXIST'
  F679  0D            			DB		0DH
                      			
  F67A                	MSG_KEY1:
  F67A  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:'
  F690  0D            			DB		0DH
  F691                	MSG_KEY2:
  F691  204F5220534849			DB		' OR SHIFT+BREAK'
  F6A0  0D            			DB		0DH
                      			
  F6A1                	MSG_DELQ:
  F6A1  46494C45204445			DB		'FILE DELETE?(Y:OK ELSE:CANSEL)'
  F6BF  0D            			DB		0DH
                      			
  F6C0                	MSG_DELY:
  F6C0  44454C45544520			DB		'DELETE OK'
  F6C9  0D            			DB		0DH
                      			
  F6CA                	MSG_DELN:
  F6CA  44454C45544520			DB		'DELETE CANSEL'
  F6D7  0D            			DB		0DH
                      			
  F6D8                	MSG_REN:
  F6D8  4E4557204E414D			DB		'NEW NAME:                            '
  F6FD  0D            			DB		0DH
                      			
  F6FE                	MSG_DNAME:
  F6FE  444F532046494C			DB		'DOS FILE:'
  F707                	MSG_DNAMEEND:
  F707  20202020202020			DB		'                            '
  F723  0D            			DB		0DH
                      			
  F724                	MSG_RENY:
  F724  52454E414D4520			DB		'RENAME OK'
  F72D  0D            			DB		0DH
                      			
  F72E                	MSG_AD1:
  F72E  41445253202B30			DB		'ADRS +0 +1 +2 +3 +4 +5 +6 +7 01234567'
  F753  0D            			DB		0DH
                      			
  F754                	MSG_AD2:
  F754  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:SHIFT+BREAK'
  F775  0D            			DB		0DH
                      			
  F776                	MSG_CPY:
  F776  434F5059204F4B			DB		'COPY OK'
  F77D  0D            			DB		0DH
                      			
  F77E                	MSG_FDW:
  F77E  2A46445720    			DB		'*FDW '
  F783  0D            			DB		0DH
                      	
  F784                	MSG99:
  F784  204552524F52  			DB		' ERROR'
  F78A  0D            			DB		0DH
                      			
  F78B                	DEFNAME:
  F78B  30303030      			DB		'0000'
  F78F  0D            			DB		0DH
  F790                	NEND:
                      	
  F790                	DEFDIR:
  F790  2A46442020    			DB		'*FD  '
  F795                	DEND:
                      	
                      	;*********************** 0436H MONITOR ライト インフォメーション代替処理 ************
  F795                	MSHED:
  F795  F3            			DI
  F796  D5            			PUSH	DE
  F797  C5            			PUSH	BC
  F798  E5            			PUSH	HL
  F799  CD82F0        			CALL	INIT
  F79C  3E91          			LD		A,91H      ;HEADER SAVEコマンド91H
  F79E  CD3AF9        			CALL	MCMD       ;コマンドコード送信
  F7A1  A7            			AND		A          ;00以外ならERROR
  F7A2  C246F9        			JP		NZ,MERR
                      	
                      	;S-OS SWORD、8080用テキスト・エディタ＆アセンブラはファイルネームの後ろが20h詰めとなるため0dhに修正
  F7A5  0611          			LD		B,11H
  F7A7  210111        			LD		HL,FNAME+10H     ;ファイルネーム
  F7AA  3E0D          			LD		A,0DH            ;17文字目には常に0DHをセットする
  F7AC  77            			LD		(HL),A
  F7AD  7E            	MSH0:	LD		A,(HL)
  F7AE  FE0D          			CP		0DH              ;0DHであればひとつ前の文字の検査に移る
  F7B0  2807          			JR		Z,MSH1
  F7B2  FE20          			CP		20H              ;20Hであれば0DHをセットしてひとつ前の文字の検査に移る
  F7B4  2007          			JR		NZ,MSH2          ;0DH、20H以外の文字であれば終了
  F7B6  3E0D          			LD		A,0DH
  F7B8  77            			LD		(HL),A
                      			
  F7B9  2B            	MSH1:	DEC		HL
  F7BA  05            			DEC		B
  F7BB  20F0          			JR		NZ,MSH0
                      	
  F7BD  CD0600        	MSH2:	CALL	LETLN
  F7C0  11D6F5        			LD		DE,WRMSG   ;'WRITING '
  F7C3  CD1500        			CALL	MSGPR        ;メッセージ表示
  F7C6  11F110        			LD		DE,FNAME     ;ファイルネーム
  F7C9  CD1500        			CALL	MSGPR       ;メッセージ表示
                      	
  F7CC  21F010        			LD		HL,IBUFE
  F7CF  0680          			LD		B,80H
  F7D1  7E            	MSH3:	LD		A,(HL)     ;インフォメーション ブロック送信
  F7D2  CD5FF5        			CALL	SNDBYTE
  F7D5  23            			INC		HL
  F7D6  05            			DEC		B
  F7D7  20F8          			JR		NZ,MSH3
                      	
  F7D9  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7DC  A7            			AND		A          ;00以外ならERROR
  F7DD  C246F9        			JP		NZ,MERR
                      	
  F7E0  C341F9        			JP		MRET       ;正常RETURN
                      	
                      	;******************** 0475H MONITOR ライト データ代替処理 **********************
  F7E3                	MSDAT:
  F7E3  F3            			DI
  F7E4  D5            			PUSH	DE
  F7E5  C5            			PUSH	BC
  F7E6  E5            			PUSH	HL
  F7E7  3E92          			LD		A,92H      ;DATA SAVEコマンド92H
  F7E9  CD3AF9        			CALL	MCMD       ;コマンドコード送信
  F7EC  A7            			AND		A          ;00以外ならERROR
  F7ED  C246F9        			JP		NZ,MERR
                      	
  F7F0  210211        			LD		HL,FSIZE   ;FSIZE送信
  F7F3  7E            			LD		A,(HL)
  F7F4  CD5FF5        			CALL	SNDBYTE
  F7F7  23            			INC		HL
  F7F8  7E            			LD		A,(HL)
  F7F9  CD5FF5        			CALL	SNDBYTE
                      	
  F7FC  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7FF  A7            			AND		A          ;00以外ならERROR
  F800  C246F9        			JP		NZ,MERR
                      	
  F803  ED5B0211      			LD		DE,(FSIZE)
  F807  2A0411        			LD		HL,(SADRS)
  F80A  7E            	MSD1:	LD		A,(HL)
  F80B  CD5FF5        			CALL	SNDBYTE      ;SADRSからFSIZE Byteを送信。分割セーブの場合、直前に0436HでOPENされたファイルを対象として256バイトずつ0475HがCALLされる。
  F80E  1B            			DEC		DE
  F80F  7A            			LD		A,D
  F810  B3            			OR		E
  F811  23            			INC		HL
  F812  20F6          			JR		NZ,MSD1
                      			
  F814  C341F9        			JP		MRET       ;正常RETURN
                      	
                      	;************************** 04D8H MONITOR リード インフォメーション代替処理 *****************
  F817                	MLHED:
  F817  F3            			DI
  F818  D5            			PUSH	DE
  F819  C5            			PUSH	BC
  F81A  E5            			PUSH	HL
  F81B  CD82F0        			CALL	INIT
                      	
  F81E  3E00          			LD		A,00H
  F820  110000        			LD		DE,0000H
  F823  CD3300        			CALL	TIMST
                      	
  F826  0608          			LD		B,08H      ;LBUFを0DHで埋めファイルネームが指定されなかったことにする
  F828  11A311        			LD		DE,LBUF
  F82B  3E0D          			LD		A,0DH
  F82D  12            	MLH0:	LD		(DE),A
  F82E  13            			INC		DE
  F82F  05            			DEC		B
  F830  20FB          			JR		NZ,MLH0
                      	
  F832  3E03          			LD		A,03H          ;一行分をクリアするため3文字削除、37文字出力
  F834  327111        			LD		(DSPX),A
  F837  3EC7          			LD		A,0C7H
  F839  CDDC0D        			CALL	DPCT
  F83C  CDDC0D        			CALL	DPCT
  F83F  CDDC0D        			CALL	DPCT
  F842  11FEF6        	MLH6:	LD		DE,MSG_DNAME   ;'DOS FILE:'
  F845  CD1500        			CALL	MSGPR
  F848  3E09          			LD		A,09H          ;カーソルを9文字目に戻す
  F84A  327111        			LD		(DSPX),A
                      	
  F84D  11AE11        			LD		DE,MBUF    ;ファイルネームを指示するための苦肉の策。LOADコマンドとしてはファイルネームなしとして改行したのちに行バッファの位置をずらしてDOSファイルネームを入力する。
  F850  CD0300        			CALL	GETL
                      			
  F853  11B711        			LD		DE,MBUF+9
                      			
  F856  1A            			LD		A,(DE)
                      	;**** ファイルネームの先頭が'*'なら拡張コマンド処理へ移行 ****
  F857  FE2A          			CP		'*'
  F859  2845          			JR		Z,MLHCMD
                      	
  F85B  3E93          			LD		A,93H      ;HEADER LOADコマンド93H
  F85D  CD3AF9        			CALL	MCMD       ;コマンドコード送信
  F860  A7            			AND		A          ;00以外ならERROR
  F861  C246F9        			JP		NZ,MERR
                      	
  F864                	MLH1:
  F864  1A            			LD		A,(DE)
  F865  FE20          			CP		20H                 ;行頭のスペースをファイルネームまで読み飛ばし
  F867  2003          			JR		NZ,MLH2
  F869  13            			INC		DE
  F86A  18F8          			JR		MLH1
                      	
  F86C  0620          	MLH2:	LD		B,20H
  F86E  1A            	MLH4:	LD		A,(DE)     ;FNAME送信
  F86F  CD5FF5        			CALL	SNDBYTE
  F872  13            			INC		DE
  F873  05            			DEC		B
  F874  20F8          			JR		NZ,MLH4
  F876  3E0D          			LD		A,0DH
  F878  CD5FF5        			CALL	SNDBYTE
                      			
  F87B  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F87E  A7            			AND		A          ;00以外ならERROR
  F87F  C246F9        			JP		NZ,MERR
                      	
  F882  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F885  A7            			AND		A          ;00以外ならERROR
  F886  C246F9        			JP		NZ,MERR
                      	
  F889  21F010        			LD		HL,IBUFE
  F88C  0680          			LD		B,80H
  F88E  CD4CF5        	MLH5:	CALL	RCVBYTE    ;読みだされたインフォメーションブロックを受信
  F891  77            			LD		(HL),A
  F892  23            			INC		HL
  F893  05            			DEC		B
  F894  20F8          			JR		NZ,MLH5
                      	
  F896  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F899  A7            			AND		A          ;00以外ならERROR
  F89A  C246F9        			JP		NZ,MERR
                      	
  F89D  C341F9        			JP		MRET       ;正常RETURN
                      	
                      	;**************************** アプリケーション内SD-CARD操作処理 **********************
  F8A0                	MLHCMD:
                      	;**** HL、DE、BCレジスタを保存 ****
  F8A0  E5            			PUSH	HL
  F8A1  D5            			PUSH	DE
  F8A2  C5            			PUSH	BC
  F8A3  13            			INC		DE
  F8A4  0603          			LD		B,03H
                      	;**** FDLコマンド ****
  F8A6  2100F9        			LD		HL,CMD1
  F8A9  CDECF8        			CALL	CMPSTR
  F8AC  2805          			JR		Z,MLHCMD2
  F8AE  C1            			POP		BC
  F8AF  D1            			POP		DE
  F8B0  E1            			POP		HL
                      	;**** ファイルネーム入力へ復帰 ****
  F8B1  188F          			JR		MLH6
                      	
  F8B3                	MLHCMD2:
  F8B3  13            			INC		DE
  F8B4  13            			INC		DE
  F8B5  13            			INC		DE
  F8B6  21FEF6        			LD		HL,MSG_DNAME         ;行頭に'DOS FILE:'を付けることでカーソルを移動させリターンで実行できるように
  F8B9  010900        			LD		BC,MSG_DNAMEEND-MSG_DNAME
                      	;**** FDLコマンド呼び出し ****
  F8BC  CD2EF2        			CALL	DIRLIST
  F8BF  A7            			AND		A          ;00以外ならERROR
  F8C0  2006          			JR		NZ,SERR
  F8C2  C1            			POP		BC
  F8C3  D1            			POP		DE
  F8C4  E1            			POP		HL
                      	;**** ファイルネーム入力へ復帰 ****
  F8C5  C342F8        			JP		MLH6
                      	
                      	;******* アプリケーション内SD-CARD操作処理用ERROR処理 **************
  F8C8                	SERR:
  F8C8  FEF0          			CP		0F0H
  F8CA  2005          			JR		NZ,SERR3
  F8CC  1148F6        			LD		DE,MSG_F0
  F8CF  180F          			JR		SERRMSG
                      			
  F8D1  FEF1          	SERR3:	CP		0F1H
  F8D3  2005          			JR		NZ,SERR99
  F8D5  1161F6        			LD		DE,MSG_F1
  F8D8  1806          			JR		SERRMSG
                      			
  F8DA  CDC303        	SERR99:	CALL	PRTBYT
  F8DD  1184F7        			LD		DE,MSG99
                      			
  F8E0                	SERRMSG:
  F8E0  CD1500        			CALL	MSGPR
  F8E3  CD0600        			CALL	LETLN
  F8E6  C1            			POP		BC
  F8E7  D1            			POP		DE
  F8E8  E1            			POP		HL
                      	;**** ファイルネーム入力へ復帰 ****
  F8E9  C342F8        			JP		MLH6
                      	
                      	;**** コマンド文字列比較 ****
  F8EC                	CMPSTR:
  F8EC  C5            			PUSH	BC
  F8ED  D5            			PUSH	DE
  F8EE  1A            	CMP1:	LD		A,(DE)
  F8EF  BE            			CP		(HL)
  F8F0  200B          			JR		NZ,CMP2
  F8F2  05            			DEC		B
  F8F3  2808          			JR		Z,CMP2
  F8F5  FE0D          			CP		0Dh
  F8F7  2804          			JR		Z,CMP2
  F8F9  13            			INC		DE
  F8FA  23            			INC		HL
  F8FB  18F1          			JR		CMP1
  F8FD  D1            	CMP2:	POP		DE
  F8FE  C1            			POP		BC
  F8FF  C9            			RET
                      	
                      	;**** コマンドリスト ****
                      	; 将来拡張用
  F900  46444C0D      	CMD1:	DB		'FDL',0DH
                      	
                      	
                      	;**************************** 04F8H MONITOR リード データ代替処理 ********************
  F904                	MLDAT:
  F904  F3            			DI
  F905  D5            			PUSH	DE
  F906  C5            			PUSH	BC
  F907  E5            			PUSH	HL
  F908  3E94          			LD		A,94H      ;DATA LOADコマンド94H
  F90A  CD3AF9        			CALL	MCMD       ;コマンドコード送信
  F90D  A7            			AND		A          ;00以外ならERROR
  F90E  C246F9        			JP		NZ,MERR
                      	
  F911  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F914  A7            			AND		A          ;00以外ならERROR
  F915  C246F9        			JP		NZ,MERR
                      	
  F918  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F91B  A7            			AND		A          ;00以外ならERROR
  F91C  C246F9        			JP		NZ,MERR
                      	
  F91F  110211        			LD		DE,FSIZE   ;FSIZE送信
  F922  1A            			LD		A,(DE)
  F923  CD5FF5        			CALL	SNDBYTE
  F926  13            			INC		DE
  F927  1A            			LD		A,(DE)
  F928  CD5FF5        			CALL	SNDBYTE
  F92B  CDEDF0        			CALL	DBRCV      ;FSIZE分のデータを受信し、SADRSから格納。分割ロードの場合、直前に0436HでOPENされたファイルを対象として256バイトずつSADRSが加算されて04F8HがCALLされる。
                      	
  F92E  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F931  A7            			AND		A          ;00以外ならERROR
  F932  C246F9        			JP		NZ,MERR
                      	
  F935  180A          			JR		MRET       ;正常RETURN
                      	
                      	;************************** 0588H VRFY CMT ベリファイ代替処理 *******************
  F937                	MVRFY:
  F937  F3            			DI
  F938  AF            			XOR		A          ;正常終了フラグ
                      	;		EI
                      	
  F939  C9            			RET
                      	
                      	;******* 代替処理用コマンドコード送信 (IN:A コマンドコード) **********
  F93A                	MCMD:
                      	;		PUSH	AF
                      	;		CALL	INIT
                      	;		POP		AF
  F93A  CD5FF5        			CALL	SNDBYTE    ;コマンドコード送信
  F93D  CD4CF5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F940  C9            			RET
                      	
                      	;****** 代替処理用正常RETURN処理 **********
  F941  E1            	MRET:	POP		HL
  F942  C1            			POP		BC
  F943  D1            			POP		DE
  F944  AF            			XOR		A          ;正常終了フラグ
                      	;		EI
                      			
  F945  C9            			RET
                      	
                      	;******* 代替処理用ERROR処理 **************
  F946                	MERR:
  F946  FEF0          			CP		0F0H
  F948  2005          			JR		NZ,MERR3
  F94A  1148F6        			LD		DE,MSG_F0
  F94D  180F          			JR		MERRMSG
                      	;代替処理ではファイル種類コードの判別なし
                      	;MERR2:	CP		0F2H
                      	;		JR		NZ,MERR3
                      	;		LD		DE,MSG_F2
                      	;		JR		MERRMSG
                      			
  F94F  FEF1          	MERR3:	CP		0F1H
  F951  2005          			JR		NZ,MERR99
  F953  1161F6        			LD		DE,MSG_F1
  F956  1806          			JR		MERRMSG
                      			
  F958  CDC303        	MERR99:	CALL	PRTBYT
  F95B  1184F7        			LD		DE,MSG99
                      			
  F95E                	MERRMSG:
  F95E  CD1500        			CALL	MSGPR
  F961  CD0600        			CALL	LETLN
  F964  E1            			POP		HL
  F965  C1            			POP		BC
  F966  D1            			POP		DE
  F967  3E02          			LD		A,02H
  F969  37            			SCF
                      	;		EI
                      	
  F96A  C9            			RET
                      	
  F96B                			END
