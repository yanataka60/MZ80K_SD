			  Z80 ASSEMBLER - ZASM VER 1.6
                      	;2021.12.12 MZ-700でFDP、FDMが文字化けする現象に対処
                      	;2022. 1.23 04D8H MONITOR リード インフォメーション代替処理のバグを修正
                      	;2022. 1.24 ファイルネームの後ろの20h詰めを0dhに修正するための処理をArduino側からMZ-80K側に修正
                      	;2022. 1.25 0475H MONITOR ライト データ代替処理、04F8H MONITOR リード データ代替処理での8255初期化を廃止
                      	;2022. 1.26 FDコマンドでロード可能なファイル種類コードは0x01のみとしていた制限を撤廃
                      	;2022. 1.29 CMT代替処理RETURN時の割込み許可(EI)を削除
                      	;
  0003                	GETL		EQU		0003H
  0006                	LETLN		EQU		0006H
  0009                	NEWLIN		EQU		0009H
  000C                	PRNTS		EQU		000CH
  0015                	MSGPR		EQU		0015H
  0018                	PLIST		EQU		0018H
  001B                	GETKEY		EQU		001BH
  03BA                	PRTWRD		EQU		03BAH
  03C3                	PRTBYT		EQU		03C3H
  0410                	HLHEX		EQU		0410H
  041F                	TWOHEX		EQU		041FH
  0BB9                	ADCN		EQU		0BB9H
  0DB5                	DISPCH		EQU		0DB5H
  0DDC                	DPCT		EQU		0DDCH
  10F0                	IBUFE		EQU		10F0H
  10F1                	FNAME		EQU		10F1H
  1102                	EADRS		EQU		1102H
  1102                	FSIZE		EQU		1102H
  1104                	SADRS		EQU		1104H
  1106                	EXEAD		EQU		1106H
  1171                	DSPX		EQU		1171H
  11A3                	LBUF		EQU		11A3H
  11AE                	MBUF		EQU		11AEH
  0082                	MONITOR_80K	EQU		0082H
  00AD                	MONITOR_700	EQU		00ADH
                      	
                      	;0D8H PORTA 送信データ(下位4ビット)
                      	;0D9H PORTB 受信データ(8ビット)
                      	;
                      	;0DAH PORTC Bit
                      	;7 IN  CHK
                      	;6 IN
                      	;5 IN
                      	;4 IN 
                      	;3 OUT
                      	;2 OUT FLG
                      	;1 OUT
                      	;0 OUT
                      	;
                      	;0DBH コントロールレジスタ
                      	
                      	
  F000                	       ORG		0F000H
                      	
  F000  00            			NOP                   ;ROM識別コード
  F001  C313F0        			JP		START
                      	;******************** MONITOR CMTルーチン代替 *************************************
  F004  C38FF7        	ENT1:	JP		MSHED
  F007  C3DCF7        	ENT2:	JP		MSDAT
  F00A  C30FF8        	ENT3:	JP		MLHED
  F00D  C389F8        	ENT4:	JP		MLDAT
  F010  C3BBF8        	ENT5:	JP		MVRFY
                      	
                      			
  F013  CD82F0        	START:	CALL	INIT
  F016  11A311        			LD		DE,LBUF     ;MZ-80K、MZ-700とも起動コマンドは'*FD'に統一
  F019  1A            			LD		A,(DE)
  F01A  FE2A          			CP		'*'
  F01C  C294F1        			JP		NZ,MON
  F01F  13            			INC 	DE
  F020  1A            			LD		A,(DE)
  F021  FE46          			CP		'F'
  F023  C294F1        			JP		NZ,MON
  F026  13            			INC		DE
  F027  1A            			LD		A,(DE)
  F028  FE44          			CP		'D'
  F02A  C294F1        			JP		NZ,MON
                      			
  F02D  13            			INC		DE          ;FDの次の文字へ移動
  F02E  1A            	STT2:	LD		A,(DE)
  F02F  FE20          			CP		20H         ;FDの後に1文字空白があれば以降をファイルネームとしてロード(ファイルネームは32文字まで)
  F031  285A          			JR		Z,SDLOAD
  F033  FE2F          			CP		'/'         ;FDの後が'/'なら以降をファイルネームとしてロード、実行はしない(ファイルネームは32文字まで)
  F035  2856          			JR		Z,SDLOAD
  F037  FE0D          			CP		0DH         ;FDだけで改行の場合にはDEFNAMEの文字列をファイルネームとしてロード
  F039  200D          			JR		NZ,STETC    ;該当なしなら他コマンドをチェック
  F03B  D5            	STT3:	PUSH	DE          ;設定ファイル名(0000.mzt)を転送
  F03C  2185F7        			LD		HL,DEFNAME
  F03F  13            			INC		DE
  F040  010500        			LD		BC,NEND-DEFNAME
  F043  EDB0          			LDIR
  F045  D1            			POP		DE
  F046  1845          			JR		SDLOAD      ;LOAD処理へ
  F048                	STETC:
  F048  FE53          			CP		'S'         ;FDS:SAVE処理へ
  F04A  CAF7F0        			JP		Z,STSV
  F04D  FE41          			CP		'A'			;FDA:自動起動ファイル設定処理へ
  F04F  CA0AF2        			JP		Z,STAS
  F052  FE4C          			CP		'L'         ;FDL:ファイル一覧表示
  F054  CA15F2        			JP		Z,STLT
  F057  FE44          			CP		'D'         ;FDD:DELETE
  F059  CAB5F2        			JP		Z,STDE
  F05C  FE52          			CP		'R'         ;FDR:RENAME
  F05E  CAF2F2        			JP		Z,STRN
  F061  FE50          			CP		'P'         ;FDP:DUMP
  F063  CA1FF3        			JP		Z,STPR
  F066  FE43          			CP		'C'         ;FDC:COPY
  F068  CAC4F3        			JP		Z,STCP
  F06B  FE4D          			CP		'M'         ;FDM:MEMORY DUMP
  F06D  CAF1F3        			JP		Z,STMD
  F070  FE57          			CP		'W'         ;FDW:MEMORY WRITE
  F072  CA76F4        			JP		Z,STMW
  F075  FE5A          			CP		'Z'         ;FDZ:MZ-700 PATCH START
  F077  CAC3F4        			JP		Z,STMZ
  F07A  FE55          			CP		'U'         ;FDU:MZ-700 裏RAM START
  F07C  CA32F5        			JP		Z,STURA
  F07F  C344F1        			JP		CMDERR
                      	
                      	;**** 8255初期化 ****
                      	;PORTC下位BITをOUTPUT、上位BITをINPUT、PORTBをINPUT、PORTAをOUTPUT
  F082  3E8A          	INIT:	LD		A,8AH
  F084  D3DB          			OUT		(0DBH),A
                      	;出力BITをリセット
  F086  3E00          	INIT2:	LD		A,00H      ;PORTA <- 0
  F088  D3D8          			OUT		(0D8H),A
  F08A  D3DA          			OUT		(0DAH),A   ;PORTC <- 0
  F08C  C9            			RET
                      	
                      	;**** LOAD ****
                      	;受信ヘッダ情報をセットし、SDカードからLOAD実行
  F08D  3E81          	SDLOAD:	LD		A,81H  ;LOADコマンド81H
  F08F  CDB5F5        			CALL	STCMD
  F092  CDA4F0        			CALL	HDRCV      ;ヘッダ情報受信
  F095  CDE5F0        			CALL	DBRCV      ;データ受信
  F098  3AA611        			LD		A,(LBUF+3)
  F09B  FE2F          			CP		'/'        ;'*FD/'であれば実行アドレスに飛ばずにMONITORコマンド待ちに戻る
  F09D  CA94F1        			JP		Z,MON
  F0A0  2A0611        			LD		HL,(EXEAD)
  F0A3  E9            			JP		(HL)
                      	
                      	;ヘッダ受信
  F0A4  21F110        	HDRCV:	LD		HL,FNAME
  F0A7  0611          			LD		B,11H
  F0A9  CD49F5        	HDRC1:	CALL	RCVBYTE    ;ファイルネーム受信
  F0AC  77            			LD		(HL),A
  F0AD  23            			INC		HL
  F0AE  05            			DEC		B
  F0AF  20F8          			JR		NZ,HDRC1
  F0B1  11C9F5        			LD		DE,MSG_LD  ;ファイルネームLOADING表示
  F0B4  CD1500        			CALL	MSGPR
  F0B7  11F110        			LD		DE,FNAME
  F0BA  CD1500        			CALL	MSGPR
  F0BD  CD0600        			CALL	LETLN
  F0C0  210411        			LD		HL,SADRS  ;SADRS取得
  F0C3  CD49F5        			CALL	RCVBYTE
  F0C6  77            			LD		(HL),A
  F0C7  23            			INC		HL
  F0C8  CD49F5        			CALL	RCVBYTE
  F0CB  77            			LD		(HL),A
  F0CC  210211        			LD		HL,FSIZE   ;FSIZE取得
  F0CF  CD49F5        			CALL	RCVBYTE
  F0D2  77            			LD		(HL),A
  F0D3  23            			INC		HL
  F0D4  CD49F5        			CALL	RCVBYTE
  F0D7  77            			LD		(HL),A
  F0D8  210611        			LD		HL,EXEAD   ;EXEAD取得
  F0DB  CD49F5        			CALL	RCVBYTE
  F0DE  77            			LD		(HL),A
  F0DF  23            			INC		HL
  F0E0  CD49F5        			CALL	RCVBYTE
  F0E3  77            			LD		(HL),A
  F0E4  C9            			RET
                      	
                      	;データ受信
  F0E5  ED5B0211      	DBRCV:	LD		DE,(FSIZE)
  F0E9  2A0411        			LD		HL,(SADRS)
  F0EC  CD49F5        	DBRLOP:	CALL	RCVBYTE
  F0EF  77            			LD		(HL),A
  F0F0  1B            			DEC		DE
  F0F1  7A            			LD		A,D
  F0F2  B3            			OR		E
  F0F3  23            			INC		HL
  F0F4  20F6          			JR		NZ,DBRLOP   ;DE=0までLOOP
  F0F6  C9            			RET
                      	
                      	;**** SAVE ****
  F0F7  13            	STSV:	INC		DE
  F0F8  13            			INC		DE
  F0F9  D5            			PUSH	DE
  F0FA  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればSADRSにセットして続行
  F0FD  383B          			JR		C,STSV1
                      	
  F0FF  220411        			LD		(SADRS),HL      ;SARDS保存
  F102  D1            			POP		DE
  F103  13            			INC		DE
  F104  13            			INC		DE
  F105  13            			INC		DE
  F106  13            			INC		DE
  F107  13            			INC		DE
  F108  D5            			PUSH	DE          ;5文字進めて4桁の16進数であればEADRSにセットして続行
  F109  CD1004        			CALL	HLHEX
  F10C  382C          			JR		C,STSV1
  F10E  E5            			PUSH	HL
  F10F  ED4B0411      			LD		BC,(SADRS)
  F113  ED42          			SBC		HL,BC       ;EADRSがSADRSより大きくない場合はエラー
  F115  E1            			POP		HL
  F116  2822          			JR		Z,STSV1
  F118  3820          			JR		C,STSV1
                      	
  F11A  220211        			LD		(EADRS),HL      ;EADRS保存
  F11D  D1            			POP		DE
  F11E  13            			INC		DE
  F11F  13            			INC		DE
  F120  13            			INC		DE
  F121  13            			INC		DE
  F122  13            			INC		DE          ;5文字進めて4桁の16進数であればEXEADにセットして続行
  F123  D5            			PUSH	DE
  F124  CD1004        			CALL	HLHEX
  F127  3811          			JR		C,STSV1
                      			
  F129  220611        			LD		(EXEAD),HL      ;EXEAD保存
  F12C  D1            			POP		DE
  F12D  13            			INC		DE
  F12E  13            			INC		DE
  F12F  13            			INC		DE
  F130  13            			INC		DE
  F131  13            			INC		DE			;5文字進めてファイルネームがあれば続行
  F132  1A            			LD		A,(DE)
  F133  FE31          			CP		31H
  F135  3808          			JR		C,STSV2
  F137  EB            			EX		DE,HL
  F138  180F          			JR		SDSAVE      ;SAVE処理へ
  F13A                	STSV1:                      ;16進数4桁の取得に失敗又はEADRSがSARDSより大きくない
  F13A  1113F6        			LD		DE,MSG_AD
  F13D  184F          			JR		ERRMSG
  F13F                	STSV2:                      ;ファイルネームの取得に失敗
  F13F  1123F6        			LD		DE,MSG_FNAME
  F142  184A          			JR		ERRMSG
  F144                	CMDERR:                     ;コマンド異常
  F144  1135F6        			LD		DE,MSG_CMD
  F147  1845          			JR		ERRMSG
                      	
                      	;送信ヘッダ情報をセットし、SDカードへSAVE実行
  F149  3E80          	SDSAVE:	LD		A,80H      ;SAVEコマンド80H
  F14B  CD9BF5        			CALL	STCD
  F14E  A7            			AND		A          ;00以外ならERROR
  F14F  C264F1        			JP		NZ,SVERR
  F152  CDAEF1        			CALL	HDSEND     ;ヘッダ情報送信
  F155  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F158  A7            			AND		A          ;00以外ならERROR
  F159  2009          			JR		NZ,SVERR
  F15B  CDF3F1        			CALL	DBSEND     ;データ送信
  F15E  11DCF5        			LD		DE,MSG_SV
  F161  182B          			JR		ERRMSG
                      	
  F163                	SVER0:
  F163  D1            			POP		DE         ;CALL元STACKを破棄する
  F164                	SVERR:
  F164  FEF0          			CP		0F0H
  F166  2005          			JR		NZ,ERR3
  F168  1145F6        			LD		DE,MSG_F0  ;SD-CARD INITIALIZE ERROR
  F16B  1821          			JR		ERRMSG
                      	;FDコマンドでロード可能なファイル種類コードは0x01のみとしていた制限を撤廃
                      	;ERR2:	CP		0F2H
                      	;		JR		NZ,ERR3
                      	;		LD		DE,MSG_F2  ;NOT OBJECT FILE
                      	;		JR		ERRMSG
  F16D  FEF1          	ERR3:	CP		0F1H
  F16F  2005          			JR		NZ,ERR4
  F171  115EF6        			LD		DE,MSG_F1  ;NOT FIND FILE
  F174  1818          			JR		ERRMSG
  F176  FEF3          	ERR4:	CP		0F3H
  F178  2005          			JR		NZ,ERR5
  F17A  116CF6        			LD		DE,MSG_F3  ;FILE EXIST
  F17D  180F          			JR		ERRMSG
  F17F  FEF4          	ERR5:	CP		0F4H
  F181  2005          			JR		NZ,ERR99
  F183  1135F6        			LD		DE,MSG_CMD
  F186  1806          			JR		ERRMSG
  F188  CDC303        	ERR99:	CALL	PRTBYT
  F18B  117EF7        			LD		DE,MSG99   ;その他ERROR
  F18E  CD1500        	ERRMSG:	CALL	MSGPR
  F191  CD0600        			CALL	LETLN
  F194  214E01        	MON:	LD		HL,014EH
  F197  7E            			LD		A,(HL)
  F198  FE50          			CP		'P'             ;014EHが'P'ならMZ-80K
  F19A  CA8200        			JP		Z,MONITOR_80K
  F19D  FE4E          			CP		'N'             ;014EHが'N'ならFN-700
  F19F  CA8200        			JP		Z,MONITOR_80K
  F1A2  21EB06        			LD		HL,06EBH
  F1A5  7E            			LD		A,(HL)
  F1A6  FE4D          			CP		'M'             ;06EBHが'M'ならMZ-700
  F1A8  CAAD00        			JP		Z,MONITOR_700
  F1AB  C30000        			JP		0000H           ;識別できなかったら0000Hへジャンプ
                      	
                      	;ヘッダ送信
  F1AE  E5            	HDSEND:	PUSH	HL
  F1AF  0620          			LD		B,20H
  F1B1  7E            	SS1:	LD		A,(HL)     ;FNAME送信
  F1B2  CD5CF5        			CALL	SNDBYTE
  F1B5  23            			INC		HL
  F1B6  05            			DEC		B
  F1B7  20F8          			JR		NZ,SS1
  F1B9  3E0D          			LD		A,0DH
  F1BB  CD5CF5        			CALL	SNDBYTE
  F1BE  E1            			POP		HL
  F1BF  0610          			LD		B,10H
  F1C1  7E            	SS2:	LD		A,(HL)     ;PNAME送信
  F1C2  CD5CF5        			CALL	SNDBYTE
  F1C5  23            			INC		HL
  F1C6  05            			DEC		B
  F1C7  20F8          			JR		NZ,SS2
  F1C9  3E0D          			LD		A,0DH
  F1CB  CD5CF5        			CALL	SNDBYTE
  F1CE  210411        			LD		HL,SADRS   ;SADRS送信
  F1D1  7E            			LD		A,(HL)
  F1D2  CD5CF5        			CALL	SNDBYTE
  F1D5  23            			INC		HL
  F1D6  7E            			LD		A,(HL)
  F1D7  CD5CF5        			CALL	SNDBYTE
  F1DA  210211        			LD		HL,EADRS   ;EADRS送信
  F1DD  7E            			LD		A,(HL)
  F1DE  CD5CF5        			CALL	SNDBYTE
  F1E1  23            			INC		HL
  F1E2  7E            			LD		A,(HL)
  F1E3  CD5CF5        			CALL	SNDBYTE
  F1E6  210611        			LD		HL,EXEAD   ;EXEAD送信
  F1E9  7E            			LD		A,(HL)
  F1EA  CD5CF5        			CALL	SNDBYTE
  F1ED  23            			INC		HL
  F1EE  7E            			LD		A,(HL)
  F1EF  CD5CF5        			CALL	SNDBYTE
  F1F2  C9            			RET
                      	
                      	;データ送信
                      	;SADRSからEADRSまでを送信
  F1F3  2A0211        	DBSEND:	LD		HL,(EADRS)
  F1F6  EB            			EX		DE,HL
  F1F7  2A0411        			LD		HL,(SADRS)
  F1FA  7E            	DBSLOP:	LD		A,(HL)
  F1FB  CD5CF5        			CALL	SNDBYTE
  F1FE  7C            			LD		A,H
  F1FF  BA            			CP		D
  F200  2004          			JR		NZ,DBSLP1
  F202  7D            			LD		A,L
  F203  BB            			CP		E
  F204  2803          			JR		Z,DBSLP2   ;HL = DE までLOOP
  F206  23            	DBSLP1:	INC		HL
  F207  18F1          			JR		DBSLOP
  F209  C9            	DBSLP2:	RET
                      	
                      	;**** AUTO START SET ****
  F20A  3E82          	STAS:	LD		A,82H      ;AUTO START SETコマンド82H
  F20C  CDB5F5        			CALL	STCMD
  F20F  11EBF5        			LD		DE,MSG_AS
  F212  C38EF1        			JP		ERRMSG
                      	
                      	;**** DIRLIST ****
  F215  13            	STLT:	INC		DE
  F216  1A            			LD		A,(DE)
  F217  FE0D          			CP		0DH
  F219  2004          			JR		NZ,STLT1
  F21B  3E30          			LD		A,30H
  F21D  1819          			JR		DIRLIST    ;FDLだけなら'30H'を送信(全ファイルを表示)
  F21F  13            	STLT1:	INC		DE         ;FDLの後に数字一文字があれば'31H'〜'39H','41H'〜'5AH'を送信(20件を1ページとしてnページを表示)
  F220  1A            			LD		A,(DE)
  F221  FE30          			CP		30H
  F223  DA44F1        			JP		C,CMDERR
  F226  FE3A          			CP		3AH
  F228  3002          			JR		NC,STLT2
  F22A  180C          			JR		DIRLIST
  F22C  FE41          	STLT2:	CP		41H        ;念のためA〜Zにも対応
  F22E  DA44F1        			JP		C,CMDERR
  F231  FE5B          			CP		5BH
  F233  D244F1        			JP		NC,CMDERR
  F236  D607          			SUB		07H
  F238                	DIRLIST:
  F238  F5            			PUSH	AF
  F239  3E83          			LD		A,83H      ;DIRLISTコマンド83Hを送信
  F23B  CD9BF5        			CALL	STCD       ;コマンドコード送信
  F23E  A7            			AND		A          ;00以外ならERROR
  F23F  C264F1        			JP		NZ,SVERR
  F242  F1            			POP		AF
  F243  CD5CF5        			CALL	SNDBYTE           ;ページ指示を送信
  F246  218AF7        	DL1:	LD		HL,DEFDIR         ;行頭に'*FD 'を付けることでカーソルを移動させリターンで実行できるように
  F249  11A311        			LD		DE,LBUF
  F24C  010500        			LD		BC,DEND-DEFDIR
  F24F  EDB0          			LDIR
  F251  EB            			EX		DE,HL
  F252  CD49F5        	DL2:	CALL	RCVBYTE           ;'00H'を受信するまでを一行とする
  F255  FE00          			CP		00H
  F257  280C          			JR		Z,DL3
  F259  FEFF          			CP		0FFH              ;'0FFH'を受信したら終了
  F25B  2813          			JR		Z,DL4
  F25D  FEFE          			CP		0FEH              ;'0FEH'を受信したら一時停止して一文字入力待ち
  F25F  2819          			JR		Z,DL5
  F261  77            			LD		(HL),A
  F262  23            			INC		HL
  F263  18ED          			JR		DL2
  F265  11A311        	DL3:	LD		DE,LBUF           ;'00H'を受信したら一行分を表示して改行
  F268  CD1500        			CALL	MSGPR
  F26B  CD0600        			CALL	LETLN
  F26E  18D6          			JR		DL1
  F270  CD49F5        	DL4:	CALL	RCVBYTE           ;状態取得(00H=OK)
  F273  A7            			AND		A                 ;00以外ならERROR
  F274  C264F1        			JP		NZ,SVERR
  F277  C394F1        			JP		MON
                      	
  F27A  1177F6        	DL5:	LD		DE,MSG_KEY1        ;HIT ANT KEY表示
  F27D  CD1500        			CALL	MSGPR
  F280  3EC2          			LD		A,0C2H
  F282  CDB50D        			CALL	DISPCH
  F285  118AF6        			LD		DE,MSG_KEY2        ;HIT ANT KEY表示
  F288  CD1500        			CALL	MSGPR
  F28B  CD0600        			CALL	LETLN
  F28E  CD1B00        	DL6:	CALL	GETKEY            ;1文字入力待ち
  F291  FE00          			CP		00H
  F293  28F9          			JR		Z,DL6
  F295  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F297  2815          			JR		Z,DL7
  F299  FE12          			CP		12H               ;カーソル↑で打ち切り
  F29B  2807          			JR		Z,DL9
  F29D  CD0600        			CALL	LETLN             ;それ以外で継続
  F2A0  3E00          			LD		A,00H
  F2A2  180C          			JR		DL8
  F2A4  3EC2          	DL9:	LD		A,0C2H            ;カーソル↑で打ち切ったときにカーソル2行上へ
  F2A6  CDDC0D        			CALL	DPCT
  F2A9  3EC2          			LD		A,0C2H
  F2AB  CDDC0D        			CALL	DPCT
  F2AE  3EFF          	DL7:	LD		A,0FFH            ;0FFH中断コードを送信
  F2B0  CD5CF5        	DL8:	CALL	SNDBYTE
  F2B3  189D          			JR		DL2
                      	
                      	;**** FILE DELETE ****
  F2B5  3E84          	STDE:	LD		A,84H      ;FILE DELETEコマンド84H
  F2B7  CDB5F5        			CALL	STCMD
                      	
  F2BA  119BF6        			LD		DE,MSG_DELQ ;'DELETE?'表示
  F2BD  CD1500        			CALL	MSGPR
  F2C0  CD0600        			CALL	LETLN
  F2C3  CD1B00        	STDE3:	CALL	GETKEY
  F2C6  FE00          			CP		00H
  F2C8  28F9          			JR		Z,STDE3
  F2CA  FE59          			CP		59H         ;'Y'ならOKとして00Hを送信
  F2CC  2004          			JR		NZ,STDE4
  F2CE  3E00          			LD		A,00H
  F2D0  1802          			JR		STDE5
  F2D2  3EFF          	STDE4:	LD		A,0FFH      ;'Y'以外ならCANSELとして0FFHを送信
  F2D4  CD5CF5        	STDE5:	CALL	SNDBYTE
  F2D7  CD49F5        			CALL	RCVBYTE
  F2DA  FE00          			CP		00H         ;00Hを受信すればDELETE完了
  F2DC  2005          			JR		NZ,STDE6
  F2DE  11BAF6        			LD		DE,MSG_DELY ;'DELETE OK'表示
  F2E1  180C          			JR		STDE8
  F2E3  FE01          	STDE6:	CP		01H         ;01Hを受信すればCANSEL完了
  F2E5  2005          			JR		NZ,STDE7
  F2E7  11C4F6        			LD		DE,MSG_DELN ;'DELETE CANSEL'表示
  F2EA  1803          			JR		STDE8
  F2EC  C364F1        	STDE7:	JP		SVERR
  F2EF  C38EF1        	STDE8:	JP		ERRMSG
                      	
                      	;**** FILE RENAME ****
  F2F2  3E85          	STRN:	LD		A,85H      ;FILE RENAMEコマンド85H
  F2F4  CDB5F5        			CALL	STCMD
                      	
  F2F7  11D2F6        			LD		DE,MSG_REN ;'NEW NAME:'表示
  F2FA  CD1500        			CALL	MSGPR
                      			
  F2FD  3E09          			LD		A,09H
  F2FF  327111        			LD		(DSPX),A  ;カーソル位置を'NEW NAME:'の次へ
  F302  11A311        			LD		DE,LBUF    ;NEW FILE NAMEを取得
  F305  CD0300        			CALL	GETL
  F308  11AB11        			LD		DE,LBUF+8  ;NEW FILE NAMEを送信
  F30B  CD8CF5        			CALL	STFN
  F30E  CDA2F5        			CALL	STFS
                      			
  F311  CD49F5        			CALL	RCVBYTE
  F314  FE00          			CP		00H         ;00Hを受信すればRENAME完了
  F316  C264F1        			JP		NZ,SVERR
  F319  111EF7        			LD		DE,MSG_RENY
  F31C  C38EF1        			JP		ERRMSG
                      	
                      	;**** FILE DUMP ****
  F31F  3E86          	STPR:	LD		A,86H      ;FILE DUMPEコマンド86H
  F321  CDB5F5        			CALL	STCMD
                      	
                      	;		LD		A,0C6H     ;画面クリア
                      	;		CALL	DPCT
  F324  210411        	STPR6:	LD		HL,SADRS   ;SADRS取得
  F327  CD49F5        			CALL	RCVBYTE
  F32A  77            			LD		(HL),A
  F32B  23            			INC		HL
  F32C  CD49F5        			CALL	RCVBYTE
  F32F  77            			LD		(HL),A
  F330  2A0411        			LD		HL,(SADRS)
  F333  7C            			LD		A,H
  F334  FEFF          			CP		0FFH        ;ADRSに0FFFFHが送信されてきたらDUMP処理終了
  F336  2008          			JR		NZ,STPR7
  F338  7D            			LD		A,L
  F339  FEFF          			CP		0FFH
  F33B  2003          			JR		NZ,STPR7
  F33D  C3BEF3        			JP		STPR8
  F340  1128F7        	STPR7:	LD		DE,MSG_AD1 ;DUMP TITLE表示
  F343  CD1500        			CALL	MSGPR
  F346  CD0600        			CALL	LETLN
  F349  0E10          			LD		C,10H      ;16行(128Byte)を表示
  F34B  C5            	STPR0:	PUSH	BC
  F34C  0608          			LD		B,08H      ;一行(8Byte)を受信
  F34E  21A311        			LD		HL,LBUF
  F351  CD49F5        	STPR1:	CALL	RCVBYTE
  F354  77            			LD		(HL),A
  F355  05            			DEC		B
  F356  23            			INC		HL
  F357  20F8          			JR		NZ,STPR1
                      	
  F359  2A0411        			LD		HL,(SADRS) ;アドレス表示
  F35C  CDBA03        			CALL	PRTWRD
  F35F  110800        			LD		DE,0008H   ;一画面(128Byte)中はアドレスを受け取らないので自前でインクリメント
  F362  19            			ADD		HL,DE
  F363  220411        			LD		(SADRS),HL
                      			
  F366  0608          			LD		B,08H      ;一行(8Byte)のデータを16進数表示
  F368  11A311        			LD		DE,LBUF
  F36B  CD0C00        	STPR2:	CALL	PRNTS
  F36E  1A            			LD		A,(DE)
  F36F  CDC303        			CALL	PRTBYT
  F372  13            			INC		DE
  F373  05            			DEC		B
  F374  20F5          			JR		NZ,STPR2
                      			
  F376  CD0C00        			CALL	PRNTS
  F379  11A311        			LD		DE,LBUF    ;一行(8Byte)のデータをキャラクタ表示
  F37C  0608          			LD		B,08H
  F37E  1A            	STPR9:	LD		A,(DE)
  F37F  FE10          			CP		10H        ;MZ-700での文字化けに対処
  F381  3002          			JR		NC,STPRA
  F383  3E20          			LD		A,20H
  F385  CDB90B        	STPRA:	CALL	ADCN
  F388  CDB50D        			CALL	DISPCH
  F38B  13            			INC		DE
  F38C  05            			DEC		B
  F38D  20EF          			JR		NZ,STPR9
                      	
  F38F  CD0600        			CALL	LETLN
  F392  C1            			POP		BC
  F393  0D            			DEC		C
  F394  20B5          			JR		NZ,STPR0
                      			
  F396  114EF7        			LD		DE,MSG_AD2        ;入力待ちメッセージ表示
  F399  CD1500        			CALL	MSGPR
  F39C  CD0600        			CALL	LETLN
  F39F  CD0600        			CALL	LETLN
  F3A2  CD1B00        	STPR3:	CALL	GETKEY            ;1文字入力待ち
  F3A5  FE00          			CP		00H
  F3A7  28F9          			JR		Z,STPR3
  F3A9  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F3AB  2806          			JR		Z,STPR4
  F3AD  CD5CF5        			CALL	SNDBYTE           ;SHIFT+BREAK以外はASCIIコードのまま送信、ARDUINO側で'B'を処理
  F3B0  C324F3        			JP		STPR6
  F3B3  3EFF          	STPR4:	LD		A,0FFH            ;0FFH中断コードを送信
  F3B5  CD5CF5        	STPR5:	CALL	SNDBYTE
  F3B8  CD49F5        			CALL	RCVBYTE           ;SHIFT+BREAKでの中断時はADRS'0FFFFH'の受信及び状態コードの受信を破棄
  F3BB  CD49F5        			CALL	RCVBYTE
  F3BE  CD49F5        	STPR8:	CALL	RCVBYTE
  F3C1  C394F1        			JP		MON
                      	
                      	;**** FILE COPY ****
  F3C4  3E87          	STCP:	LD		A,87H      ;FILE COPYコマンド87H
  F3C6  CDB5F5        			CALL	STCMD
  F3C9  11D2F6        			LD		DE,MSG_REN ;'NEW NAME:'表示
  F3CC  CD1500        			CALL	MSGPR
                      			
  F3CF  3E09          			LD		A,09H
  F3D1  327111        			LD		(DSPX),A    ;カーソル位置を'NEW NAME:'の次へ
  F3D4  11A311        			LD		DE,LBUF      ;NEW FILE NAMEを取得
  F3D7  CD0300        			CALL	GETL
  F3DA  11AB11        			LD		DE,LBUF+8    ;NEW FILE NAMEを送信
  F3DD  CD8CF5        			CALL	STFN
  F3E0  CDA2F5        			CALL	STFS
                      			
  F3E3  CD49F5        			CALL	RCVBYTE
  F3E6  FE00          			CP		00H         ;00Hを受信すればRENAME完了
  F3E8  C264F1        			JP		NZ,SVERR
  F3EB  1170F7        			LD		DE,MSG_CPY
  F3EE  C38EF1        			JP		ERRMSG
                      	
                      	;**** MEMORY DUMP ****
  F3F1  13            	STMD:	INC		DE
  F3F2  13            			INC		DE
  F3F3  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればSADRSにセットして続行
  F3F6  DA3AF1        			JP		C,STSV1
  F3F9  220411        			LD		(SADRS),HL      ;SARDS保存
                      	
  F3FC  1128F7        	STMD6:	LD		DE,MSG_AD1 ;DUMP TITLE表示
  F3FF  CD1500        			CALL	MSGPR
  F402  CD0600        			CALL	LETLN
  F405  0E10          			LD		C,10H      ;16行(128Byte)を表示
  F407  2A0411        	STMD7:	LD		HL,(SADRS) ;アドレス表示
  F40A  CDBA03        			CALL	PRTWRD
  F40D  CD0C00        			CALL	PRNTS
                      	
                      			
  F410  0608          			LD		B,08H      ;一行(8Byte)のデータを16進数表示
  F412  7E            	STMD0:	LD		A,(HL)
  F413  CDC303        			CALL	PRTBYT
  F416  CD0C00        			CALL	PRNTS
  F419  CD1B00        			CALL	GETKEY
  F41C  FE64          			CP		64H
  F41E  2853          			JR		Z,STMD4
  F420  23            			INC		HL
  F421  05            			DEC		B
  F422  20EE          			JR		NZ,STMD0
                      	
  F424  2A0411        			LD		HL,(SADRS)
  F427  0608          			LD		B,08H      ;一行(8Byte)のデータをキャラクタ表示
  F429  7E            	STMD2:	LD		A,(HL)
  F42A  FE10          			CP		10H        ;MZ-700での文字化けに対処
  F42C  3002          			JR		NC,STMD8
  F42E  3E20          			LD		A,20H
  F430  CDB90B        	STMD8:	CALL	ADCN
  F433  CDB50D        			CALL	DISPCH
  F436  CD1B00        			CALL	GETKEY
  F439  FE64          			CP		64H        ;表示途中でもSHIFT+BREAKで打ち切り
  F43B  2836          			JR		Z,STMD4
  F43D  23            			INC		HL
  F43E  05            			DEC		B
  F43F  20E8          			JR		NZ,STMD2
                      	
  F441  220411        			LD		(SADRS),HL
  F444  CD0600        			CALL	LETLN
                      	
  F447  0D            			DEC		C
  F448  20BD          			JR		NZ,STMD7
                      			
  F44A  114EF7        			LD		DE,MSG_AD2        ;入力待ちメッセージ表示
  F44D  CD1500        			CALL	MSGPR
  F450  CD0600        			CALL	LETLN
  F453  CD0600        			CALL	LETLN
  F456  CD1B00        	STMD3:	CALL	GETKEY            ;1文字入力待ち
  F459  FE00          			CP		00H
  F45B  28F9          			JR		Z,STMD3
  F45D  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F45F  2812          			JR		Z,STMD4
  F461  FE42          			CP		42H
  F463  200B          			JR		NZ,STMD5
  F465  2A0411        			LD		HL,(SADRS)
  F468  110001        			LD		DE,0100H
  F46B  ED52          			SBC		HL,DE
  F46D  220411        			LD		(SADRS),HL
  F470  C3FCF3        	STMD5:	JP		STMD6
  F473  C394F1        	STMD4:	JP		MON
                      	
                      	;**** MEMORY WRITE ****
  F476  13            	STMW:	INC		DE
  F477  13            			INC		DE
  F478  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればHLにセットして続行
  F47B  DA3AF1        			JP		C,STSV1
                      	
  F47E  13            			INC		DE
  F47F  13            			INC		DE
  F480  13            			INC		DE
  F481  13            			INC		DE
  F482  1A            	STSP1:	LD		A,(DE)
  F483  FE0D          			CP		0DH
  F485  2839          			JR		Z,STMW9     ;アドレスのみなら終了
  F487  FE20          			CP		20H
  F489  2003          			JR		NZ,STMW1
  F48B  13            			INC		DE          ;空白は飛ばす
  F48C  18F4          			JR		STSP1
                      	
  F48E                	STMW1:
  F48E  CD1F04        			CALL	TWOHEX
  F491  380E          			JR		C,STMW8
  F493  77            			LD		(HL),A      ;2桁の16進数があれば(HL)に書き込み
  F494  23            			INC		HL
                      	
  F495  1A            	STSP2:	LD		A,(DE)
  F496  FE0D          			CP		0DH         ;一行終了
  F498  2807          			JR		Z,STMW8
  F49A  FE20          			CP		20H
  F49C  20F0          			JR		NZ,STMW1
  F49E  13            			INC		DE          ;空白は飛ばす
  F49F  18F4          			JR		STSP2
                      	
  F4A1                	STMW8:	
  F4A1  1178F7        			LD		DE,MSG_FDW  ;行頭に'*FDW '
  F4A4  CD1500        			CALL	MSGPR
  F4A7  CDBA03        			CALL	PRTWRD      ;アドレス表示
  F4AA  CD0C00        			CALL	PRNTS
  F4AD  11A311        			LD		DE,LBUF     ;一行入力
  F4B0  CD0300        			CALL	GETL
  F4B3  11A311        			LD		DE,LBUF
  F4B6  1A            			LD		A,(DE)
  F4B7  FE1B          			CP		1BH
  F4B9  2805          			JR		Z,STMW9     ;SHIFT+BREAKで破棄、終了
  F4BB  11A611        			LD		DE,LBUF+3
  F4BE  18B6          			JR		STMW
  F4C0  C394F1        	STMW9:	JP		MON
                      	
                      	;**** MZ-700 PATCH START ****
  F4C3  F3            	STMZ:	DI
  F4C4  210000        			LD		HL,0000H      ;ROMを2000Hにコピー
  F4C7  110020        			LD		DE,2000H
  F4CA  010010        			LD		BC,1000H
  F4CD  EDB0          			LDIR
  F4CF  D3E0          			OUT		(0E0H),A      ;裏RAM ON
  F4D1  210020        			LD		HL,2000H      ;裏RAMにROMの内容をコピー
  F4D4  110000        			LD		DE,0000H
  F4D7  010010        			LD		BC,1000H
  F4DA  EDB0          			LDIR
  F4DC  2105F5        			LD		HL,STMZ2      ;書き換えアドレス
  F4DF  1123F5        			LD		DE,STMZ3      ;書き換えデータ
  F4E2  060F          			LD		B,0FH
  F4E4  C5            	STMZ1:	PUSH	BC
  F4E5  4E            			LD		C,(HL)
  F4E6  23            			INC		HL
  F4E7  46            			LD		B,(HL)
  F4E8  1A            			LD		A,(DE)
  F4E9  02            			LD		(BC),A
  F4EA  C1            			POP		BC
  F4EB  13            			INC		DE
  F4EC  23            			INC		HL
  F4ED  05            			DEC		B
  F4EE  20F4          			JR		NZ,STMZ1
  F4F0  21AD00        			LD		HL,00ADH
  F4F3  7E            			LD		A,(HL)
  F4F4  FECD          			CP		0CDH
  F4F6  C20000        			JP		NZ,0000H         ;MZ-700と判断できなければ0000Hからスタート
  F4F9  11FCF5        			LD		DE,MSG_ST
  F4FC  CD1500        			CALL	MSGPR
  F4FF  CD0600        			CALL	LETLN
  F502  C3AD00        			JP		MONITOR_700      ;MZ-700と判断できれば00ADHからスタート
                      	
  F505  370438043904  	STMZ2:	DW		0437H,0438H,0439H
  F50B  760477047804  			DW		0476H,0477H,0478H
  F511  D904DA04DB04  			DW		04D9H,04DAH,04DBH
  F517  F904FA04FB04  			DW		04F9H,04FAH,04FBH
  F51D  89058A058B05  			DW		0589H,058AH,058BH
                      	
  F523  C3            	STMZ3:	DB		0C3H
  F524  04F0          			DW		ENT1
  F526  C3            			DB		0C3H
  F527  07F0          			DW		ENT2
  F529  C3            			DB		0C3H
  F52A  0AF0          			DW		ENT3
  F52C  C3            			DB		0C3H
  F52D  0DF0          			DW		ENT4
  F52F  C3            			DB		0C3H
  F530  10F0          			DW		ENT5
                      	
                      	;**** MZ-700 裏RAM START ****
  F532  D3E0          	STURA:	OUT		(0E0H),A      ;裏RAM ON
  F534  21AD00        			LD		HL,00ADH
  F537  7E            			LD		A,(HL)
  F538  FECD          			CP		0CDH
  F53A  C20000        			JP		NZ,0000H         ;0CDHでなければNZ-700などと判断して0000Hからスタート
  F53D  11FCF5        			LD		DE,MSG_ST
  F540  CD1500        			CALL	MSGPR
  F543  CD0600        			CALL	LETLN
  F546  C3AD00        			JP		MONITOR_700      ;(00ADH)が0CDHなら1Z-009A又は1Z-009Bのパッチ済みMONITORと判断して00ADHからスタート
                      	
                      	;**** 1BYTE受信 ****
                      	;受信DATAをAレジスタにセットしてリターン
  F549                	RCVBYTE:
  F549  CD7EF5        			CALL	F1CHK      ;PORTC BIT7が1になるまでLOOP
  F54C  DBD9          			IN		A,(0D9h)   ;PORTB -> A
  F54E  F5            			PUSH 	AF
  F54F  3E05          			LD		A,05H
  F551  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 1
  F553  CD85F5        			CALL	F2CHK      ;PORTC BIT7が0になるまでLOOP
  F556  3E04          			LD		A,04H
  F558  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 0
  F55A  F1            			POP 	AF
  F55B  C9            			RET
                      			
                      	;**** 1BYTE送信 ****
                      	;Aレジスタの内容をPORTA下位4BITに4BITずつ送信
  F55C                	SNDBYTE:
  F55C  F5            			PUSH	AF
  F55D  1F            			RRA
  F55E  1F            			RRA
  F55F  1F            			RRA
  F560  1F            			RRA
  F561  E60F          			AND		0FH
  F563  CD6DF5        			CALL	SND4BIT
  F566  F1            			POP		AF
  F567  E60F          			AND		0FH
  F569  CD6DF5        			CALL	SND4BIT
  F56C  C9            			RET
                      	
                      	;**** 4BIT送信 ****
                      	;Aレジスタ下位4ビットを送信する
  F56D                	SND4BIT:
  F56D  D3D8          			OUT		(0D8H),A
  F56F  3E05          			LD		A,05H
  F571  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 1
  F573  CD7EF5        			CALL	F1CHK      ;PORTC BIT7が1になるまでLOOP
  F576  3E04          			LD		A,04H
  F578  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 0
  F57A  CD85F5        			CALL	F2CHK
  F57D  C9            			RET
                      			
                      	;**** BUSYをCHECK(1) ****
                      	; 82H BIT7が1になるまでLOP
  F57E  DBDA          	F1CHK:	IN		A,(0DAH)
  F580  E680          			AND		80H        ;PORTC BIT7 = 1?
  F582  28FA          			JR		Z,F1CHK
  F584  C9            			RET
                      	
                      	;**** BUSYをCHECK(0) ****
                      	; 82H BIT7が0になるまでLOOP
  F585  DBDA          	F2CHK:	IN		A,(0DAH)
  F587  E680          			AND		80H        ;PORTC BIT7 = 0?
  F589  20FA          			JR		NZ,F2CHK
  F58B  C9            			RET
                      	
                      	;****** FILE NAME 取得 (IN:DE コマンド文字の次の文字 OUT:HL ファイルネームの先頭)*********
  F58C  F5            	STFN:	PUSH	AF
  F58D  13            	STFN1:	INC		DE         ;ファイルネームまでスペース読み飛ばし
  F58E  1A            			LD		A,(DE)
  F58F  FE20          			CP		20H
  F591  28FA          			JR		Z,STFN1
  F593  FE30          			CP		30H        ;「0」以上の文字でなければエラーとする
  F595  DA3FF1        			JP		C,STSV2
  F598  EB            			EX		DE,HL
  F599  F1            			POP		AF
  F59A  C9            			RET
                      	
                      	;**** コマンド送信 (IN:A コマンドコード)****
  F59B  CD5CF5        	STCD:	CALL	SNDBYTE    ;Aレジスタのコマンドコードを送信
  F59E  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F5A1  C9            			RET
                      	
                      	;**** ファイルネーム送信(IN:HL ファイルネームの先頭) ******
  F5A2  0620          	STFS:	LD		B,20H
  F5A4  7E            	STFS1:	LD		A,(HL)     ;FNAME送信
  F5A5  CD5CF5        			CALL	SNDBYTE
  F5A8  23            			INC		HL
  F5A9  05            			DEC		B
  F5AA  20F8          			JR		NZ,STFS1
  F5AC  3E0D          			LD		A,0DH
  F5AE  CD5CF5        			CALL	SNDBYTE
  F5B1  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F5B4  C9            			RET
                      	
                      	;**** コマンド、ファイル名送信 (IN:A コマンドコード HL:ファイルネームの先頭)****
  F5B5  CD8CF5        	STCMD:	CALL	STFN       ;ファイルネーム取得
  F5B8  E5            			PUSH	HL
  F5B9  CD9BF5        			CALL	STCD       ;コマンドコード送信
  F5BC  E1            			POP		HL
  F5BD  A7            			AND		A          ;00以外ならERROR
  F5BE  C263F1        			JP		NZ,SVER0
  F5C1  CDA2F5        			CALL	STFS       ;ファイルネーム送信
  F5C4  A7            			AND		A          ;00以外ならERROR
  F5C5  C263F1        			JP		NZ,SVER0
  F5C8  C9            			RET
                      	
                      	;******** MESSAGE DATA ********************
  F5C9                	MSG_LD:
  F5C9  16            			DB		16H
  F5CA  4C4F4144494E47			DB		'LOADING '
  F5D2  0D            			DB		0DH
                      	
  F5D3                	WRMSG:
  F5D3  57524954494E47			DB		'WRITING '
  F5DB  0D            			DB		0DH
                      	
  F5DC                	MSG_SV:
  F5DC  53415645204649			DB		'SAVE FINISHED!'
  F5EA  0D            			DB		0DH
                      			
  F5EB                	MSG_AS:
  F5EB  41535441525420			DB		'ASTART FINISHED!'
  F5FB  0D            			DB		0DH
                      			
  F5FC                	MSG_ST:
  F5FC  50415443484544			DB		'PATCHED MONITOR START!'
  F612  0D            			DB		0DH
                      			
  F613                	MSG_AD:
  F613  41444452455353			DB		'ADDRESS FAILED!'
  F622  0D            			DB		0DH
                      			
  F623                	MSG_FNAME:
  F623  46494C45204E41			DB		'FILE NAME FAILED!'
  F634  0D            			DB		0DH
                      			
  F635                	MSG_CMD:
  F635  434F4D4D414E44			DB		'COMMAND FAILED!'
  F644  0D            			DB		0DH
                      			
  F645                	MSG_F0:
  F645  53442D43415244			DB		'SD-CARD INITIALIZE ERROR'
  F65D  0D            			DB		0DH
                      			
  F65E                	MSG_F1:
  F65E  4E4F542046494E			DB		'NOT FIND FILE'
  F66B  0D            			DB		0DH
                      			
                      	;MSG_F2:
                      	;		DB		'NOT OBJECT FILE'
                      	;		DB		0DH
                      			
  F66C                	MSG_F3:
  F66C  46494C45204558			DB		'FILE EXIST'
  F676  0D            			DB		0DH
                      			
  F677                	MSG_KEY1:
  F677  48495420414E59			DB		'HIT ANY KEY(BREAK:'
  F689  0D            			DB		0DH
  F68A                	MSG_KEY2:
  F68A  204F5220534849			DB		' OR SHIFT+BREAK)'
  F69A  0D            			DB		0DH
                      			
  F69B                	MSG_DELQ:
  F69B  46494C45204445			DB		'FILE DELETE?(Y:OK ELSE:CANSEL)'
  F6B9  0D            			DB		0DH
                      			
  F6BA                	MSG_DELY:
  F6BA  44454C45544520			DB		'DELETE OK'
  F6C3  0D            			DB		0DH
                      			
  F6C4                	MSG_DELN:
  F6C4  44454C45544520			DB		'DELETE CANSEL'
  F6D1  0D            			DB		0DH
                      			
  F6D2                	MSG_REN:
  F6D2  4E4557204E414D			DB		'NEW NAME:                            '
  F6F7  0D            			DB		0DH
                      			
  F6F8                	MSG_DNAME:
  F6F8  444F532046494C			DB		'DOS FILE:                            '
  F71D  0D            			DB		0DH
                      			
  F71E                	MSG_RENY:
  F71E  52454E414D4520			DB		'RENAME OK'
  F727  0D            			DB		0DH
                      			
  F728                	MSG_AD1:
  F728  41445253202B30			DB		'ADRS +0 +1 +2 +3 +4 +5 +6 +7 01234567'
  F74D  0D            			DB		0DH
                      			
  F74E                	MSG_AD2:
  F74E  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:SHIFT+BREAK'
  F76F  0D            			DB		0DH
                      			
  F770                	MSG_CPY:
  F770  434F5059204F4B			DB		'COPY OK'
  F777  0D            			DB		0DH
                      			
  F778                	MSG_FDW:
  F778  2A46445720    			DB		'*FDW '
  F77D  0D            			DB		0DH
                      	
  F77E                	MSG99:
  F77E  204552524F52  			DB		' ERROR'
  F784  0D            			DB		0DH
                      			
  F785                	DEFNAME:
  F785  30303030      			DB		'0000'
  F789  0D            			DB		0DH
  F78A                	NEND:
                      	
  F78A                	DEFDIR:
  F78A  2A46442020    			DB		'*FD  '
  F78F                	DEND:
                      	
                      	;*********************** 0436H MONITOR ライト インフォメーション代替処理 ************
  F78F                	MSHED:
  F78F  D5            			PUSH	DE
  F790  C5            			PUSH	BC
  F791  E5            			PUSH	HL
  F792  CD82F0        			CALL	INIT
  F795  3E91          			LD		A,91H      ;HEADER SAVEコマンド91H
  F797  CDBDF8        			CALL	MCMD       ;コマンドコード送信
  F79A  A7            			AND		A          ;00以外ならERROR
  F79B  C2C9F8        			JP		NZ,MERR
                      	
                      	;S-OS SWORD、8080用テキスト・エディタ＆アセンブラはファイルネームの後ろが20h詰めとなるため0dhに修正
  F79E  0611          			LD		B,11H
  F7A0  210111        			LD		HL,FNAME+10H     ;ファイルネーム
  F7A3  3E0D          			LD		A,0DH            ;17文字目には常に0DHをセットする
  F7A5  77            			LD		(HL),A
  F7A6  7E            	MSH0:	LD		A,(HL)
  F7A7  FE0D          			CP		0DH              ;0DHであればひとつ前の文字の検査に移る
  F7A9  2807          			JR		Z,MSH1
  F7AB  FE20          			CP		20H              ;20Hであれば0DHをセットしてひとつ前の文字の検査に移る
  F7AD  2007          			JR		NZ,MSH2          ;0DH、20H以外の文字であれば終了
  F7AF  3E0D          			LD		A,0DH
  F7B1  77            			LD		(HL),A
                      			
  F7B2  05            	MSH1:	DEC		B
  F7B3  2B            			DEC		HL
  F7B4  20F0          			JR		NZ,MSH0
                      	
  F7B6  CD0600        	MSH2:	CALL	LETLN
  F7B9  11D3F5        			LD		DE,WRMSG   ;'WRITING '
  F7BC  CD1500        			CALL	MSGPR        ;メッセージ表示
  F7BF  11F110        			LD		DE,FNAME     ;ファイルネーム
  F7C2  CD1500        			CALL	MSGPR       ;メッセージ表示
                      	
  F7C5  21F010        			LD		HL,IBUFE
  F7C8  0680          			LD		B,80H
  F7CA  7E            	MSH3:	LD		A,(HL)     ;インフォメーション ブロック送信
  F7CB  CD5CF5        			CALL	SNDBYTE
  F7CE  23            			INC		HL
  F7CF  05            			DEC		B
  F7D0  20F8          			JR		NZ,MSH3
                      	
  F7D2  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7D5  A7            			AND		A          ;00以外ならERROR
  F7D6  C2C9F8        			JP		NZ,MERR
                      	
  F7D9  C3C4F8        			JP		MRET       ;正常RETURN
                      	
                      	;******************** 0475H MONITOR ライト データ代替処理 **********************
  F7DC                	MSDAT:
  F7DC  D5            			PUSH	DE
  F7DD  C5            			PUSH	BC
  F7DE  E5            			PUSH	HL
  F7DF  3E92          			LD		A,92H      ;DATA SAVEコマンド92H
  F7E1  CDBDF8        			CALL	MCMD       ;コマンドコード送信
  F7E4  A7            			AND		A          ;00以外ならERROR
  F7E5  C2C9F8        			JP		NZ,MERR
                      	
  F7E8  210211        			LD		HL,FSIZE   ;FSIZE送信
  F7EB  7E            			LD		A,(HL)
  F7EC  CD5CF5        			CALL	SNDBYTE
  F7EF  23            			INC		HL
  F7F0  7E            			LD		A,(HL)
  F7F1  CD5CF5        			CALL	SNDBYTE
                      	
  F7F4  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7F7  A7            			AND		A          ;00以外ならERROR
  F7F8  C2C9F8        			JP		NZ,MERR
                      	
  F7FB  ED5B0211      			LD		DE,(FSIZE)
  F7FF  2A0411        			LD		HL,(SADRS)
  F802  7E            	MSD1:	LD		A,(HL)
  F803  CD5CF5        			CALL	SNDBYTE      ;SADRSからFSIZE Byteを送信。分割セーブの場合、直前に0436HでOPENされたファイルを対象として256バイトずつ0475HがCALLされる。
  F806  1B            			DEC		DE
  F807  7A            			LD		A,D
  F808  B3            			OR		E
  F809  23            			INC		HL
  F80A  20F6          			JR		NZ,MSD1
                      			
  F80C  C3C4F8        			JP		MRET       ;正常RETURN
                      	
                      	;************************** 04D8H MONITOR リード インフォメーション代替処理 *****************
  F80F                	MLHED:
  F80F  D5            			PUSH	DE
  F810  C5            			PUSH	BC
  F811  E5            			PUSH	HL
  F812  CD82F0        			CALL	INIT
  F815  3E93          			LD		A,93H      ;HEADER LOADコマンド93H
  F817  CDBDF8        			CALL	MCMD       ;コマンドコード送信
  F81A  A7            			AND		A          ;00以外ならERROR
  F81B  C2C9F8        			JP		NZ,MERR
                      	
  F81E  0608          			LD		B,08H      ;LBUFを0DHで埋めファイルネームが指定されなかったことにする
  F820  11A311        			LD		DE,LBUF
  F823  3E0D          			LD		A,0DH
  F825  12            	MLH0:	LD		(DE),A
  F826  13            			INC		DE
  F827  05            			DEC		B
  F828  20FB          			JR		NZ,MLH0
                      	
  F82A  3E03          			LD		A,03H          ;一行分をクリアするため3文字削除、37文字出力
  F82C  327111        			LD		(DSPX),A
  F82F  3EC7          			LD		A,0C7H
  F831  CDDC0D        			CALL	DPCT
  F834  CDDC0D        			CALL	DPCT
  F837  CDDC0D        			CALL	DPCT
  F83A  11F8F6        			LD		DE,MSG_DNAME   ;'DOS FILE:'
  F83D  CD1500        			CALL	MSGPR
  F840  3E09          			LD		A,09H          ;カーソルを9文字目に戻す
  F842  327111        			LD		(DSPX),A
                      	
  F845  11AE11        			LD		DE,MBUF    ;ファイルネームを指示するための苦肉の策。LOADコマンドとしてはファイルネームなしとして改行したのちに行バッファの位置をずらしてDOSファイルネームを入力する。
  F848  CD0300        			CALL	GETL
                      			
  F84B  11B711        			LD		DE,MBUF+9
                      	;		LD		DE,MBUF
  F84E                	MLH1:
  F84E  1A            			LD		A,(DE)
  F84F  FE20          			CP		20H                 ;行頭のスペースをファイルネームまで読み飛ばし
  F851  2003          			JR		NZ,MLH2
  F853  13            			INC		DE
  F854  18F8          			JR		MLH1
                      	
  F856  0620          	MLH2:	LD		B,20H
  F858  1A            	MLH4:	LD		A,(DE)     ;FNAME送信
  F859  CD5CF5        			CALL	SNDBYTE
  F85C  13            			INC		DE
  F85D  05            			DEC		B
  F85E  20F8          			JR		NZ,MLH4
  F860  3E0D          			LD		A,0DH
  F862  CD5CF5        			CALL	SNDBYTE
                      			
  F865  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F868  A7            			AND		A          ;00以外ならERROR
  F869  C2C9F8        			JP		NZ,MERR
                      	
  F86C  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F86F  A7            			AND		A          ;00以外ならERROR
  F870  C2C9F8        			JP		NZ,MERR
                      	
  F873  21F010        			LD		HL,IBUFE
  F876  0680          			LD		B,80H
  F878  CD49F5        	MLH5:	CALL	RCVBYTE    ;読みだされたインフォメーションブロックを受信
  F87B  77            			LD		(HL),A
  F87C  23            			INC		HL
  F87D  05            			DEC		B
  F87E  20F8          			JR		NZ,MLH5
                      	
  F880  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F883  A7            			AND		A          ;00以外ならERROR
  F884  C2C9F8        			JP		NZ,MERR
                      	
  F887  183B          			JR		MRET       ;正常RETURN
                      	
                      	;**************************** 04F8H MONITOR リード データ代替処理 ********************
  F889                	MLDAT:
  F889  D5            			PUSH	DE
  F88A  C5            			PUSH	BC
  F88B  E5            			PUSH	HL
  F88C  3E94          			LD		A,94H      ;DATA LOADコマンド94H
  F88E  CDBDF8        			CALL	MCMD       ;コマンドコード送信
  F891  A7            			AND		A          ;00以外ならERROR
  F892  C2C9F8        			JP		NZ,MERR
                      	
  F895  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F898  A7            			AND		A          ;00以外ならERROR
  F899  C2C9F8        			JP		NZ,MERR
                      	
  F89C  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F89F  A7            			AND		A          ;00以外ならERROR
  F8A0  C2C9F8        			JP		NZ,MERR
                      	
  F8A3  110211        			LD		DE,FSIZE   ;FSIZE送信
  F8A6  1A            			LD		A,(DE)
  F8A7  CD5CF5        			CALL	SNDBYTE
  F8AA  13            			INC		DE
  F8AB  1A            			LD		A,(DE)
  F8AC  CD5CF5        			CALL	SNDBYTE
  F8AF  CDE5F0        			CALL	DBRCV      ;FSIZE分のデータを受信し、SADRSから格納。分割ロードの場合、直前に0436HでOPENされたファイルを対象として256バイトずつSADRSが加算されて04F8HがCALLされる。
                      	
  F8B2  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F8B5  A7            			AND		A          ;00以外ならERROR
  F8B6  C2C9F8        			JP		NZ,MERR
                      	
  F8B9  1809          			JR		MRET       ;正常RETURN
                      	
                      	;************************** 0588H VRFY CMT ベリファイ代替処理 *******************
  F8BB  AF            	MVRFY:	XOR		A          ;正常終了フラグ
                      	;		EI
  F8BC  C9            			RET
                      	
                      	;******* 代替処理用コマンドコード送信 (IN:A コマンドコード) **********
  F8BD                	MCMD:
                      	;		PUSH	AF
                      	;		CALL	INIT
                      	;		POP		AF
  F8BD  CD5CF5        			CALL	SNDBYTE    ;コマンドコード送信
  F8C0  CD49F5        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F8C3  C9            			RET
                      	
                      	;****** 代替処理用正常RETURN処理 **********
  F8C4  E1            	MRET:	POP		HL
  F8C5  C1            			POP		BC
  F8C6  D1            			POP		DE
  F8C7  AF            			XOR		A          ;正常終了フラグ
                      	;		EI
  F8C8  C9            			RET
                      	
                      	;******* 代替処理用ERROR処理 **************
  F8C9                	MERR:
  F8C9  FEF0          			CP		0F0H
  F8CB  2005          			JR		NZ,MERR3
  F8CD  1145F6        			LD		DE,MSG_F0
  F8D0  180F          			JR		MERRMSG
                      	;代替処理ではファイル種類コードの判別なし
                      	;MERR2:	CP		0F2H
                      	;		JR		NZ,MERR3
                      	;		LD		DE,MSG_F2
                      	;		JR		MERRMSG
                      			
  F8D2  FEF1          	MERR3:	CP		0F1H
  F8D4  2005          			JR		NZ,MERR99
  F8D6  115EF6        			LD		DE,MSG_F1
  F8D9  1806          			JR		MERRMSG
                      			
  F8DB  CDC303        	MERR99:	CALL	PRTBYT
  F8DE  117EF7        			LD		DE,MSG99
                      			
  F8E1                	MERRMSG:
  F8E1  CD1500        			CALL	MSGPR
  F8E4  CD0600        			CALL	LETLN
  F8E7  E1            			POP		HL
  F8E8  C1            			POP		BC
  F8E9  D1            			POP		DE
  F8EA  3E02          			LD		A,02H
  F8EC  37            			SCF
                      	;		EI
  F8ED  C9            			RET
                      	
  F8EE                			END
