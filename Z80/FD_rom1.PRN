			  Z80 ASSEMBLER - ZASM VER 1.6
                      	;2021.12.12 MZ-700でFDP、FDMが文字化けする現象に対処
                      	;2022. 1.23 04D8H MONITOR リード インフォメーション代替処理のバグを修正
                      	;
  0003                	GETL		EQU		0003H
  0006                	LETLN		EQU		0006H
  0009                	NEWLIN		EQU		0009H
  000C                	PRNTS		EQU		000CH
  0015                	MSGPR		EQU		0015H
  0018                	PLIST		EQU		0018H
  001B                	GETKEY		EQU		001BH
  03BA                	PRTWRD		EQU		03BAH
  03C3                	PRTBYT		EQU		03C3H
  0410                	HLHEX		EQU		0410H
  041F                	TWOHEX		EQU		041FH
  0BB9                	ADCN		EQU		0BB9H
  0DB5                	DISPCH		EQU		0DB5H
  0DDC                	DPCT		EQU		0DDCH
  10F0                	IBUFE		EQU		10F0H
  10F1                	FNAME		EQU		10F1H
  1102                	EADRS		EQU		1102H
  1102                	FSIZE		EQU		1102H
  1104                	SADRS		EQU		1104H
  1106                	EXEAD		EQU		1106H
  1171                	DSPX		EQU		1171H
  11A3                	LBUF		EQU		11A3H
  11AE                	MBUF		EQU		11AEH
  0082                	MONITOR_80K	EQU		0082H
  00AD                	MONITOR_700	EQU		00ADH
                      	
                      	;0D8H PORTA 送信データ(下位4ビット)
                      	;0D9H PORTB 受信データ(8ビット)
                      	;
                      	;0DAH PORTC Bit
                      	;7 IN  CHK
                      	;6 IN
                      	;5 IN
                      	;4 IN 
                      	;3 OUT
                      	;2 OUT FLG
                      	;1 OUT
                      	;0 OUT
                      	;
                      	;0DBH コントロールレジスタ
                      	
                      	
  F000                	       ORG		0F000H
                      	
  F000  00            			NOP                   ;ROM識別コード
  F001  C313F0        			JP		START
                      	;******************** MONITOR CMTルーチン代替 *************************************
  F004  C301F7        			JP		MSHED
  F007  C330F7        			JP		MSDAT
  F00A  C363F7        			JP		MLHED
  F00D  C3DAF7        			JP		MLDAT
  F010  C30CF8        			JP		MVRFY
                      	
                      			
  F013  CD78F0        	START:	CALL	INIT
  F016  11A311        			LD		DE,LBUF     ;MZ-80K、MZ-700とも起動コマンドは'*FD'に統一
  F019  1A            			LD		A,(DE)
  F01A  FE2A          			CP		'*'
  F01C  C293F1        			JP		NZ,MON
  F01F  13            			INC 	DE
  F020  1A            			LD		A,(DE)
  F021  FE46          			CP		'F'
  F023  C293F1        			JP		NZ,MON
  F026  13            			INC		DE
  F027  1A            			LD		A,(DE)
  F028  FE44          			CP		'D'
  F02A  C293F1        			JP		NZ,MON
                      			
  F02D  13            			INC		DE          ;FDの次の文字へ移動
  F02E  1A            	STT2:	LD		A,(DE)
  F02F  FE20          			CP		20H         ;FDの後に1文字空白があれば以降をファイルネームとしてロード(ファイルネームは32文字まで)
  F031  2850          			JR		Z,SDLOAD
  F033  FE2F          			CP		'/'         ;FDの後が'/'なら以降をファイルネームとしてロード、実行はしない(ファイルネームは32文字まで)
  F035  284C          			JR		Z,SDLOAD
  F037  FE0D          			CP		0DH         ;FDだけで改行の場合にはDEFNAMEの文字列をファイルネームとしてロード
  F039  200D          			JR		NZ,STETC    ;該当なしなら他コマンドをチェック
  F03B  D5            	STT3:	PUSH	DE          ;設定ファイル名(0000.mzt)を転送
  F03C  21F7F6        			LD		HL,DEFNAME
  F03F  13            			INC		DE
  F040  010500        			LD		BC,NEND-DEFNAME
  F043  EDB0          			LDIR
  F045  D1            			POP		DE
  F046  183B          			JR		SDLOAD      ;LOAD処理へ
  F048                	STETC:
  F048  FE53          			CP		'S'         ;FDS:SAVE処理へ
  F04A  CAEDF0        			JP		Z,STSV
  F04D  FE41          			CP		'A'			;FDA:自動起動ファイル設定処理へ
  F04F  CA09F2        			JP		Z,STAS
  F052  FE4C          			CP		'L'         ;FDL:ファイル一覧表示
  F054  CA14F2        			JP		Z,STLT
  F057  FE44          			CP		'D'         ;FDD:DELETE
  F059  CAB4F2        			JP		Z,STDE
  F05C  FE52          			CP		'R'         ;FDR:RENAME
  F05E  CAF1F2        			JP		Z,STRN
  F061  FE50          			CP		'P'         ;FDP:DUMP
  F063  CA1EF3        			JP		Z,STPR
  F066  FE43          			CP		'C'         ;FDC:COPY
  F068  CAC3F3        			JP		Z,STCP
  F06B  FE4D          			CP		'M'         ;FDM:MEMORY DUMP
  F06D  CAF0F3        			JP		Z,STMD
  F070  FE57          			CP		'W'         ;FDW:MEMORY WRITE
  F072  CA75F4        			JP		Z,STMW
  F075  C33AF1        			JP		CMDERR
                      	
                      	;**** 8255初期化 ****
                      	;PORTC下位BITをOUTPUT、上位BITをINPUT、PORTBをINPUT、PORTAをOUTPUT
  F078  3E8A          	INIT:	LD		A,8AH
  F07A  D3DB          			OUT		(0DBH),A
                      	;出力BITをリセット
  F07C  3E00          	INIT2:	LD		A,00H      ;PORTA <- 0
  F07E  D3D8          			OUT		(0D8H),A
  F080  D3DA          			OUT		(0DAH),A   ;PORTC <- 0
  F082  C9            			RET
                      	
                      	;**** LOAD ****
                      	;受信ヘッダ情報をセットし、SDカードからLOAD実行
  F083  3E81          	SDLOAD:	LD		A,81H  ;LOADコマンド81H
  F085  CD2EF5        			CALL	STCMD
  F088  CD9AF0        			CALL	HDRCV      ;ヘッダ情報受信
  F08B  CDDBF0        			CALL	DBRCV      ;データ受信
  F08E  3AA611        			LD		A,(LBUF+3)
  F091  FE2F          			CP		'/'        ;'*FD/'であれば実行アドレスに飛ばずにMONITORコマンド待ちに戻る
  F093  CA93F1        			JP		Z,MON
  F096  2A0611        			LD		HL,(EXEAD)
  F099  E9            			JP		(HL)
                      	
                      	;ヘッダ受信
  F09A  21F110        	HDRCV:	LD		HL,FNAME
  F09D  0611          			LD		B,11H
  F09F  CDC2F4        	HDRC1:	CALL	RCVBYTE    ;ファイルネーム受信
  F0A2  77            			LD		(HL),A
  F0A3  23            			INC		HL
  F0A4  05            			DEC		B
  F0A5  20F8          			JR		NZ,HDRC1
  F0A7  1142F5        			LD		DE,MSG_LD  ;ファイルネームLOADING表示
  F0AA  CD1500        			CALL	MSGPR
  F0AD  11F110        			LD		DE,FNAME
  F0B0  CD1500        			CALL	MSGPR
  F0B3  CD0600        			CALL	LETLN
  F0B6  210411        			LD		HL,SADRS  ;SADRS取得
  F0B9  CDC2F4        			CALL	RCVBYTE
  F0BC  77            			LD		(HL),A
  F0BD  23            			INC		HL
  F0BE  CDC2F4        			CALL	RCVBYTE
  F0C1  77            			LD		(HL),A
  F0C2  210211        			LD		HL,FSIZE   ;FSIZE取得
  F0C5  CDC2F4        			CALL	RCVBYTE
  F0C8  77            			LD		(HL),A
  F0C9  23            			INC		HL
  F0CA  CDC2F4        			CALL	RCVBYTE
  F0CD  77            			LD		(HL),A
  F0CE  210611        			LD		HL,EXEAD   ;EXEAD取得
  F0D1  CDC2F4        			CALL	RCVBYTE
  F0D4  77            			LD		(HL),A
  F0D5  23            			INC		HL
  F0D6  CDC2F4        			CALL	RCVBYTE
  F0D9  77            			LD		(HL),A
  F0DA  C9            			RET
                      	
                      	;データ受信
  F0DB  ED5B0211      	DBRCV:	LD		DE,(FSIZE)
  F0DF  2A0411        			LD		HL,(SADRS)
  F0E2  CDC2F4        	DBRLOP:	CALL	RCVBYTE
  F0E5  77            			LD		(HL),A
  F0E6  1B            			DEC		DE
  F0E7  7A            			LD		A,D
  F0E8  B3            			OR		E
  F0E9  23            			INC		HL
  F0EA  20F6          			JR		NZ,DBRLOP   ;DE=0までLOOP
  F0EC  C9            			RET
                      	
                      	;**** SAVE ****
  F0ED  13            	STSV:	INC		DE
  F0EE  13            			INC		DE
  F0EF  D5            			PUSH	DE
  F0F0  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればSADRSにセットして続行
  F0F3  383B          			JR		C,STSV1
                      	
  F0F5  220411        			LD		(SADRS),HL      ;SARDS保存
  F0F8  D1            			POP		DE
  F0F9  13            			INC		DE
  F0FA  13            			INC		DE
  F0FB  13            			INC		DE
  F0FC  13            			INC		DE
  F0FD  13            			INC		DE
  F0FE  D5            			PUSH	DE          ;5文字進めて4桁の16進数であればEADRSにセットして続行
  F0FF  CD1004        			CALL	HLHEX
  F102  382C          			JR		C,STSV1
  F104  E5            			PUSH	HL
  F105  ED4B0411      			LD		BC,(SADRS)
  F109  ED42          			SBC		HL,BC       ;EADRSがSADRSより大きくない場合はエラー
  F10B  E1            			POP		HL
  F10C  2822          			JR		Z,STSV1
  F10E  3820          			JR		C,STSV1
                      	
  F110  220211        			LD		(EADRS),HL      ;EADRS保存
  F113  D1            			POP		DE
  F114  13            			INC		DE
  F115  13            			INC		DE
  F116  13            			INC		DE
  F117  13            			INC		DE
  F118  13            			INC		DE          ;5文字進めて4桁の16進数であればEXEADにセットして続行
  F119  D5            			PUSH	DE
  F11A  CD1004        			CALL	HLHEX
  F11D  3811          			JR		C,STSV1
                      			
  F11F  220611        			LD		(EXEAD),HL      ;EXEAD保存
  F122  D1            			POP		DE
  F123  13            			INC		DE
  F124  13            			INC		DE
  F125  13            			INC		DE
  F126  13            			INC		DE
  F127  13            			INC		DE			;5文字進めてファイルネームがあれば続行
  F128  1A            			LD		A,(DE)
  F129  FE31          			CP		31H
  F12B  3808          			JR		C,STSV2
  F12D  EB            			EX		DE,HL
  F12E  180F          			JR		SDSAVE      ;SAVE処理へ
  F130                	STSV1:                      ;16進数4桁の取得に失敗又はEADRSがSARDSより大きくない
  F130  1175F5        			LD		DE,MSG_AD
  F133  1858          			JR		ERRMSG
  F135                	STSV2:                      ;ファイルネームの取得に失敗
  F135  1185F5        			LD		DE,MSG_FNAME
  F138  1853          			JR		ERRMSG
  F13A                	CMDERR:                     ;コマンド異常
  F13A  1197F5        			LD		DE,MSG_CMD
  F13D  184E          			JR		ERRMSG
                      	
                      	;送信ヘッダ情報をセットし、SDカードへSAVE実行
  F13F  3E80          	SDSAVE:	LD		A,80H      ;SAVEコマンド80H
  F141  CD14F5        			CALL	STCD
  F144  A7            			AND		A          ;00以外ならERROR
  F145  C25AF1        			JP		NZ,SVERR
  F148  CDADF1        			CALL	HDSEND     ;ヘッダ情報送信
  F14B  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F14E  A7            			AND		A          ;00以外ならERROR
  F14F  2009          			JR		NZ,SVERR
  F151  CDF2F1        			CALL	DBSEND     ;データ送信
  F154  1155F5        			LD		DE,MSG_SV
  F157  1834          			JR		ERRMSG
                      	
  F159                	SVER0:
  F159  D1            			POP		DE         ;CALL元STACKを破棄する
  F15A                	SVERR:
  F15A  FEF0          			CP		0F0H
  F15C  2005          			JR		NZ,ERR2
  F15E  11A7F5        			LD		DE,MSG_F0  ;SD-CARD INITIALIZE ERROR
  F161  182A          			JR		ERRMSG
  F163  FEF2          	ERR2:	CP		0F2H
  F165  2005          			JR		NZ,ERR3
  F167  11CEF5        			LD		DE,MSG_F2  ;NOT OBJECT FILE
  F16A  1821          			JR		ERRMSG
  F16C  FEF1          	ERR3:	CP		0F1H
  F16E  2005          			JR		NZ,ERR4
  F170  11C0F5        			LD		DE,MSG_F1  ;NOT FIND FILE
  F173  1818          			JR		ERRMSG
  F175  FEF3          	ERR4:	CP		0F3H
  F177  2005          			JR		NZ,ERR5
  F179  11DEF5        			LD		DE,MSG_F3  ;FILE EXIST
  F17C  180F          			JR		ERRMSG
  F17E  FEF4          	ERR5:	CP		0F4H
  F180  2005          			JR		NZ,ERR99
  F182  1197F5        			LD		DE,MSG_CMD
  F185  1806          			JR		ERRMSG
  F187  CDC303        	ERR99:	CALL	PRTBYT
  F18A  11F0F6        			LD		DE,MSG99   ;その他ERROR
  F18D  CD1500        	ERRMSG:	CALL	MSGPR
  F190  CD0600        			CALL	LETLN
  F193  214E01        	MON:	LD		HL,014EH
  F196  7E            			LD		A,(HL)
  F197  FE50          			CP		'P'             ;014EHが'P'ならMZ-80K
  F199  CA8200        			JP		Z,MONITOR_80K
  F19C  FE4E          			CP		'N'             ;014EHが'N'ならFN-700
  F19E  CA8200        			JP		Z,MONITOR_80K
  F1A1  21EB06        			LD		HL,06EBH
  F1A4  7E            			LD		A,(HL)
  F1A5  FE4D          			CP		'M'             ;06EBHが'M'ならMZ-700
  F1A7  CAAD00        			JP		Z,MONITOR_700
  F1AA  C30000        			JP		0000H           ;識別できなかったら0000Hへジャンプ
                      	
                      	;ヘッダ送信
  F1AD  E5            	HDSEND:	PUSH	HL
  F1AE  0620          			LD		B,20H
  F1B0  7E            	SS1:	LD		A,(HL)     ;FNAME送信
  F1B1  CDD5F4        			CALL	SNDBYTE
  F1B4  23            			INC		HL
  F1B5  05            			DEC		B
  F1B6  20F8          			JR		NZ,SS1
  F1B8  3E0D          			LD		A,0DH
  F1BA  CDD5F4        			CALL	SNDBYTE
  F1BD  E1            			POP		HL
  F1BE  0610          			LD		B,10H
  F1C0  7E            	SS2:	LD		A,(HL)     ;PNAME送信
  F1C1  CDD5F4        			CALL	SNDBYTE
  F1C4  23            			INC		HL
  F1C5  05            			DEC		B
  F1C6  20F8          			JR		NZ,SS2
  F1C8  3E0D          			LD		A,0DH
  F1CA  CDD5F4        			CALL	SNDBYTE
  F1CD  210411        			LD		HL,SADRS   ;SADRS送信
  F1D0  7E            			LD		A,(HL)
  F1D1  CDD5F4        			CALL	SNDBYTE
  F1D4  23            			INC		HL
  F1D5  7E            			LD		A,(HL)
  F1D6  CDD5F4        			CALL	SNDBYTE
  F1D9  210211        			LD		HL,EADRS   ;EADRS送信
  F1DC  7E            			LD		A,(HL)
  F1DD  CDD5F4        			CALL	SNDBYTE
  F1E0  23            			INC		HL
  F1E1  7E            			LD		A,(HL)
  F1E2  CDD5F4        			CALL	SNDBYTE
  F1E5  210611        			LD		HL,EXEAD   ;EXEAD送信
  F1E8  7E            			LD		A,(HL)
  F1E9  CDD5F4        			CALL	SNDBYTE
  F1EC  23            			INC		HL
  F1ED  7E            			LD		A,(HL)
  F1EE  CDD5F4        			CALL	SNDBYTE
  F1F1  C9            			RET
                      	
                      	;データ送信
                      	;SADRSからEADRSまでを送信
  F1F2  2A0211        	DBSEND:	LD		HL,(EADRS)
  F1F5  EB            			EX		DE,HL
  F1F6  2A0411        			LD		HL,(SADRS)
  F1F9  7E            	DBSLOP:	LD		A,(HL)
  F1FA  CDD5F4        			CALL	SNDBYTE
  F1FD  7C            			LD		A,H
  F1FE  BA            			CP		D
  F1FF  2004          			JR		NZ,DBSLP1
  F201  7D            			LD		A,L
  F202  BB            			CP		E
  F203  2803          			JR		Z,DBSLP2   ;HL = DE までLOOP
  F205  23            	DBSLP1:	INC		HL
  F206  18F1          			JR		DBSLOP
  F208  C9            	DBSLP2:	RET
                      	
                      	;**** AUTO START SET ****
  F209  3E82          	STAS:	LD		A,82H      ;AUTO START SETコマンド82H
  F20B  CD2EF5        			CALL	STCMD
  F20E  1164F5        			LD		DE,MSG_AS
  F211  C38DF1        			JP		ERRMSG
                      	
                      	;**** DIRLIST ****
  F214  13            	STLT:	INC		DE
  F215  1A            			LD		A,(DE)
  F216  FE0D          			CP		0DH
  F218  2004          			JR		NZ,STLT1
  F21A  3E30          			LD		A,30H
  F21C  1819          			JR		DIRLIST    ;FDLだけなら'30H'を送信(全ファイルを表示)
  F21E  13            	STLT1:	INC		DE         ;FDLの後に数字一文字があれば'31H'〜'39H','41H'〜'5AH'を送信(20件を1ページとしてnページを表示)
  F21F  1A            			LD		A,(DE)
  F220  FE30          			CP		30H
  F222  DA3AF1        			JP		C,CMDERR
  F225  FE3A          			CP		3AH
  F227  3002          			JR		NC,STLT2
  F229  180C          			JR		DIRLIST
  F22B  FE41          	STLT2:	CP		41H        ;念のためA〜Zにも対応
  F22D  DA3AF1        			JP		C,CMDERR
  F230  FE5B          			CP		5BH
  F232  D23AF1        			JP		NC,CMDERR
  F235  D607          			SUB		07H
  F237                	DIRLIST:
  F237  F5            			PUSH	AF
  F238  3E83          			LD		A,83H      ;DIRLISTコマンド83Hを送信
  F23A  CD14F5        			CALL	STCD       ;コマンドコード送信
  F23D  A7            			AND		A          ;00以外ならERROR
  F23E  C25AF1        			JP		NZ,SVERR
  F241  F1            			POP		AF
  F242  CDD5F4        			CALL	SNDBYTE           ;ページ指示を送信
  F245  21FCF6        	DL1:	LD		HL,DEFDIR         ;行頭に'*FD 'を付けることでカーソルを移動させリターンで実行できるように
  F248  11A311        			LD		DE,LBUF
  F24B  010500        			LD		BC,DEND-DEFDIR
  F24E  EDB0          			LDIR
  F250  EB            			EX		DE,HL
  F251  CDC2F4        	DL2:	CALL	RCVBYTE           ;'00H'を受信するまでを一行とする
  F254  FE00          			CP		00H
  F256  280C          			JR		Z,DL3
  F258  FEFF          			CP		0FFH              ;'0FFH'を受信したら終了
  F25A  2813          			JR		Z,DL4
  F25C  FEFE          			CP		0FEH              ;'0FEH'を受信したら一時停止して一文字入力待ち
  F25E  2819          			JR		Z,DL5
  F260  77            			LD		(HL),A
  F261  23            			INC		HL
  F262  18ED          			JR		DL2
  F264  11A311        	DL3:	LD		DE,LBUF           ;'00H'を受信したら一行分を表示して改行
  F267  CD1500        			CALL	MSGPR
  F26A  CD0600        			CALL	LETLN
  F26D  18D6          			JR		DL1
  F26F  CDC2F4        	DL4:	CALL	RCVBYTE           ;状態取得(00H=OK)
  F272  A7            			AND		A                 ;00以外ならERROR
  F273  C25AF1        			JP		NZ,SVERR
  F276  C393F1        			JP		MON
                      	
  F279  11E9F5        	DL5:	LD		DE,MSG_KEY1        ;HIT ANT KEY表示
  F27C  CD1500        			CALL	MSGPR
  F27F  3EC2          			LD		A,0C2H
  F281  CDB50D        			CALL	DISPCH
  F284  11FCF5        			LD		DE,MSG_KEY2        ;HIT ANT KEY表示
  F287  CD1500        			CALL	MSGPR
  F28A  CD0600        			CALL	LETLN
  F28D  CD1B00        	DL6:	CALL	GETKEY            ;1文字入力待ち
  F290  FE00          			CP		00H
  F292  28F9          			JR		Z,DL6
  F294  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F296  2815          			JR		Z,DL7
  F298  FE12          			CP		12H               ;カーソル↑で打ち切り
  F29A  2807          			JR		Z,DL9
  F29C  CD0600        			CALL	LETLN             ;それ以外で継続
  F29F  3E00          			LD		A,00H
  F2A1  180C          			JR		DL8
  F2A3  3EC2          	DL9:	LD		A,0C2H            ;カーソル↑で打ち切ったときにカーソル2行上へ
  F2A5  CDDC0D        			CALL	DPCT
  F2A8  3EC2          			LD		A,0C2H
  F2AA  CDDC0D        			CALL	DPCT
  F2AD  3EFF          	DL7:	LD		A,0FFH            ;0FFH中断コードを送信
  F2AF  CDD5F4        	DL8:	CALL	SNDBYTE
  F2B2  189D          			JR		DL2
                      	
                      	;**** FILE DELETE ****
  F2B4  3E84          	STDE:	LD		A,84H      ;FILE DELETEコマンド84H
  F2B6  CD2EF5        			CALL	STCMD
                      	
  F2B9  110DF6        			LD		DE,MSG_DELQ ;'DELETE?'表示
  F2BC  CD1500        			CALL	MSGPR
  F2BF  CD0600        			CALL	LETLN
  F2C2  CD1B00        	STDE3:	CALL	GETKEY
  F2C5  FE00          			CP		00H
  F2C7  28F9          			JR		Z,STDE3
  F2C9  FE59          			CP		59H         ;'Y'ならOKとして00Hを送信
  F2CB  2004          			JR		NZ,STDE4
  F2CD  3E00          			LD		A,00H
  F2CF  1802          			JR		STDE5
  F2D1  3EFF          	STDE4:	LD		A,0FFH      ;'Y'以外ならCANSELとして0FFHを送信
  F2D3  CDD5F4        	STDE5:	CALL	SNDBYTE
  F2D6  CDC2F4        			CALL	RCVBYTE
  F2D9  FE00          			CP		00H         ;00Hを受信すればDELETE完了
  F2DB  2005          			JR		NZ,STDE6
  F2DD  112CF6        			LD		DE,MSG_DELY ;'DELETE OK'表示
  F2E0  180C          			JR		STDE8
  F2E2  FE01          	STDE6:	CP		01H         ;01Hを受信すればCANSEL完了
  F2E4  2005          			JR		NZ,STDE7
  F2E6  1136F6        			LD		DE,MSG_DELN ;'DELETE CANSEL'表示
  F2E9  1803          			JR		STDE8
  F2EB  C35AF1        	STDE7:	JP		SVERR
  F2EE  C38DF1        	STDE8:	JP		ERRMSG
                      	
                      	;**** FILE RENAME ****
  F2F1  3E85          	STRN:	LD		A,85H      ;FILE RENAMEコマンド85H
  F2F3  CD2EF5        			CALL	STCMD
                      	
  F2F6  1144F6        			LD		DE,MSG_REN ;'NEW NAME:'表示
  F2F9  CD1500        			CALL	MSGPR
                      			
  F2FC  3E09          			LD		A,09H
  F2FE  327111        			LD		(DSPX),A  ;カーソル位置を'NEW NAME:'の次へ
  F301  11A311        			LD		DE,LBUF    ;NEW FILE NAMEを取得
  F304  CD0300        			CALL	GETL
  F307  11AB11        			LD		DE,LBUF+8  ;NEW FILE NAMEを送信
  F30A  CD05F5        			CALL	STFN
  F30D  CD1BF5        			CALL	STFS
                      			
  F310  CDC2F4        			CALL	RCVBYTE
  F313  FE00          			CP		00H         ;00Hを受信すればRENAME完了
  F315  C25AF1        			JP		NZ,SVERR
  F318  1190F6        			LD		DE,MSG_RENY
  F31B  C38DF1        			JP		ERRMSG
                      	
                      	;**** FILE DUMP ****
  F31E  3E86          	STPR:	LD		A,86H      ;FILE DUMPEコマンド86H
  F320  CD2EF5        			CALL	STCMD
                      	
                      	;		LD		A,0C6H     ;画面クリア
                      	;		CALL	DPCT
  F323  210411        	STPR6:	LD		HL,SADRS   ;SADRS取得
  F326  CDC2F4        			CALL	RCVBYTE
  F329  77            			LD		(HL),A
  F32A  23            			INC		HL
  F32B  CDC2F4        			CALL	RCVBYTE
  F32E  77            			LD		(HL),A
  F32F  2A0411        			LD		HL,(SADRS)
  F332  7C            			LD		A,H
  F333  FEFF          			CP		0FFH        ;ADRSに0FFFFHが送信されてきたらDUMP処理終了
  F335  2008          			JR		NZ,STPR7
  F337  7D            			LD		A,L
  F338  FEFF          			CP		0FFH
  F33A  2003          			JR		NZ,STPR7
  F33C  C3BDF3        			JP		STPR8
  F33F  119AF6        	STPR7:	LD		DE,MSG_AD1 ;DUMP TITLE表示
  F342  CD1500        			CALL	MSGPR
  F345  CD0600        			CALL	LETLN
  F348  0E10          			LD		C,10H      ;16行(128Byte)を表示
  F34A  C5            	STPR0:	PUSH	BC
  F34B  0608          			LD		B,08H      ;一行(8Byte)を受信
  F34D  21A311        			LD		HL,LBUF
  F350  CDC2F4        	STPR1:	CALL	RCVBYTE
  F353  77            			LD		(HL),A
  F354  05            			DEC		B
  F355  23            			INC		HL
  F356  20F8          			JR		NZ,STPR1
                      	
  F358  2A0411        			LD		HL,(SADRS) ;アドレス表示
  F35B  CDBA03        			CALL	PRTWRD
  F35E  110800        			LD		DE,0008H   ;一画面(128Byte)中はアドレスを受け取らないので自前でインクリメント
  F361  19            			ADD		HL,DE
  F362  220411        			LD		(SADRS),HL
                      			
  F365  0608          			LD		B,08H      ;一行(8Byte)のデータを16進数表示
  F367  11A311        			LD		DE,LBUF
  F36A  CD0C00        	STPR2:	CALL	PRNTS
  F36D  1A            			LD		A,(DE)
  F36E  CDC303        			CALL	PRTBYT
  F371  13            			INC		DE
  F372  05            			DEC		B
  F373  20F5          			JR		NZ,STPR2
                      			
  F375  CD0C00        			CALL	PRNTS
  F378  11A311        			LD		DE,LBUF    ;一行(8Byte)のデータをキャラクタ表示
  F37B  0608          			LD		B,08H
  F37D  1A            	STPR9:	LD		A,(DE)
  F37E  FE10          			CP		10H        ;MZ-700での文字化けに対処
  F380  3002          			JR		NC,STPRA
  F382  3E20          			LD		A,20H
  F384  CDB90B        	STPRA:	CALL	ADCN
  F387  CDB50D        			CALL	DISPCH
  F38A  13            			INC		DE
  F38B  05            			DEC		B
  F38C  20EF          			JR		NZ,STPR9
                      	
  F38E  CD0600        			CALL	LETLN
  F391  C1            			POP		BC
  F392  0D            			DEC		C
  F393  20B5          			JR		NZ,STPR0
                      			
  F395  11C0F6        			LD		DE,MSG_AD2        ;入力待ちメッセージ表示
  F398  CD1500        			CALL	MSGPR
  F39B  CD0600        			CALL	LETLN
  F39E  CD0600        			CALL	LETLN
  F3A1  CD1B00        	STPR3:	CALL	GETKEY            ;1文字入力待ち
  F3A4  FE00          			CP		00H
  F3A6  28F9          			JR		Z,STPR3
  F3A8  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F3AA  2806          			JR		Z,STPR4
  F3AC  CDD5F4        			CALL	SNDBYTE           ;SHIFT+BREAK以外はASCIIコードのまま送信、ARDUINO側で'B'を処理
  F3AF  C323F3        			JP		STPR6
  F3B2  3EFF          	STPR4:	LD		A,0FFH            ;0FFH中断コードを送信
  F3B4  CDD5F4        	STPR5:	CALL	SNDBYTE
  F3B7  CDC2F4        			CALL	RCVBYTE           ;SHIFT+BREAKでの中断時はADRS'0FFFFH'の受信及び状態コードの受信を破棄
  F3BA  CDC2F4        			CALL	RCVBYTE
  F3BD  CDC2F4        	STPR8:	CALL	RCVBYTE
  F3C0  C393F1        			JP		MON
                      	
                      	;**** FILE COPY ****
  F3C3  3E87          	STCP:	LD		A,87H      ;FILE COPYコマンド87H
  F3C5  CD2EF5        			CALL	STCMD
  F3C8  1144F6        			LD		DE,MSG_REN ;'NEW NAME:'表示
  F3CB  CD1500        			CALL	MSGPR
                      			
  F3CE  3E09          			LD		A,09H
  F3D0  327111        			LD		(DSPX),A    ;カーソル位置を'NEW NAME:'の次へ
  F3D3  11A311        			LD		DE,LBUF      ;NEW FILE NAMEを取得
  F3D6  CD0300        			CALL	GETL
  F3D9  11AB11        			LD		DE,LBUF+8    ;NEW FILE NAMEを送信
  F3DC  CD05F5        			CALL	STFN
  F3DF  CD1BF5        			CALL	STFS
                      			
  F3E2  CDC2F4        			CALL	RCVBYTE
  F3E5  FE00          			CP		00H         ;00Hを受信すればRENAME完了
  F3E7  C25AF1        			JP		NZ,SVERR
  F3EA  11E2F6        			LD		DE,MSG_CPY
  F3ED  C38DF1        			JP		ERRMSG
                      	
                      	;**** MEMORY DUMP ****
  F3F0  13            	STMD:	INC		DE
  F3F1  13            			INC		DE
  F3F2  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればSADRSにセットして続行
  F3F5  DA30F1        			JP		C,STSV1
  F3F8  220411        			LD		(SADRS),HL      ;SARDS保存
                      	
  F3FB  119AF6        	STMD6:	LD		DE,MSG_AD1 ;DUMP TITLE表示
  F3FE  CD1500        			CALL	MSGPR
  F401  CD0600        			CALL	LETLN
  F404  0E10          			LD		C,10H      ;16行(128Byte)を表示
  F406  2A0411        	STMD7:	LD		HL,(SADRS) ;アドレス表示
  F409  CDBA03        			CALL	PRTWRD
  F40C  CD0C00        			CALL	PRNTS
                      	
                      			
  F40F  0608          			LD		B,08H      ;一行(8Byte)のデータを16進数表示
  F411  7E            	STMD0:	LD		A,(HL)
  F412  CDC303        			CALL	PRTBYT
  F415  CD0C00        			CALL	PRNTS
  F418  CD1B00        			CALL	GETKEY
  F41B  FE64          			CP		64H
  F41D  2853          			JR		Z,STMD4
  F41F  23            			INC		HL
  F420  05            			DEC		B
  F421  20EE          			JR		NZ,STMD0
                      	
  F423  2A0411        			LD		HL,(SADRS)
  F426  0608          			LD		B,08H      ;一行(8Byte)のデータをキャラクタ表示
  F428  7E            	STMD2:	LD		A,(HL)
  F429  FE10          			CP		10H        ;MZ-700での文字化けに対処
  F42B  3002          			JR		NC,STMD8
  F42D  3E20          			LD		A,20H
  F42F  CDB90B        	STMD8:	CALL	ADCN
  F432  CDB50D        			CALL	DISPCH
  F435  CD1B00        			CALL	GETKEY
  F438  FE64          			CP		64H        ;表示途中でもSHIFT+BREAKで打ち切り
  F43A  2836          			JR		Z,STMD4
  F43C  23            			INC		HL
  F43D  05            			DEC		B
  F43E  20E8          			JR		NZ,STMD2
                      	
  F440  220411        			LD		(SADRS),HL
  F443  CD0600        			CALL	LETLN
                      	
  F446  0D            			DEC		C
  F447  20BD          			JR		NZ,STMD7
                      			
  F449  11C0F6        			LD		DE,MSG_AD2        ;入力待ちメッセージ表示
  F44C  CD1500        			CALL	MSGPR
  F44F  CD0600        			CALL	LETLN
  F452  CD0600        			CALL	LETLN
  F455  CD1B00        	STMD3:	CALL	GETKEY            ;1文字入力待ち
  F458  FE00          			CP		00H
  F45A  28F9          			JR		Z,STMD3
  F45C  FE64          			CP		64H               ;SHIFT+BREAKで打ち切り
  F45E  2812          			JR		Z,STMD4
  F460  FE42          			CP		42H
  F462  200B          			JR		NZ,STMD5
  F464  2A0411        			LD		HL,(SADRS)
  F467  110001        			LD		DE,0100H
  F46A  ED52          			SBC		HL,DE
  F46C  220411        			LD		(SADRS),HL
  F46F  C3FBF3        	STMD5:	JP		STMD6
  F472  C393F1        	STMD4:	JP		MON
                      	
                      	;**** MEMORY WRITE ****
  F475  13            	STMW:	INC		DE
  F476  13            			INC		DE
  F477  CD1004        			CALL	HLHEX       ;1文字空けて4桁の16進数であればHLにセットして続行
  F47A  DA30F1        			JP		C,STSV1
                      	
  F47D  13            			INC		DE
  F47E  13            			INC		DE
  F47F  13            			INC		DE
  F480  13            			INC		DE
  F481  1A            	STSP1:	LD		A,(DE)
  F482  FE0D          			CP		0DH
  F484  2839          			JR		Z,STMW9     ;アドレスのみなら終了
  F486  FE20          			CP		20H
  F488  2003          			JR		NZ,STMW1
  F48A  13            			INC		DE          ;空白は飛ばす
  F48B  18F4          			JR		STSP1
                      	
  F48D                	STMW1:
  F48D  CD1F04        			CALL	TWOHEX
  F490  380E          			JR		C,STMW8
  F492  77            			LD		(HL),A      ;2桁の16進数があれば(HL)に書き込み
  F493  23            			INC		HL
                      	
  F494  1A            	STSP2:	LD		A,(DE)
  F495  FE0D          			CP		0DH         ;一行終了
  F497  2807          			JR		Z,STMW8
  F499  FE20          			CP		20H
  F49B  20F0          			JR		NZ,STMW1
  F49D  13            			INC		DE          ;空白は飛ばす
  F49E  18F4          			JR		STSP2
                      	
  F4A0                	STMW8:	
  F4A0  11EAF6        			LD		DE,MSG_FDW  ;行頭に'*FDW '
  F4A3  CD1500        			CALL	MSGPR
  F4A6  CDBA03        			CALL	PRTWRD      ;アドレス表示
  F4A9  CD0C00        			CALL	PRNTS
  F4AC  11A311        			LD		DE,LBUF     ;一行入力
  F4AF  CD0300        			CALL	GETL
  F4B2  11A311        			LD		DE,LBUF
  F4B5  1A            			LD		A,(DE)
  F4B6  FE1B          			CP		1BH
  F4B8  2805          			JR		Z,STMW9     ;SHIFT+BREAKで破棄、終了
  F4BA  11A611        			LD		DE,LBUF+3
  F4BD  18B6          			JR		STMW
  F4BF  C393F1        	STMW9:	JP		MON
                      	
                      	;**** 1BYTE受信 ****
                      	;受信DATAをAレジスタにセットしてリターン
  F4C2                	RCVBYTE:
  F4C2  CDF7F4        			CALL	F1CHK      ;PORTC BIT7が1になるまでLOOP
  F4C5  DBD9          			IN		A,(0D9h)   ;PORTB -> A
  F4C7  F5            			PUSH 	AF
  F4C8  3E05          			LD		A,05H
  F4CA  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 1
  F4CC  CDFEF4        			CALL	F2CHK      ;PORTC BIT7が0になるまでLOOP
  F4CF  3E04          			LD		A,04H
  F4D1  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 0
  F4D3  F1            			POP 	AF
  F4D4  C9            			RET
                      			
                      	;**** 1BYTE送信 ****
                      	;Aレジスタの内容をPORTA下位4BITに4BITずつ送信
  F4D5                	SNDBYTE:
  F4D5  F5            			PUSH	AF
  F4D6  1F            			RRA
  F4D7  1F            			RRA
  F4D8  1F            			RRA
  F4D9  1F            			RRA
  F4DA  E60F          			AND		0FH
  F4DC  CDE6F4        			CALL	SND4BIT
  F4DF  F1            			POP		AF
  F4E0  E60F          			AND		0FH
  F4E2  CDE6F4        			CALL	SND4BIT
  F4E5  C9            			RET
                      	
                      	;**** 4BIT送信 ****
                      	;Aレジスタ下位4ビットを送信する
  F4E6                	SND4BIT:
  F4E6  D3D8          			OUT		(0D8H),A
  F4E8  3E05          			LD		A,05H
  F4EA  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 1
  F4EC  CDF7F4        			CALL	F1CHK      ;PORTC BIT7が1になるまでLOOP
  F4EF  3E04          			LD		A,04H
  F4F1  D3DB          			OUT		(0DBH),A    ;PORTC BIT2 <- 0
  F4F3  CDFEF4        			CALL	F2CHK
  F4F6  C9            			RET
                      			
                      	;**** BUSYをCHECK(1) ****
                      	; 82H BIT7が1になるまでLOP
  F4F7  DBDA          	F1CHK:	IN		A,(0DAH)
  F4F9  E680          			AND		80H        ;PORTC BIT7 = 1?
  F4FB  28FA          			JR		Z,F1CHK
  F4FD  C9            			RET
                      	
                      	;**** BUSYをCHECK(0) ****
                      	; 82H BIT7が0になるまでLOOP
  F4FE  DBDA          	F2CHK:	IN		A,(0DAH)
  F500  E680          			AND		80H        ;PORTC BIT7 = 0?
  F502  20FA          			JR		NZ,F2CHK
  F504  C9            			RET
                      	
                      	;****** FILE NAME 取得 (IN:DE コマンド文字の次の文字 OUT:HL ファイルネームの先頭)*********
  F505  F5            	STFN:	PUSH	AF
  F506  13            	STFN1:	INC		DE         ;ファイルネームまでスペース読み飛ばし
  F507  1A            			LD		A,(DE)
  F508  FE20          			CP		20H
  F50A  28FA          			JR		Z,STFN1
  F50C  FE30          			CP		30H        ;「0」以上の文字でなければエラーとする
  F50E  DA35F1        			JP		C,STSV2
  F511  EB            			EX		DE,HL
  F512  F1            			POP		AF
  F513  C9            			RET
                      	
                      	;**** コマンド送信 (IN:A コマンドコード)****
  F514  CDD5F4        	STCD:	CALL	SNDBYTE    ;Aレジスタのコマンドコードを送信
  F517  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F51A  C9            			RET
                      	
                      	;**** ファイルネーム送信(IN:HL ファイルネームの先頭) ******
  F51B  0620          	STFS:	LD		B,20H
  F51D  7E            	STFS1:	LD		A,(HL)     ;FNAME送信
  F51E  CDD5F4        			CALL	SNDBYTE
  F521  23            			INC		HL
  F522  05            			DEC		B
  F523  20F8          			JR		NZ,STFS1
  F525  3E0D          			LD		A,0DH
  F527  CDD5F4        			CALL	SNDBYTE
  F52A  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F52D  C9            			RET
                      	
                      	;**** コマンド、ファイル名送信 (IN:A コマンドコード HL:ファイルネームの先頭)****
  F52E  CD05F5        	STCMD:	CALL	STFN       ;ファイルネーム取得
  F531  E5            			PUSH	HL
  F532  CD14F5        			CALL	STCD       ;コマンドコード送信
  F535  E1            			POP		HL
  F536  A7            			AND		A          ;00以外ならERROR
  F537  C259F1        			JP		NZ,SVER0
  F53A  CD1BF5        			CALL	STFS       ;ファイルネーム送信
  F53D  A7            			AND		A          ;00以外ならERROR
  F53E  C259F1        			JP		NZ,SVER0
  F541  C9            			RET
                      	
                      	;******** MESSAGE DATA ********************
  F542                	MSG_LD:
  F542  16            			DB		16H
  F543  4C4F4144494E47			DB		'LOADING '
  F54B  0D            			DB		0DH
                      	
  F54C                	WRMSG:
  F54C  57524954494E47			DB		'WRITING '
  F554  0D            			DB		0DH
                      	
  F555                	MSG_SV:
  F555  53415645204649			DB		'SAVE FINISHED!'
  F563  0D            			DB		0DH
                      			
  F564                	MSG_AS:
  F564  41535441525420			DB		'ASTART FINISHED!'
  F574  0D            			DB		0DH
                      			
  F575                	MSG_AD:
  F575  41444452455353			DB		'ADDRESS FAILED!'
  F584  0D            			DB		0DH
                      			
  F585                	MSG_FNAME:
  F585  46494C45204E41			DB		'FILE NAME FAILED!'
  F596  0D            			DB		0DH
                      			
  F597                	MSG_CMD:
  F597  434F4D4D414E44			DB		'COMMAND FAILED!'
  F5A6  0D            			DB		0DH
                      			
  F5A7                	MSG_F0:
  F5A7  53442D43415244			DB		'SD-CARD INITIALIZE ERROR'
  F5BF  0D            			DB		0DH
                      			
  F5C0                	MSG_F1:
  F5C0  4E4F542046494E			DB		'NOT FIND FILE'
  F5CD  0D            			DB		0DH
                      			
  F5CE                	MSG_F2:
  F5CE  4E4F54204F424A			DB		'NOT OBJECT FILE'
  F5DD  0D            			DB		0DH
                      			
  F5DE                	MSG_F3:
  F5DE  46494C45204558			DB		'FILE EXIST'
  F5E8  0D            			DB		0DH
                      			
  F5E9                	MSG_KEY1:
  F5E9  48495420414E59			DB		'HIT ANY KEY(BREAK:'
  F5FB  0D            			DB		0DH
  F5FC                	MSG_KEY2:
  F5FC  204F5220534849			DB		' OR SHIFT+BREAK)'
  F60C  0D            			DB		0DH
                      			
  F60D                	MSG_DELQ:
  F60D  46494C45204445			DB		'FILE DELETE?(Y:OK ELSE:CANSEL)'
  F62B  0D            			DB		0DH
                      			
  F62C                	MSG_DELY:
  F62C  44454C45544520			DB		'DELETE OK'
  F635  0D            			DB		0DH
                      			
  F636                	MSG_DELN:
  F636  44454C45544520			DB		'DELETE CANSEL'
  F643  0D            			DB		0DH
                      			
  F644                	MSG_REN:
  F644  4E4557204E414D			DB		'NEW NAME:                            '
  F669  0D            			DB		0DH
                      			
  F66A                	MSG_DNAME:
  F66A  444F532046494C			DB		'DOS FILE:                            '
  F68F  0D            			DB		0DH
                      			
  F690                	MSG_RENY:
  F690  52454E414D4520			DB		'RENAME OK'
  F699  0D            			DB		0DH
                      			
  F69A                	MSG_AD1:
  F69A  41445253202B30			DB		'ADRS +0 +1 +2 +3 +4 +5 +6 +7 01234567'
  F6BF  0D            			DB		0DH
                      			
  F6C0                	MSG_AD2:
  F6C0  4E4558543A414E			DB		'NEXT:ANY BACK:B BREAK:SHIFT+BREAK'
  F6E1  0D            			DB		0DH
                      			
  F6E2                	MSG_CPY:
  F6E2  434F5059204F4B			DB		'COPY OK'
  F6E9  0D            			DB		0DH
                      			
  F6EA                	MSG_FDW:
  F6EA  2A46445720    			DB		'*FDW '
  F6EF  0D            			DB		0DH
                      	
  F6F0                	MSG99:
  F6F0  204552524F52  			DB		' ERROR'
  F6F6  0D            			DB		0DH
                      			
  F6F7                	DEFNAME:
  F6F7  30303030      			DB		'0000'
  F6FB  0D            			DB		0DH
  F6FC                	NEND:
                      	
  F6FC                	DEFDIR:
  F6FC  2A46442020    			DB		'*FD  '
  F701                	DEND:
                      	
                      	;*********************** 0436H MONITOR ライト インフォメーション代替処理 ************
  F701                	MSHED:
  F701  D5            			PUSH	DE
  F702  C5            			PUSH	BC
  F703  E5            			PUSH	HL
  F704  3E91          			LD		A,91H      ;HEADER SAVEコマンド91H
  F706  CD0FF8        			CALL	MCMD       ;コマンドコード送信
  F709  A7            			AND		A          ;00以外ならERROR
  F70A  C221F8        			JP		NZ,MERR
                      	
  F70D  114CF5        			LD		DE,WRMSG   ;'WRITING '
  F710  CD1500        			CALL	MSGPR        ;メッセージ表示
  F713  11F110        			LD		DE,FNAME     ;ファイルネーム
  F716  CD1500        			CALL	MSGPR       ;メッセージ表示
                      	
  F719  21F010        			LD		HL,IBUFE
  F71C  0680          			LD		B,80H
  F71E  7E            	MSH1:	LD		A,(HL)     ;インフォメーション ブロック送信
  F71F  CDD5F4        			CALL	SNDBYTE
  F722  23            			INC		HL
  F723  05            			DEC		B
  F724  20F8          			JR		NZ,MSH1
                      	
  F726  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F729  A7            			AND		A          ;00以外ならERROR
  F72A  C221F8        			JP		NZ,MERR
                      	
  F72D  C31BF8        			JP		MRET       ;正常RETURN
                      	
                      	;******************** 0475H MONITOR ライト データ代替処理 **********************
  F730                	MSDAT:
  F730  D5            			PUSH	DE
  F731  C5            			PUSH	BC
  F732  E5            			PUSH	HL
  F733  3E92          			LD		A,92H      ;DATA SAVEコマンド92H
  F735  CD0FF8        			CALL	MCMD       ;コマンドコード送信
  F738  A7            			AND		A          ;00以外ならERROR
  F739  C221F8        			JP		NZ,MERR
                      	
  F73C  210211        			LD		HL,FSIZE   ;FSIZE送信
  F73F  7E            			LD		A,(HL)
  F740  CDD5F4        			CALL	SNDBYTE
  F743  23            			INC		HL
  F744  7E            			LD		A,(HL)
  F745  CDD5F4        			CALL	SNDBYTE
                      	
  F748  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F74B  A7            			AND		A          ;00以外ならERROR
  F74C  C221F8        			JP		NZ,MERR
                      	
  F74F  ED5B0211      			LD		DE,(FSIZE)
  F753  2A0411        			LD		HL,(SADRS)
  F756  7E            	MSD1:	LD		A,(HL)
  F757  CDD5F4        			CALL	SNDBYTE      ;SADRSからFSIZE Byteを送信。分割セーブの場合、直前に0436HでOPENされたファイルを対象として256バイトずつ0475HがCALLされる。
  F75A  1B            			DEC		DE
  F75B  7A            			LD		A,D
  F75C  B3            			OR		E
  F75D  23            			INC		HL
  F75E  20F6          			JR		NZ,MSD1
                      			
  F760  C31BF8        			JP		MRET       ;正常RETURN
                      	
                      	;************************** 04D8H MONITOR リード インフォメーション代替処理 *****************
  F763                	MLHED:
  F763  D5            			PUSH	DE
  F764  C5            			PUSH	BC
  F765  E5            			PUSH	HL
  F766  3E93          			LD		A,93H      ;HEADER LOADコマンド93H
  F768  CD0FF8        			CALL	MCMD       ;コマンドコード送信
  F76B  A7            			AND		A          ;00以外ならERROR
  F76C  C221F8        			JP		NZ,MERR
                      	
  F76F  0608          			LD		B,08H
  F771  11A311        			LD		DE,LBUF
  F774  3E0D          			LD		A,0DH
  F776  12            	MLH0:	LD		(DE),A
  F777  13            			INC		DE
  F778  05            			DEC		B
  F779  20FB          			JR		NZ,MLH0
                      	
  F77B  3E03          			LD		A,03H          ;一行分をクリアするため3文字削除、37文字出力
  F77D  327111        			LD		(DSPX),A
  F780  3EC7          			LD		A,0C7H
  F782  CDDC0D        			CALL	DPCT
  F785  CDDC0D        			CALL	DPCT
  F788  CDDC0D        			CALL	DPCT
  F78B  116AF6        			LD		DE,MSG_DNAME   ;'DOS FILE:'
  F78E  CD1500        			CALL	MSGPR
  F791  3E09          			LD		A,09H          ;カーソルを9文字目に戻す
  F793  327111        			LD		(DSPX),A
                      	
  F796  11AE11        			LD		DE,MBUF    ;ファイルネームを指示するための苦肉の策。LOADコマンドとしてはファイルネームなしとして改行したのちに行バッファの位置をずらしてDOSファイルネームを入力する。
  F799  CD0300        			CALL	GETL
                      			
  F79C  11B711        			LD		DE,MBUF+9
                      	;		LD		DE,MBUF
  F79F                	MLH1:
  F79F  1A            			LD		A,(DE)
  F7A0  FE20          			CP		20H                 ;行頭のスペースをファイルネームまで読み飛ばし
  F7A2  2003          			JR		NZ,MLH2
  F7A4  13            			INC		DE
  F7A5  18F8          			JR		MLH1
                      	
  F7A7  0620          	MLH2:	LD		B,20H
  F7A9  1A            	MLH4:	LD		A,(DE)     ;FNAME送信
  F7AA  CDD5F4        			CALL	SNDBYTE
  F7AD  13            			INC		DE
  F7AE  05            			DEC		B
  F7AF  20F8          			JR		NZ,MLH4
  F7B1  3E0D          			LD		A,0DH
  F7B3  CDD5F4        			CALL	SNDBYTE
                      			
  F7B6  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7B9  A7            			AND		A          ;00以外ならERROR
  F7BA  C221F8        			JP		NZ,MERR
                      	
  F7BD  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7C0  A7            			AND		A          ;00以外ならERROR
  F7C1  C221F8        			JP		NZ,MERR
                      	
  F7C4  21F010        			LD		HL,IBUFE
  F7C7  0680          			LD		B,80H
  F7C9  CDC2F4        	MLH5:	CALL	RCVBYTE    ;読みだされたインフォメーションブロックを受信
  F7CC  77            			LD		(HL),A
  F7CD  23            			INC		HL
  F7CE  05            			DEC		B
  F7CF  20F8          			JR		NZ,MLH5
                      	
  F7D1  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7D4  A7            			AND		A          ;00以外ならERROR
  F7D5  C221F8        			JP		NZ,MERR
                      	
  F7D8  1841          			JR		MRET       ;正常RETURN
                      	
                      	;**************************** 04F8H MONITOR リード データ代替処理 ********************
  F7DA                	MLDAT:
  F7DA  D5            			PUSH	DE
  F7DB  C5            			PUSH	BC
  F7DC  E5            			PUSH	HL
  F7DD  3E94          			LD		A,94H      ;DATA LOADコマンド94H
  F7DF  CD0FF8        			CALL	MCMD       ;コマンドコード送信
  F7E2  A7            			AND		A          ;00以外ならERROR
  F7E3  C221F8        			JP		NZ,MERR
                      	
  F7E6  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7E9  A7            			AND		A          ;00以外ならERROR
  F7EA  C221F8        			JP		NZ,MERR
                      	
  F7ED  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F7F0  A7            			AND		A          ;00以外ならERROR
  F7F1  C221F8        			JP		NZ,MERR
                      	
  F7F4  110211        			LD		DE,FSIZE   ;FSIZE送信
  F7F7  1A            			LD		A,(DE)
  F7F8  CDD5F4        			CALL	SNDBYTE
  F7FB  13            			INC		DE
  F7FC  1A            			LD		A,(DE)
  F7FD  CDD5F4        			CALL	SNDBYTE
  F800  CDDBF0        			CALL	DBRCV      ;FSIZE分のデータを受信し、SADRSから格納。分割ロードの場合、直前に0436HでOPENされたファイルを対象として256バイトずつSADRSが加算されて04F8HがCALLされる。
                      	
  F803  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F806  A7            			AND		A          ;00以外ならERROR
  F807  C221F8        			JP		NZ,MERR
                      	
  F80A  180F          			JR		MRET       ;正常RETURN
                      	
                      	;************************** 0588H VRFY CMT ベリファイ代替処理 *******************
  F80C  AF            	MVRFY:	XOR		A          ;正常終了フラグ
  F80D  FB            			EI
  F80E  C9            			RET
                      	
                      	;******* 代替処理用コマンドコード送信 (IN:A コマンドコード) **********
  F80F  F5            	MCMD:	PUSH	AF
  F810  CD78F0        			CALL	INIT
  F813  F1            			POP		AF
  F814  CDD5F4        			CALL	SNDBYTE    ;コマンドコード送信
  F817  CDC2F4        			CALL	RCVBYTE    ;状態取得(00H=OK)
  F81A  C9            			RET
                      	
                      	;****** 代替処理用正常RETURN処理 **********
  F81B  E1            	MRET:	POP		HL
  F81C  C1            			POP		BC
  F81D  D1            			POP		DE
  F81E  AF            			XOR		A          ;正常終了フラグ
  F81F  FB            			EI
  F820  C9            			RET
                      	
                      	;******* 代替処理用ERROR処理 **************
  F821                	MERR:
  F821  FEF0          			CP		0F0H
  F823  2005          			JR		NZ,MERR2
  F825  11A7F5        			LD		DE,MSG_F0
  F828  1818          			JR		MERRMSG
                      			
  F82A  FEF2          	MERR2:	CP		0F2H
  F82C  2005          			JR		NZ,MERR3
  F82E  11CEF5        			LD		DE,MSG_F2
  F831  180F          			JR		MERRMSG
                      			
  F833  FEF1          	MERR3:	CP		0F1H
  F835  2005          			JR		NZ,MERR99
  F837  11C0F5        			LD		DE,MSG_F1
  F83A  1806          			JR		MERRMSG
                      			
  F83C  CDC303        	MERR99:	CALL	PRTBYT
  F83F  11F0F6        			LD		DE,MSG99
                      			
  F842                	MERRMSG:
  F842  CD1500        			CALL	MSGPR
  F845  CD0600        			CALL	LETLN
  F848  E1            			POP		HL
  F849  C1            			POP		BC
  F84A  D1            			POP		DE
  F84B  3E02          			LD		A,02H
  F84D  37            			SCF
  F84E  FB            			EI
  F84F  C9            			RET
                      	
  F850                			END
